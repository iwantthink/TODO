# 配置多项目构建

[Configuring Multi-Project Builds](https://docs.gradle.org/current/userguide/multi_project_builds.html)

# 1. 简介

对多项目构建的强大支持是Gradle独特的卖点之一

Gradle中的多项目构建由一个根项目和一个或多个子项目组成，这些子项目也可能有子项目

# 2. 跨项目配置

虽然每个子项目都可以在完全独立于其他子项目的情况下配置自己，但是子项目具有相同的特性是很常见的。针对这种情况，通常更可取的做法是在项目之间共享配置，这样相同的配置会影响多个子项目


Gradle的核心是一个通用的构建工具，因此项目不必是Java项目


## 2.1 配置和执行

[`Build phases`](https://docs.gradle.org/current/userguide/build_lifecycle.html#sec:build_phases)描述了每个构建的阶段，在这里，主要关注多项目构建的配置和执行阶段

- `Configuration`阶段意味着执行项目的`build.gradle`文件，这意味着下载所有使用`apply`语句或`plugins`块声明的插件。默认情况下，所有对项目的配置都发生在任何任务执行之前。这意味着在多项目中执行单个项目的单个任务时，会先去配置多项目构建的所有项目

	每个项目都需要进行配置的原因是为了支持去访问或修改Gradle项目模型的任何部分
	
## 2.2 按需配置

因为每个项目都在执行阶段之前配置，因此可以实现配置注入功能和对完整项目模型的访问 然而，在非常大的多项目构建中，这种方法可能不是最有效的。当Gradle构建具有数百个子项目的层次结构，其配置阶段将非常耗时，因此`scalability`是Gradle的重要要求

- 从版本1.4开始，引入了一种新的孵化模式 按需配置(`configuration on demand`)。

按需配置模式尝试只配置与请求任务相关的项目，即只执行参与构建的项目中的`build.gradle`文件。这样，可以减少大型多项目构建的配置时间。从长远来看，这种模式将成为默认模式，可能是Gradle构建执行的唯一模式

- 按需配置特性正在孵化中，因此不能保证每个构建都能正确工作。该特性对于已解耦项目的多项目构建应该非常有效

在按需配置模式中，项目按照如下规则进行配置:

- **根项目总是会被配置**。这种行为支持了通用配置(`allprojects`或`subprojects`脚本块)

- **执行构建的目录中的项目也会被配置**，但仅在没有指定任何task的去执行Gradle命令时。 这样，在按需配置项目时，默认任务才会正常运行

- **标准的项目依赖关系会导致配置相关的项目**。即如果项目A对项目B具有编译依赖关系，那么构建A将导致对两个项目的配置

- **通过任务路径声明的任务依赖项会导致配置相关的项目**。举例`someTask.dependsOn(":someOtherProject:someOtherTask")`

- **通过命令行（或Tooling API）执行指定任务路径下的任务会导致配置相关项目**。 例如，构建`projectA：projectB：someTask`会导致projectB的配置


如果想要设置所有的构建都进行按需配置，查看[Gradle properties](https://docs.gradle.org/current/userguide/build_environment.html#sec:gradle_configuration_properties)
	
如果仅仅针对个别构建进行按需配置，查看[command-line performance-oriented options](https://docs.gradle.org/current/userguide/command_line_interface.html#sec:command_line_performance)


## 2.3 定义跨项目的通用行为

Gradle主要有俩种方式去定义跨项目的通用行为:

1. **`Project API`提供了一个属性`allprojects`和`allprojects()`方法。前者返回一个列表，其中包含了当前项目及其下的所有子项目 。 后者可以接受一个闭包作为参数，项目对象将传给闭包作为其代理**

2. **共享配置的另外一种方法就是使用公共外部脚本**

下面介绍一个多项目构建，其中`water`是其根项目，`bluewhale`是其子项目

这个示例可以在发行版的`samples/userguide/multiproject/firstExample/water`目录下找到

### 2.3.1 多项目树 - water&bluewhale

	// 项目布局
	.
	├── bluewhale/
	├── build.gradle
	└── settings.gradle

	// settings.gradle
	rootProject.name = 'water'
	include 'bluewhale'

- `bluewhale`项目的构建脚本在哪里？

	在Gradle中，构建脚本是可选的。对于单项目构建来说，项目如果没有构建脚本是没有意义的。但是对于多项目构建来说，情况就不同了
	
	
### 2.3.2 构建water项目

	// build.gradle
	Closure cl = { task -> println "I'm $task.project.name" }
	task('hello').doLast(cl)
	project(':bluewhale') {
	    task('hello').doLast(cl)
	}	
		
	> gradle -q hello
	I'm water
	I'm bluewhale
	
- Gradle允许从任何构建脚本中去访问多项目构建的任何项目。Project API提供了一个名为`project()`的方法，该方法接受一个路径作为参数，并返回该路径的项目对象

	**Gradle将这种从任意构建脚本中对任意项目进行配置的行为成为 跨项目构建**,这是Gradle借助配置注入(`configuration injection`)实现的
	
	
- 这种显示为各个项目添加任务的行为并不方便，可以通过其他的方式更加方便的实现

### 2.3.3 添加一个krill子项目

	// 项目布局
	.
	├── bluewhale/
	├── build.gradle
	├── krill/
	└── settings.gradle


	// settings.gradle
	rootProject.name = 'water'
	
	include 'bluewhale', 'krill'
	
下面重写water项目的构建脚本，将其对所有项目添加`hello`任务的逻辑进行简化

	//build.gradle
	allprojects {
	    task hello {
	        doLast { task ->
	            println "I'm $task.project.name"
	        }
	    }
	}	
	
	
	> gradle -q hello
	I'm water
	I'm bluewhale
	I'm krill
	

-  如果将闭包作为参数调用`allprojects()`方法，则闭包的语句将委托给与`allprojects`关联的项目。 此外，还可以通过`allprojects.each`（在Groovy中）进行迭代
	
	
**其他构建系统使用继承作为定义通用行为的主要手段。 Gradle同样为项目提供继承，稍后您将看到。但Gradle默认使用配置注入去定义通用行为**
	


# 3. 子项目配置

Project PI还提供了一个仅用于访问子项目的属性

## 3.1 定义所有子项目的通用行为

	// build.gradle
	allprojects {
	    task hello {
	        doLast { task ->
	            println "I'm $task.project.name"
	        }
	    }
	}
	subprojects {
	    hello {
	        doLast {
	            println "- I depend on water"
	        }
	    }
	}
	
	> gradle -q hello
	I'm water
	I'm bluewhale
	- I depend on water
	I'm krill
	- I depend on water
	
- 上面例子中，有两个代码片段引用了“hello”任务

	1. 第一个代码片段中，使用“task”关键字（在Groovy中）构造了任务，并提供了基本配置
	
	2. 第二个代码片段中，不需要使用“task”关键字，因为这里只是进一步配置了现有的“hello”任务

	任务只能在项目中被构造一次，但可以添加任意数量代码块为其提供配置
	
	
## 3.2 添加特定的行为

Gradle允许在公共行为上添加特定的行为。通常情况下，会将项目的特定行为放在项目的构建脚本中，这个构建脚本会应用该特定行为。但是Gradle提供了其他方式去实现 

### 3.2.1 为特定项目添加特定行为

	// build.gradle
	allprojects {
	    task hello {
	        doLast { task ->
	            println "I'm $task.project.name"
	        }
	    }
	}
	subprojects {
	    hello {
	        doLast {
	            println "- I depend on water"
	        }
	    }
	}
	project(':bluewhale').hello {
	    doLast {
	        println "- I'm the largest animal that has ever lived on this planet."
	    }
	}	
	
	
	> gradle -q hello
	I'm water
	I'm bluewhale
	- I depend on water
	- I'm the largest animal that has ever lived on this planet.
	I'm krill
	- I depend on water

### 3.2.2 为krill项目添加特定行为

**通常更喜欢将项目的特定行为放入对应项目的构建脚本中。这里对krill项目进行重构并添加一些特定于项目的行为**

	// 项目布局
	.
	├── bluewhale
	│   └── build.gradle
	├── build.gradle
	├── krill
	│   └── build.gradle
	└── settings.gradle
	
	
	// settings.gradle
	rootProject.name = 'water'
	include 'bluewhale', 'krill'	
		
	// bluewhale/build.gradle
	hello.doLast {
	  println "- I'm the largest animal that has ever lived on this planet."
	}	
	
	// krill/build.gradle
	hello.doLast {
	  println "- The weight of my species in summer is twice as heavy as all human beings."
	}	
		
	
	// 根项目的build.gradle
	allprojects {
	    task hello {
	        doLast { task ->
	            println "I'm $task.project.name"
	        }
	    }
	}
	subprojects {
	    hello {
	        doLast {
	            println "- I depend on water"
	        }
	    }
	}
	
	
	> gradle -q hello
	I'm water
	I'm bluewhale
	- I depend on water
	- I'm the largest animal that has ever lived on this planet.
	I'm krill
	- I depend on water
	- The weight of my species in summer is twice as heavy as all human beings.
	
	
# 4. 过滤项目

为了展示配置注入的更多功能，添加另一个名为`tropicalFish`的项目，并通过`water`项目的构建脚本添加更多的行为到构建中


## 4.1 通过名称过滤

给特定的项目添加自定义行为(通过项目名称进行过滤)

	// 项目布局
	.
	├── bluewhale/
	│   └── build.gradle
	├── build.gradle
	├── krill/
	│   └── build.gradle
	├── settings.gradle
	└── tropicalFish/

	// settings.gradle
	rootProject.name = 'water'
	include 'bluewhale', 'krill', 'tropicalFish'
	
	// build.gradle
	allprojects {
	    task hello {
	        doLast { task ->
	            println "I'm $task.project.name"
	        }
	    }
	}
	subprojects {
	    hello {
	        doLast {
	            println "- I depend on water"
	        }
	    }
	}
	configure(subprojects.findAll {it.name != 'tropicalFish'}) {
	    hello {
	        doLast {
	            println '- I love to spend time in the arctic waters.'
	        }
	    }
	}	
	
	> gradle -q hello
	I'm water
	I'm bluewhale
	- I depend on water
	- I love to spend time in the arctic waters.
	- I'm the largest animal that has ever lived on this planet.
	I'm krill
	- I depend on water
	- I love to spend time in the arctic waters.
	- The weight of my species in summer is twice as heavy as all human beings.
	I'm tropicalFish
	- I depend on water	

- **`configure()`方法接受列表作为参数，并将配置应用于列表中的项目**

## 4.2 通过属性过滤

除了使用项目名称进行过滤，还可以通过属性进行过滤，筛选出特定项目

	// 项目布局
	.
	├── bluewhale
	│   └── build.gradle
	├── build.gradle
	├── krill
	│   └── build.gradle
	├── settings.gradle
	└── tropicalFish
	    └── build.gradle
	
	// settings.gradle
	rootProject.name = 'water'
	include 'bluewhale', 'krill', 'tropicalFish'
	
	// bluewhale/build.gradle
	ext.arctic = true
	hello.doLast {
	  println "- I'm the largest animal that has ever lived on this planet."
	}
		
	// krill/build.gradle
	ext.arctic = true
	hello.doLast {
	    println "- The weight of my species in summer is twice as heavy as all human beings."
	}
	

	// build.gradle
	allprojects {
	    task hello {
	        doLast { task ->
	            println "I'm $task.project.name"
	        }
	    }
	}
	subprojects {
	    hello {
	        doLast {println "- I depend on water"}
	    }
	
	    afterEvaluate { Project project ->
	        if (project.arctic) {
	            hello.configure {
	                doLast {
	                    println '- I love to spend time in the arctic waters.'
	                }
	            }
	        }
	    }
	}	
		
	// tropicalFish/build.gradle
	ext.arctic = false
	
	
	> gradle -q hello
	I'm water
	I'm bluewhale
	- I depend on water
	- I'm the largest animal that has ever lived on this planet.
	- I love to spend time in the arctic waters.
	I'm krill
	- I depend on water
	- The weight of my species in summer is twice as heavy as all human beings.
	- I love to spend time in the arctic waters.
	I'm tropicalFish
	- I depend on water	
	
- 在`water`项目的构建文件中，使用了`afterEvaluate`通知。这意味着在子项目的构建脚本被评估之后，才会对所传递的闭包进行评估。这是因为`arctic`属性在每个构建脚本中设置，所以必须使用这种方式。


# 5. 多项目构建的执行规则

**当在根项目目录下执行`hello`任务时，所有的项目的`hello`任务都会被执行**

## 5.1 在子项目目录中执行任务

下面将执行目录切换到`bluewhale`项目下，看看会发生什么?

	> gradle -q hello
	I'm bluewhale
	- I depend on water
	- I'm the largest animal that has ever lived on this planet.
	- I love to spend time in the arctic waters.

	
Gradle行为背后的基本规则很简单(**名称匹配机制**)。Gradle从当前目录开始,按照层级结构向下查找名称为hello的任务，并执行它们

- 有一件事需要注意：**Gradle总是评估多项目构建的每个项目，并创建所有现有的任务对象。然后，根据任务名称参数和当前目录，Gradle筛选应该执行的任务。由于跨项目配置特性，每个项目在执行任何任务之前都必须进行评估**

### 5.1.1 评估并执行项目

	// bluewhale/build.gradle
	ext.arctic = true
	hello {
	    doLast {
	        println "- I'm the largest animal that has ever lived on this planet."
	    }
	}
	
	task distanceToIceberg {
	    doLast {
	        println '20 nautical miles'
	    }
	}

	// krill/build.gradle
	ext.arctic = true
	hello {
	    doLast {
	        println "- The weight of my species in summer is twice as heavy as all human beings."
	    }
	}
	
	task distanceToIceberg {
	    doLast {
	        println '5 nautical miles'
	    }
	}

	// 带-q选项的输出
	> gradle -q distanceToIceberg
	20 nautical miles
	5 nautical miles
	
	// 不带-q选项的输出
	> gradle distanceToIceberg
	
	> Task :bluewhale:distanceToIceberg
	20 nautical miles
	
	> Task :krill:distanceToIceberg
	5 nautical miles

- 当前构建在`water`项目目录下执行.Gradle并不关心 `water`和`tropicalFish`哪个项目拥有一个名为`distanceToIceberg`的任务，只要执行层次结构中具有此名称的所有任务就行，只有在没有此名称的任务时才会抛出问题

## 5.2 通过绝对路径执行任务

正如我们所看到的，可以在任何子项目目录中执行构建，然后从当前目录开始执行项目层次结构中所有匹配的任务。但是Gradle也提供了绝对路径来执行任务(参见项目和任务路径)

	> gradle -q :hello :krill:hello hello
	I'm water // 根目录任务的执行结果
	I'm krill // krill目录下的执行结果
	- I depend on water
	- The weight of my species in summer is twice as heavy as all human beings.
	- I love to spend time in the arctic waters.
	I'm tropicalFish //
	- I depend on water


- 当前构建是从`tropicalFish`项目中执行的。 分别执行`water`项目，`krill`项目和`tropicFish`项目下的`hello`任务。 **前两个任务由其绝对路径指定，最后一个任务使用5.1小节中的 名称匹配机制执行**


## 5.3 任务和项目的路径

项目路径具有以下模式:

- **它以一个可选的冒号开始(冒号表示根项目)。根项目是在路径中唯一不通过其名称进行使用的项目。项目路径的其余部分是以冒号为分隔的项目名称序列，在路径中下一个项目是上一个项目的子项目**

	**任务的路径就是它的项目路径加上任务名称**，例如`:bluewhale:hello`。在项目中，可以仅通过名称来定位同处一个项目中的任务(**这就是相对路径**)


# 6. Gradle支持的依赖关系

上面几个小节中的示例很特殊，因为项目没有执行依赖(`Execution Dependencies`)，它们只有配置依赖(`Configuration Dependencies`)

以下部分说明了这两种依赖关系之间的差异

## 6.1 执行依赖(`Execution dependencies`)

这个示例可以在发行版的`samples/userguide/multiproject/dependencies/firstMessages/messages`目录下找到

### 6.1.1 依赖关系和执行顺序(未达到预期效果)

	// 项目布局
	.
	├── build.gradle
	├── consumer
	│   └── build.gradle
	├── producer
	│   └── build.gradle
	└── settings.gradle
	
	
	// build.gradle
	ext.producerMessage = null	

	// settings.gradle
	include 'consumer', 'producer'
	
	// consumer/build.gradle
	task action {
	    doLast {
	        println("Consuming message: ${rootProject.producerMessage}")
	    }
	}	
	
	// producer/build.gradle
	task action {
	    doLast {
	        println "Producing message:"
	        rootProject.producerMessage = 'Watch the order of execution.'
	    }
	}
	
	> gradle -q action
	Consuming message: null
	Producing message:

- 这个结果并没有达到预期的效果。如果没有定义其他内容，Gradle将按字母数字顺序执行任务。因此，Gradle将按照先执行`:consumer:action`再执行`:producer:action`的顺序去执行任务。 让我们尝试用hack解决这个问题，并将`producer`项目重命名为`aProducer`

### 6.1.2 依赖关系和执行顺序(达到预期效果)

	// 项目布局
	.
	├── aProducer
	│   └── build.gradle
	├── build.gradle
	├── consumer
	│   └── build.gradle
	└── settings.gradle

	// build.gradle
	ext.producerMessage = null

	// settings.gradle
	include 'consumer', 'aProducer'
	
	// consumer/build.gradle
	task action {
	    doLast {
	        println("Consuming message: ${rootProject.producerMessage}")
	    }
	}	

	// aProducer/build.gradle
	task action {
	    doLast {
	        println "Producing message:"
	        rootProject.producerMessage = 'Watch the order of execution.'
	    }
	}	
	
	> gradle -q action
	Producing message:
	Consuming message: Watch the order of execution.	
	
	
如果现在切换到`consumer`目录并执行构建，那么此hack将会不起作用

	// 在consumer目录下执行构建
	> gradle -q action
	Consuming message: null

- 如果从根项目(`message`)中执行构建，Gradle将执行它们，因为它们具有相同的名称，并且它们位于层次结构中。 然后再`consumer`目录下执行构建，只有一个`action`任务在层次结构中，因此它是唯一执行的任务,此时两个`action`任务是无关的


## 6.2 依赖关系-真实的例子-跨项目配置

Gradle的多项目特性是由实际的用例驱动的

举个真实的例子,使用一个构建脚本去进行跨项目配置。两个web应用程序项目和一个父项目组成的多项目构建，该多项目构建创建两个web应用程序的发行版

	// 项目布局
	.
	├── build.gradle
	├── date
	│   └── src
	│       └── main
	│           ├── java
	│           │   └── org
	│           │       └── gradle
	│           │           └── sample
	│           │               └── DateServlet.java
	│           └── webapp
	│               └── web.xml
	├── hello
	│   └── src
	│       └── main
	│           ├── java
	│           │   └── org
	│           │       └── gradle
	│           │           └── sample
	│           │               └── HelloServlet.java
	│           └── webapp
	│               └── web.xml
	└── settings.gradle

	// settings.gradle
	rootProject.name = 'webDist'
	include 'date', 'hello'


	// build.gradle
	allprojects {
	    apply plugin: 'java'
	    group = 'org.gradle.sample'
	    version = '1.0'
	}
	
	subprojects {
	    apply plugin: 'war'
	    repositories {
	        mavenCentral()
	    }
	    dependencies {
	        compile "javax.servlet:servlet-api:2.5"
	    }
	}
	
	task explodedDist(type: Copy) {
	    into "$buildDir/explodedDist"
	    subprojects {
	        from tasks.withType(War)
	    }
	}
	
- 这里有一组有趣的依赖关系。显然，date和hello项目对webDist具有配置依赖关系，因为web应用项目的所有构建逻辑都由`webDist`项目注入

	执行依赖关系则相反，因为`webDist`在执行时依赖于`date`和`hello`项目生成的构件,甚至还有第三种依赖，所以`webDist`依赖于`date`和`hello`项目，即它需要知道这俩个项目输出构件的路径`archivePath`,但是仅在执行时需要这些信息，因此没有循环依赖


## 6.3  项目库依赖

如果一个项目需要在其编译路径中依赖另一个项目生成的jar，而且不仅仅是jar本身，还包括此jar所传递的依赖，该怎么办？ 

显然，这是Java多项目构建的一个非常常见的用例。 正如`Project dependencies`中所提到的，Gradle为此提供了项目lib依赖项

下面的例子可以在Gradle发行版的`samples/userguide/multiproject/dependencies/java`目录下找到

	// 项目布局
	.
	├── api
	│   └── src
	│       ├── main
	│       │   └── java
	│       │       └── org
	│       │           └── gradle
	│       │               └── sample
	│       │                   ├── api
	│       │                   │   └── Person.java
	│       │                   └── apiImpl
	│       │                       └── PersonImpl.java
	│       └── test
	│           └── java
	│               └── org
	│                   └── gradle
	│                       └── PersonTest.java
	├── build.gradle
	├── services
	│   └── personService
	│       └── src
	│           ├── main
	│           │   └── java
	│           │       └── org
	│           │           └── gradle
	│           │               └── sample
	│           │                   └── services
	│           │                       └── PersonService.java
	│           └── test
	│               └── java
	│                   └── org
	│                       └── gradle
	│                           └── sample
	│                               └── services
	│                                   └── PersonServiceTest.java
	├── settings.gradle
	└── shared
	    └── src
	        └── main
	            └── java
	                └── org
	                    └── gradle
	                        └── sample
	                            └── shared
	                                └── Helper.java
	                              
	// settings.gradle
	include 'api', 'shared', 'services:personService'
    
    // build.gradle
    subprojects {
	    apply plugin: 'java'
	    group = 'org.gradle.sample'
	    version = '1.0'
	    repositories {
	        mavenCentral()
	    }
	    dependencies {
	        testImplementation "junit:junit:4.12"
	    }
	}
	
	project(':api') {
	    dependencies {
	        implementation project(':shared')
	    }
	}
	
	project(':services:personService') {
	    dependencies {
	        implementation project(':shared'), project(':api')
	    }
	}


- 上面包含三个项目，分别是`shared`,`services`,`api`. `personService`项目对其他俩个项目拥有库依赖。`api`项目对`shared`项目拥有库依赖

	`services`也是一个项目，但是它仅被用作一个容器。它没有构建脚本，也没有得到其他构建脚本注入的任何东西

	这里使用`:`分隔符来定义项目路径，查看[`Settings:include(java.lang.String[])`](https://docs.gradle.org/current/dsl/org.gradle.api.initialization.Settings.html#org.gradle.api.initialization.Settings:include(java.lang.String[]))可以获取更多的关于路径的信息
	

- 所有的构建逻辑都在根项目的构建脚本中。**库依赖是执行依赖的一种特殊形式**,它将使得其他项目先被构建，并将带有其他项目类的jar添加到类路径中。此外还将其他项目需要的依赖添加到类路径中

	如果进入`api`目录并执行`gradle compile`,首先`shared`项目会被构建，然后`api`项目会被构建

	项目依赖支持部分多项目构建


	

	
## 6.4 对依赖关系的细粒度控制

	// build.gradle
	subprojects {
	    apply plugin: 'java'
	    group = 'org.gradle.sample'
	    version = '1.0'
	}
	
	project(':api') {
	    configurations {
	        spi
	    }
	    dependencies {
	        compile project(':shared')
	    }
	    task spiJar(type: Jar) {
	        archiveBaseName = 'api-spi'
	        from sourceSets.main.output
	        include('org/gradle/sample/api/**')
	    }
	    artifacts {
	        spi spiJar
	    }
	}
	
	project(':services:personService') {
	    dependencies {
	        compile project(':shared')
	        compile project(path: ':api', configuration: 'spi')
	        testCompile "junit:junit:4.12", project(':api')
	    }
	}

-  Java插件在默认情况下，向包含所有类的项目库添加一个jar。在这个例子中，我们创建了一个额外的库，它只包含`api`项目的接口。我们将这个库分配给一个新的依赖配置。对于`personService`，声明其仅针对`api`接口编译项目，但是使用`api`中的所有类进行测试

## 6.5 取决于另一个项目产生的任务输出

项目依赖模拟模块之间的依赖关系。 实际上，项目依赖就是依赖于另一个项目的主要输出。 在基于Java的项目中，它通常是一个JAR文件

有时，可能希望依赖于另一个任务产生的输出。这就需要确保提前执行任务，以生成相应的输出。声明从一个项目到另一个项目的任务依赖关系是对这种关系糟糕的模拟，并引入了不必要的耦合。为这种依赖关系建模的推荐方法是使用生成的输出，将其标记为`outgoing`构件，或者将其添加到主源集的输出中，这样就可以在其他项目中依赖该输出


### 6.5.1 用来生成包含构件信息的属性文件的任务

假设您在一个多项目构建中使用两个子项目`producer`和`consumer`。子项目`producer`定义了一个名为`buildInfo`的任务，该任务用来生成一个包含构建信息的属性文件，例如项目版本。属性`builtBy`负责建立推断的任务依赖关系。有关builtBy的更多信息，请参见[SourceSetOutput](https://docs.gradle.org/current/dsl/org.gradle.api.tasks.SourceSetOutput.html)


	// build.gradle
	task buildInfo(type: BuildInfo) {
	    version = project.version
	    outputFile = file("$buildDir/generated-resources/build-info.properties")
	}
	
	sourceSets {
	    main {
	        output.dir(buildInfo.outputFile.parentFile, builtBy: buildInfo)
	    }
	}

-  消费项目应该能够在运行时读取属性文件。 在生产项目上声明项目依赖关系将负责预先创建属性并使其对运行时类路径可用


### 6.5.2 在生产一个属性文件的项目上声明项目依赖

	// build.gradle
	dependencies {
	    runtimeOnly project(':producer')
	}

- 在上面的例子中，消费者现在声明了对生产者项目输出的依赖关系

# 7. 项目并行执行

Gradle支持并行执行，其能够尽可能的去利用现代计算机的多核能力，更具体的说法就是，并行执行将会尝试以下的行为：

- 减少多项目构建的总构建时间，其中执行是IO绑定的，否则不会消耗所有可用的CPU资源

- 为小型项目的执行提供更快的反馈，而无需等待其他项目的完成

项目并行执行允许在解耦的多项目构建中的单独项目并行执行。 虽然并行执行并不严格要求在配置时进行解耦，但长期目标是提供一组功能强大的功能，这些功能可用于完全分离的项目。 这些功能包括：

- 按需配置

- 并行配置项目

- 对未更改的项目重用配置

- 项目级的最新检查

- 使用预构建的构件代替构建依赖项目

并行执行如何工作？ 

- 首先，需要告诉Gradle使用并行模式。 您可以使用`--parallel`命令行参数或配置构建环境（Gradle属性）。 除非指定了特定数量的并行线程，否则Gradle会尝试根据可用的CPU内核选择正确的数量。 每个并行工作线程在执行任务时独占给定的项目。

	并行执行完全支持任务依赖，并行执行将首先开始执行上游任务。 请记住，并行模式下无法保证任务的按字母顺序执行。 换句话说，在并行模式下，任务将在其依赖项完成后立即运行，并且任务工作者可以运行它们，这可能早于在顺序构建期间启动它们。 需要确保正确的声明了任务的依赖和输入/输出，以避免排序问题



# 8. 项目的解耦


# 9. 多项目构建和测试









	