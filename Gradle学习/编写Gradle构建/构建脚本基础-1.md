# 构建脚本基础

[Introducing the Basics of Build Scripts](https://docs.gradle.org/current/userguide/tutorial_using_tasks.html)

[构建脚本的基础知识-极客学院](http://wiki.jikexueyuan.com/project/GradleUserGuide-Wiki/build_script_basics/README.html)


# 1. Projects 和 tasks

Gradle 里的任何东西都是基于两个基础概念:

- projects ( 项目 )

- tasks ( 任务 )

每一个构建都是由一个或多个 projects 构成的. 一个 project 到底代表什么取决于你想用 Gradle 做什么. 举个例子, 一个 project 可以代表一个 JAR 或者一个网页应用. 它也可能代表一个发布的 ZIP 压缩包, 这个 ZIP 可能是由许多其他项目的 JARs 构成的. 但是一个 project 不一定非要代表被构建的某个东西. 它可以代表一件要做的事, 比如部署你的应用.

每一个 project 是由一个或多个 tasks 构成的. 一个 task 代表一些更加细化的构建. 可能是编译一些 classes, 创建一个 JAR, 生成 javadoc, 或者生成某个目录的压缩文件.


# 2. Hello world

通过`gradle`命令可以运行一个Gradle构建

- `gradle` 命令会在当前目录中查找一个叫 `build.gradle` 的文件. 我们称这个 `build.gradle `文件为一个构建脚本 (`build script`), 但是严格来说它是一个构建配置脚本 (`build configuration script`). 这个构建脚本中定义了一个 project 和 若干task

来看一个例子，创建一个名为`build.gradle`的构建脚本

	// build.gradle
	
	task hello {
	    doLast {
	        println 'Hello world!'
	    }
	}
	
在命令行里, 进入脚本所在的文件夹然后输入 `gradle -q hello` 来执行构建脚本:

`gradle -q hello` 的输出

	gradle -q hello Hello world!

- 这里发生了什么? 

	这个构建脚本定义了一个独立的任务, 叫做 `hello`, 并且该任务加入了一个 action. 当你运行 `gradle hello`, Gradle 执行叫做 hello 的 task, 也就是执行了你所提供的 action. 这个 action 是一个包含了一些Groovy代码的闭包

- 补充一点命令里的 -q 是干什么的?

	`-q` 代表 quite 模式. 它不会生成 Gradle 的日志信息 (log messages), 所以用户只能看到 tasks 的输出. 它使得的输出更加清晰
	
	
# 3. 构建脚本即编程

Gradle 的构建脚本拥有Groovy 的所有能力，因此能够在构建脚本中进行编程

## 3.1 在Gradle的任务中使用Groovy

	task upper {
		doLast{
		    String someString = 'mY_nAmE'
		    println "Original: " + someString
		    println "Upper case: " + someString.toUpperCase()
		}
	}
	
	task count {
	    doLast {
	        4.times { print "$it " }
	    }
	}
	
- 上面俩个task都使用了Groovy进行编程	
	
# 4. 任务依赖

**任务之间可以相互依赖**

## 4.1 申明任务之间的依赖关系(通过配置)

	task hello {
	    doLast {
	        println 'Hello world!'
	    }
	}
	
	task intro {
	    dependsOn hello
	    doLast {
	        println "I'm Gradle"
	    }
	}

`gradle -q intro `命令的输出

	> gradle -q intro
	Hello world!
	I'm Gradle

- 任务`intro` 依赖于`hello`, 所以执行 `intro` 的时候 `hello` 命令会被优先执行来作为启动`intro`任务的条件

## 4.2 在任务定义之前申明依赖关系

**在当前任务添加一个依赖任务之前, 这个被依赖的任务不需要提前定义**

	task taskX {
	    dependsOn 'taskY'
	    doLast {
	        println 'taskX'
	    }
	}
	task taskY {
	    doLast {
	        println 'taskY'
	    }
	}

gradle -q taskX 命令的输出

	> gradle -q taskX
	taskY
	taskX
	
- taskX 对 taskY 的依赖在 taskY 被定义之前就已经声明了.这种形式在多项目构建中提供了极大的便利

- 需要注意的一点：**当所关联的任务还没有被定义,不能使用快捷方式进行访问**


# 5. 动态任务

Groovy所能提供的功能，不仅能够用来定义一个任务可以做什么，还可以用来动态的创建任务等操作

## 5.1 动态的创建一个任务
	
	4.times { counter ->
	    task "task$counter" {
	        doLast {
	            println "I'm task number $counter"
	        }
	    }
	}
	
- 这里动态的创建了 task0, task1, task2, task3

gradle -q task1 命令的输出

	> gradle -q task1
	I'm task number 1
	
	
	
# 6. 使用已经存在的任务

**当任务被创建之后, 它可以通过API来访问,可以在运行时动态的添加依赖，添加行为等操作**

- ANT不支持这种功能

## 6.1 通过API对任务添加依赖
	
	4.times { counter ->
	    task "task$counter" {
	        doLast {
	            println "I'm task number $counter"
	        }
	    }
	}
	task0.dependsOn task2, task3

gradle -q task0 命令的输出

	> gradle -q task0
	I'm task number 2
	I'm task number 3
	I'm task number 0
	
## 6.2 通过API添加行为(aciton/configuration)
	
	task hello {
	    doLast {
	        println 'Hello Earth'
	    }
	}
	hello.doFirst {
	    println 'Hello Venus'
	}
	hello.configure {
	    doLast {
	        println 'Hello Mars'
	    }
	}
	hello.configure {
	    doLast {
	        println 'Hello Jupiter'
	    }
	}
	
gradle -q hello 命令的输出

	> gradle -q hello
	Hello Venus
	Hello Earth
	Hello Mars
	Hello Jupiter	
	
- doFirst 和 doLast 可以被执行许多次
	
	它们的作用是分别在任务动作列表的开始和结束加入动作
	
- **当任务执行的时候, 在动作列表里的动作将被按顺序执行**



# 7. Groovy DSL shortcut notations(快捷符号)

**Gradle提供了一个便利的表示法去访问一个已经存在的任务**

- 也就是说每个任务都可以作为构建脚本的属性来使用，即通过符号`$`去访问

## 7.1 使用构建脚本的属性访问任务

	task hello {
	    doLast {
	        println 'Hello world!'
	    }
	}
	hello.doLast {
	    println "Greetings from the $hello.name task."
	}

gradle -q hello 命令的输出
	
	> gradle -q hello
	Hello world!
	Greetings from the hello task.
	
- 这里的 `name` 是任务的默认属性, 代表当前任务的名称, 这里是 `hello`

- 这使得代码易于读取， 特别是当使用了由插件（如`compile`任务）提供的任务时尤其如此.

# 8. 自定义任务属性

**任务对象可以加入自定义的属性**

## 8.1 给任务加入自定义属性
例如加入一个叫做 `myProperty` 属性, 通过设置`ext.myProperty`去初始化值. 然后, 该属性就可以像预定义的任务属性那样被读取和设置了.


	task myTask {
	    ext.myProperty = "myValue"
	}
	
	task printTaskProperties {
	    doLast {
	        println myTask.myProperty
	    }
	}
	
	> gradle -q printTaskProperties
	myValue
	
- 给任务加自定义属性是没有限制的



# 9. 调用 Ant 任务

Ant 任务是 Gradle 的一等公民. Gradle 通过 Groovy 出色的集成了 Ant 任务. Groovy 自带了一个 AntBuilder. 相比于从一个 `build.xml` 文件中使用 Ant 任务, 在 Gradle 里使用 Ant 任务更为方便和强大. 从下面的例子中, 你可以学习如何执行 Ant 任务以及如何访问 ant 属性：

例子 6.13. 使用 AntBuilder 来执行 ant.loadfile 任务
	
	task loadfile {
	    doLast {
	        def files = file('./antLoadfileResources').listFiles().sort()
	        files.each { File file ->
	            if (file.isFile()) {
	                ant.loadfile(srcFile: file, property: file.name)
	                println " *** $file.name ***"
	                println "${ant.properties[file.name]}"
	            }
	        }
	    }
	}
	
gradle -q loadfile 命令的输出

	> gradle -q loadfile
	*** agile.manifesto.txt ***
	Individuals and interactions over processes and tools
	Working software over comprehensive documentation
	Customer collaboration  over contract negotiation
	Responding to change over following a plan
	 *** gradle.manifesto.txt ***
	 
	 
# 10. 使用方法抽取逻辑
Gradle 能很好地衡量你编写脚本的逻辑能力. 首先要做的是如何提取一个方法.

## 10.1 使用方法来组织构建逻辑
	
	task checksum {
	    doLast {
	        fileList('./antLoadfileResources').each { File file ->
	            ant.checksum(file: file, property: "cs_$file.name")
	            println "$file.name Checksum: ${ant.properties["cs_$file.name"]}"
	        }
	    }
	}
	
	task loadfile {
	    doLast {
	        fileList('./antLoadfileResources').each { File file ->
	            ant.loadfile(srcFile: file, property: file.name)
	            println "I'm fond of $file.name"
	        }
	    }
	}
	
	File[] fileList(String dir) {
	    file(dir).listFiles({file -> file.isFile() } as FileFilter).sort()
	}
		
	> gradle -q loadfile
	I'm fond of agile.manifesto.txt
	I'm fond of gradle.manifesto.txt
	
- 这种方法可以在多项目构建的子项目之间共享

- 如果你的构建逻辑变得更加复杂, Gradle 为你提供了其他非常方便的方法(组织构建逻辑)


# 11. 默认任务

**Gradle 允许在脚本中定义一个或多个默认任务，默认任务将会在没有指定任务时去执行**

## 11.1 定义默认任务
	
	defaultTasks 'clean', 'run'
	
	task clean {
	    doLast {
	        println 'Default Cleaning!'
	    }
	}
	
	task run {
	    doLast {
	        println 'Default Running!'
	    }
	}
	
	task other {
	    doLast {
	        println "I'm not a default task!"
	    }
	}
	
gradle -q 命令的输出

	> gradle -q
	Default Cleaning!
	Default Running!
	
- 等价于 `gradle -q clean run`. 

- 在一个多项目构建中, 每一个子项目都可以有指定的默认任务. 如果一个子项目没有指定默认任务,父项目的默认任务将会被执行(如果有定义)


# 12. 通过 DAG 配置

**从构建的生命周期可知，Gradle 有一个配置阶段和执行阶段. 在配置阶段后, Gradle就会知道所有应该执行的任务. 为了利用这些信息，Gradle 提供一个"hook"方法**

## 12.1 不同的任务产生不同的输出

使用案例：判断`release`任务是否是要被执行的任务,并修改其`version`属性
	
	task distribution {
	    doLast {
	        println "We build the zip with version=$version"
	    }
	}
	
	task release {
	    dependsOn 'distribution'
	    doLast {
	        println 'We release now'
	    }
	}
	
	gradle.taskGraph.whenReady { taskGraph ->
	    if (taskGraph.hasTask(":release")) {
	        version = '1.0'
	    } else {
	        version = '1.0-SNAPSHOT'
	    }
	}

`gradle -q distribution` 命令的输出

	> gradle -q distribution
	We build the zip with version=1.0-SNAPSHOT
	Output of gradle -q release
	> gradle -q release
	We build the zip with version=1.0
	We release now
	
- **注意：`whenReady`会在`release` 任务执行之前对其产生影响. 甚至在`release` 任务不是首要任务时，也会产生影响 (首要任务是指通过 gradle 命令的任务)**



# 13. 构建脚本的外部依赖

如果构建脚本需要使用外部的一个库，可以将库添加到构建脚本自身类路径(`classpath`)中

- 为此，Gradle提供了`buildscript()`方法用来实现声明构建脚本所需库的类路径，其接受一个闭包进行设置

		buildscript {
		    repositories {
		        mavenCentral()
		    }
		    dependencies {
		        classpath group: 'commons-codec', name: 'commons-codec', version: '1.2'
		    }
		}

	- **传递给`buildscript`方法的闭包实际上是对`ScriptHandler`实例进行配置，通过向`dependencies()`方法添加类路径来申明构建脚本的类路径，这与Java编译类路径的使用方式相同**

	- 除了项目依赖之外，其他的任何依赖类型都可以使用

- [ScriptHandler文档](https://docs.gradle.org/current/javadoc/org/gradle/api/initialization/dsl/ScriptHandler.html)

## 13.1 构建脚本使用外部依赖示例

在构建脚本中声明了库之后，就可以直接在构建脚本中使用库中的类

	import org.apache.commons.codec.binary.Base64
	
	buildscript {
	    repositories {
	        mavenCentral()
	    }
	    dependencies {
	        classpath group: 'commons-codec', name: 'commons-codec', version: '1.2'
	    }
	}
	
	task encode {
	    doLast {
	        def byte[] encodedString = new Base64().encode('hello world\n'.getBytes())
	        println new String(encodedString)
	    }
	}

	> gradle -q encode
	aGVsbG8gd29ybGQK

- 对于多项目构建，在某个项目的`buildscript()`方法声明的依赖，可用于它的所有子项目的构建脚本

- 构建脚本依赖项可以是Gradle插件

- 每个项目都自动的具有一个`BuildEnvironmentReportTask `类型的任务`buildEnvironment`,该任务可以用来报告构建脚本依赖的解析

