# 理解构建生命周期

[Build Lifecycle](https://docs.gradle.org/current/userguide/build_lifecycle.html)


# 1. 简介

Gradle的核心是一种基于依赖关系的编程语言。用Gradle术语来说，这意味着您可以定义任务，以及任务之间的依赖关系。Gradle保证这些任务按照依赖关系的顺序执行，并且每个任务只执行一次。这些任务形成一个[有向无环图](https://zh.wikipedia.org/wiki/%E6%9C%89%E5%90%91%E6%97%A0%E7%8E%AF%E5%9B%BE)。像有一些构建工具在执行任务时会构建这样的依赖关系图一样,Gradle在执行任何任务之前都要构建完整的依赖关系图。这是Gradle的核心，使许多事情成为可能

- 在图论中，如果一个有向图从任意顶点出发无法经过若干条边回到该点，则这个图是一个有向无环图。 因为有向图中一个点经过两种路线到达另一个点未必形成环，因此有向无环图未必能转化成树，但任何有向树均为有向无环图


构建脚本配置了这个依赖关系图。因此严格地说，构建脚本可以被称为构建配置脚本(`build configuration scripts`)

# 2. 构建阶段

Gradle构建有三个不同的阶段:
 
1. 初始化阶段(`Initialization`)

	Gradle支持单个和多个项目的构建。在初始化阶段，Gradle将确定哪些项目将参与构建，并为每个项目创建一个`Project`实例

2. 配置阶段(`Configuration`)

	在此阶段，将配置`Project`对象。此外，所有项目中 作为构建的一部分的构建脚本会被执行

3. 执行阶段(`Execution`)

	 Gradle确定将要执行的任务子集，这些任务是在配置阶段被创建和配置。 子集由传递给gradle命令的任务名称参数和当前目录确定。 在确定之后，Gradle将执行每个选定的任务
	 
	 
# 3. Settings file

除了构建脚本文件，Gradle还定义了一个设置文件。设置文件由Gradle通过命名约定确定。这个文件的默认名称是`settings.gradle`

设置文件在初始化阶段执行。多项目构建项目在其根目录中必须存在`settings.gradle`文件,因为设置文件定义了哪些项目正在参与多项目构建([参见编写多项目构建](https://docs.gradle.org/current/userguide/multi_project_builds.html#multi_project_builds))

对于单项目构建，设置文件是可选的。除了定义包含的项目外，设置文件还可以将库添加到构建脚本类路径中([参见组织Gradle项目](https://docs.gradle.org/current/userguide/organizing_gradle_projects.html#organizing_gradle_projects))


## 3.1 单项目构建

	//settings.gradle
	println 'This is executed during the initialization phase.'


	//build.gradle
	println 'This is executed during the configuration phase.'
	
	task configured {
	    println 'This is also executed during the configuration phase.'
	}
	
	task test {
	    doLast {
	        println 'This is executed during the execution phase.'
	    }
	}
	
	task testBoth {
		doFirst {
		  println 'This is executed first during the execution phase.'
		}
		doLast {
		  println 'This is executed last during the execution phase.'
		}
		println 'This is executed during the configuration phase as well.'
	}
	-------------------------------------------
	//输出内容
	 > gradle test testBoth
	This is executed during the initialization phase.
	
	> Configure project :
	This is executed during the configuration phase.
	This is also executed during the configuration phase.
	This is executed during the configuration phase as well.
	
	> Task :test
	This is executed during the execution phase.
	
	> Task :testBoth
	This is executed first during the execution phase.
	This is executed last during the execution phase.


对于构建脚本，其将属性访问和方法调用委托给项目对象(即`Project`实例)。类似地，设置文件将属性访问和方法调用委托给设置对象(即`Settings`实例)。

- 有关更多信息，请查看API文档中的[Settings类](https://docs.gradle.org/current/dsl/org.gradle.api.initialization.Settings.html)


## 3.2 多项目构建

多项目构建是在Gradle的单个执行过程中构建多个项目的构建

- 必须在设置文件中声明参与多项目构建的项目。关于多项目构建([参见编写多项目构建](https://docs.gradle.org/current/userguide/multi_project_builds.html#multi_project_builds))


### 3.2.1 项目位置

多项目构建总是由具有单个根的树表示,树中的每个元素代表一个项目

- 项目有一个路径，该路径表示项目在多项目构建树中的位置。在大多数情况下，项目路径与文件系统中项目的物理位置一致。然而，这种行为是可配置的。项目树是在`settings.gradle`文件中创建

- 默认情况下，假定设置文件的地址就是根项目的地址。但是这个地址可以在`settings.gradle`文件中被重新定义


### 3.2.2 构建一个tree

在设置文件中，可以使用一组方法来构建项目树。分层布局和平面物理布局得到了特殊的支持

#### 3.2.2.1 分层布局

	//settings.gradle
	include 'project1', 'project2:child', 'project3:child1'
	
	
-  `include()`方法将接受项目路径作为参数。 

- 项目路径被假定为等同于相对物理文件系统路径。 例如，路径`services:api`默认映射到文件夹`services/api`（相对于项目根目录）

- 只需指定树的叶子。 这意味着`include services:hotels:api`将导致3个项目的创建,即`services`, `services:hotels`和 `services:hotels:api`

- 有关如何在设置文件中使用`include`方法，可以在[`Settings.include（java.lang.String []）`](https://docs.gradle.org/current/dsl/org.gradle.api.initialization.Settings.html#org.gradle.api.initialization.Settings:include(java.lang.String[]))的DSL文档中找到

#### 3.2.2.2 平面布局

	//settings.gradle
	includeFlat 'project3', 'project4'

- `includeFlat()`方法以目录名作为参数。并且这些目录需要作为根项目目录的兄弟目录存在。这些目录的位置被认为是多项目树中根项目的子项目


## 3.3 修改项目树的元素

在设置文件中创建的多项目树由所谓的项目描述符(`project descriptors`)组成。可以随时在设置文件中修改这些描述符

### 3.3.1 查找项目树中的元素

	//settings.gradle
	println rootProject.name
	println project(':projectA').name

- 使用描述符，可以更改项目的名称、项目目录和构建文件

### 3.3.2 修改项目树中的元素

	// settings.gradle
	rootProject.name = 'main'
	project(':projectA').projectDir = new File(settingsDir, '../my-project-a')
	project(':projectA').buildFileName = 'projectA.gradle'

- 查看[ProjectDescriptor](https://docs.gradle.org/current/javadoc/org/gradle/api/initialization/ProjectDescriptor.html)以获取更多信息

# 4. 初始化(`Initialization`)

## 4.1 Gradle如何知道是进行单项目或多项目构建？

如果从具有设置文件的目录触发多项目构建，则直接从设置文件中获取这个条件。 但Gradle还允许从参与构建的任何子项目中执行构建。如果在没有`settings.gradle`文件的项目中执行,`Gradle`将按以下方式查找`settings.gradle`文件：

1. 它在一个名为master的目录中查找，该目录具有与当前目录相同的嵌套级别。

2. 如果没有找到，则搜索父目录

3. 如果仍未找到，则构建将作为单个项目构建执行

4. 如果一个`settings.gradle`文件被找到，Gradle会检查当前项目是否是在找到的设置文件中定义的多项目层次结构的一部分。如果没有找到，则构建将作为单个项目构建执行。否则将作为多项目构建执行

## 4.2 这种查找方式的的目的是什么?

**Gradle需要确定您所在的项目是否是多项目构建的子项目**

1. 如果它是一个子项目，那么只构建子项目及其相关的项目，

2. 如果是一个根项目，Gradle需要为整个多项目构建创建构建配置(参见编写多项目构建)

如果当前项目包含`settings.gradle`，构建行为是：

-  如果`settings.gradle`文件没有定义多项目层次结构，则为单项目构建

-  如果`settings.gradle`文件定义了多项目层次结构，则为多项目构建


自动搜索`settings.gradle`文件仅适用于具有物理层次结构或平面布局的多项目构建。 对于平面布局，还必须遵循上述命名约定（`“master”`）。 Gradle支持多项目构建的任意物理布局，但是对于这种任意布局，需要从设置文件所在的目录执行构建


有关如何从根运行部分构建的信息，[请参阅按绝对路径运行任务](https://docs.gradle.org/current/userguide/multi_project_builds.html#sec:running_partial_build_from_the_root)

**Gradle为参与构建的每个项目创建一个Project对象**

- 对于多项目构建来说，这些对象都是`Settings`对象（以及根项目）中指定的项目

- 默认情况下，每个项目对象的名称都等于其顶级目录的名称，除根项目之外的每个项目都有一个父项目

- 任何项目都可能有子项目


# 5. 单项目构建的配置和执行

对于单项目构建，初始化阶段后的工作流程非常简单。构构建脚本针对初始化阶段创建的项目对象执行， 然后Gradle查找命令行参数传入的任务名称，如果存在这些任务名称，则它们将按照您传递的顺序作为单独的构建执行

# 6. 响应构建脚本中的生命周期

构建脚本可以在构建过程中接收通知。这些通知通常采用两种形式:

1. 实现特定的监听器接口

2. 提供一个闭包在触发通知时来执行


下面的示例使用闭包。有关如何使用监听器接口的详细信息，请参阅API文档。

## 6.1 项目评估

您可以在项目评估之前和之后立即收到通知。这可用于执行一些操作，例如在应用了构建脚本中所有的定义后执行其他配置，或者用于某些自定义日志记录或分析


### 6.1.1 将test任务添加到具有特定属性集的每个项目


下面是一个例子，它向每个hasTests属性值为true的项目添加了一个测试任务。


	//build.gradle
	allprojects {
	    afterEvaluate { project ->
	        if (project.hasTests) {
	            println "Adding test task to $project"
	            project.task('test') {
	                doLast {
	                    println "Running tests for $project"
	                }
	            }
	        }
	    }
	}

	//projectA.gradle
	hasTests = true


	> gradle -q test
	Adding test task to project ':projectA'
	Running tests for project ':projectA'


- 该例子使用`Project.afterEvaluate()`方法添加了一个闭包，这个闭包会在项目评估结束后执行

### 6.1.2 监听评估项目前的行为

Gradle支持在评估任何项目前接收通知

- 注意，无论项目评估成功与否，都会收到`afterProject`通知

此示例执行一些项目评估的自定义日志记录

	//build.gradle
	gradle.afterProject { project ->
	    if (project.state.failure) {
	        println "Evaluation of $project FAILED"
	    } else {
	        println "Evaluation of $project succeeded"
	    }
	}


	> gradle -q test
	Evaluation of root project 'buildProjectEvaluateEvents' succeeded
	Evaluation of project ':projectA' succeeded
	Evaluation of project ':projectB' FAILED
	
	FAILURE: Build failed with an exception.
	
	* Where:
	Build file '/home/user/gradle/samples/groovy/projectB.gradle' line: 1
	
	* What went wrong:
	A problem occurred evaluating project ':projectB'.
	> broken
	
	* Try:
	Run with --stacktrace option to get the stack trace. Run with --info or --debug option to get more log output. Run with --scan to get full insights.
	
	* Get more help at https://help.gradle.org
	
	BUILD FAILED in 0s

- **实际上，还可以通过给Gradle对象设置[`ProjectEvaluationListener`](https://docs.gradle.org/current/javadoc/org/gradle/api/ProjectEvaluationListener.html)来获取这些事件**

## 6.2 任务的创建

Gradle支持在将任务添加到项目后立即收到通知

- 这可以用于在构建文件中使用任务之前设置一些默认值或添加行为

### 6.2.1 为全部任务设置一个属性

	//build.gradle
	tasks.whenTaskAdded { task ->
	    task.ext.srcDir = 'src/main/java'
	}
	
	task a
	
	println "source dir is $a.srcDir"

	> gradle -q a
	source dir is src/main/java

- **实际上，还可以通过给[`TaskContainer`](https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/TaskContainer.html)对象添加`Action`来获取这些事件**

## 6.3 任务的执行图就绪

您可以在填充任务执行图之后立即收到通知([请参见DAG配置](https://docs.gradle.org/current/userguide/tutorial_using_tasks.html#configure-by-dag))

- **实际上，还可以通过给`TaskExecutionGraph `对象添加`TaskExecutionGraphListener`来获取这些事件**

## 6.4 任务执行

您可以在执行任何任务之前和之后立即收到通知

- 注意，无论任务成功完成还是异常失败，都会接收到`afterTask`通知

### 6.4.1 记录每个任务执行的前后

下面的示例记录每个任务执行的开始和结束

	//build.gradle
	task ok
	
	task broken(dependsOn: ok) {
	    doLast {
	        throw new RuntimeException('broken')
	    }
	}
	
	gradle.taskGraph.beforeTask { Task task ->
	    println "executing $task ..."
	}
	
	gradle.taskGraph.afterTask { Task task, TaskState state ->
	    if (state.failure) {
	        println "FAILED"
	    }
	    else {
	        println "done"
	    }
	}

	> gradle -q broken
	executing task ':ok' ...
	done
	executing task ':broken' ...
	FAILED
	
	FAILURE: Build failed with an exception.
	
	* Where:
	Build file '/home/user/gradle/samples/groovy/build.gradle' line: 5
	
	* What went wrong:
	Execution failed for task ':broken'.
	> broken
	
	* Try:
	Run with --stacktrace option to get the stack trace. Run with --info or --debug option to get more log output. Run with --scan to get full insights.
	
	* Get more help at https://help.gradle.org

- **实际上，还可以通过给`TaskExecutionGraph`对象添加`TaskExecutionListener`来获取这些事件**
