# 使用初始化脚本
[Initialization Scripts](https://docs.gradle.org/current/userguide/init_scripts.html)


# 1. 简介


Gradle提供了一个强大的机制，允许基于当前环境定制构建。该机制还支持希望与Gradle集成的工具

- 注意，这内置插件提供的`init`任务完全不同


# 2. 基础用法介绍

初始化脚本(`Initialization scripts`,又称`init scripts`),其与Gradle中的其他脚本类似，区别是初始化脚本在构建开始之前运行

初始化脚本几个可能的用法:

- 设置企业范围(`enterprise-wide`)的配置。 例如在哪里可以找到自定义插件

- 根据当前环境设置属性 。例如开发人员的计算机与持续集成服务器。

- 提供构建所需的关于用户的个人信息。例如存储库或数据库身份验证凭据。

- 定义特定于机器的细节。例如jdk安装在何处。

- 注册构建监听器。 例如外部工具可能希望监听Gradle事件

- 注册构建记录器。 例如 希望自定义Gradle记录它生成的事件

init脚本的限制:

- 初始化脚本的一个主要限制是它们不能访问buildSrc项目中的类


# 3. 使用初始化脚本

以下是几种使用初始化脚本的方法:

- 在命令行上指定一个文件。命令行选项是`-I`或`-init-script`，后面跟着脚本的路径。命令行选项可以出现不止一次，每次添加一个初始化脚本。但是如果命令行中指定的任何文件不存在，构建将失败

-  将一个名为`init.gradle`的文件放在`USER_HOME/.gradle/`目录中

-  将一个以`.gradle`结尾的文件放在`USER_HOME /.gradle/init.d/`目录中

-  将一个以`.gradle`结尾的文件放在Gradle发行版的`GRADLE_HOME/init.d/`目录中。 这允许您打包一个 包含自定义构建逻辑和插件的Gradle发行版。 可以将其与`Gradle Wrapper`结合使用，以便将订制逻辑针对所有的构建都可用


如果找到多个初始化脚本，那么它们都将按照上面的顺序进行执行(举个例子，工具在命令行上指定一个初始化脚本，用户将另一个初始化脚本放在自己的主目录中，以便定义环境，当执行Gradle时，这两个脚本都将运行)

- 同一个目录中的初始化脚本将会按照脚本首字母顺序进行执行


# 4. 编写初始化脚本

与Gradle构建脚本类似，初始化脚本使用Groovy编写(即Groovy脚本)

- **每个初始化脚本都有一个与之关联的Gradle实例**

- **初始化脚本中的任何属性引用和方法调用都将委托给这个Gradle实例**

- **每个初始化脚本还实现了`Script`接口**


## 4.1 通过初始化脚本配置项目

Gradle支持使用初始化脚本来配置构建中的项目。这与在多项目构建中配置项目的方式类似

### 4.1.1  在评估项目之前使用初始化脚本执行额外配置
下面的示例显示了如何在评估项目之前使用初始化脚本执行额外的配置。此示例使用此特性来配置仅用于特定环境的额外存储库

	//build.gradle
	repositories {
	    mavenCentral()
	}
	
	task showRepos {
	    doLast {
	        println "All repos:"
	        println repositories.collect { it.name }
	    }
	}

	//init.gradle
	allprojects {
	    repositories {
	        mavenLocal()
	    }
	}

	> gradle --init-script init.gradle -q showRepos
	All repos:
	[MavenLocal, MavenRepo]
	
# 5. 初始化脚本的外部依赖

初始化脚本也可以声明依赖项。Gradle提供了`initscript()`方法来实现这一点，其需要传入一个声明了初始化脚本类路径的闭包


## 5.1 为初始化脚本声明一个外部依赖

	// init.gradle
	initscript {
	    repositories {
	        mavenCentral()
	    }
	    dependencies {
	        classpath 'org.apache.commons:commons-math:2.0'
	    }
	}
		
- 传递给`initscript()`方法的闭包配置了一个`ScriptHandler`对象

- 通过向配置`classpath`添加依赖项来声明初始化脚本类路径。这与声明Java编译类路径的方法相同

- 可以使用声明依赖项中描述的任何依赖项类型，但项目依赖项除外

**在声明了初始化脚本类路径之后，就可以像使用初始化脚本中的任何其他类一样使用类路径上的类**

## 5.2 在初始化脚本中使用外部依赖

下面的示例展示了使用初始化脚本类路径中的类

	//init.gradle
	import org.apache.commons.math.fraction.Fraction
	
	initscript {
	    repositories {
	        mavenCentral()
	    }
	    dependencies {
	        classpath 'org.apache.commons:commons-math:2.0'
	    }
	}
	
	println Fraction.ONE_FIFTH.multiply(2)

	> gradle --init-script init.gradle -q doNothing
	2 / 5


# 6. 初始化脚本插件

与Gradle构建脚本和Gradle设置脚本一样，初始化脚本也可以应用插件

## 6.1 在初始化脚本中使用插件

	//init.gradle
	apply plugin: EnterpriseRepositoryPlugin
	
	class EnterpriseRepositoryPlugin implements Plugin<Gradle> {
	
	    private static String ENTERPRISE_REPOSITORY_URL = "https://repo.gradle.org/gradle/repo"
	
	    void apply(Gradle gradle) {
	        // ONLY USE ENTERPRISE REPO FOR DEPENDENCIES
	        gradle.allprojects { project ->
	            project.repositories {
	
	                // Remove all repositories not pointing to the enterprise repository url
	                all { ArtifactRepository repo ->
	                    if (!(repo instanceof MavenArtifactRepository) ||
	                          repo.url.toString() != ENTERPRISE_REPOSITORY_URL) {
	                        project.logger.lifecycle "Repository ${repo.url} removed. Only $ENTERPRISE_REPOSITORY_URL is allowed"
	                        remove repo
	                    }
	                }
	
	                // add the enterprise repository
	                maven {
	                    name "STANDARD_ENTERPRISE_REPO"
	                    url ENTERPRISE_REPOSITORY_URL
	                }
	            }
	        }
	    }
	}


	// build.gradle
	// tag::show-repos-task[]
	repositories{
	    mavenCentral()
	}
	
	 task showRepositories {
	     doLast {
	         repositories.each {
	             println "repository: ${it.name} ('${it.url}')"
	         }
	     }
	}

	> gradle --init-script init.gradle -q showRepositories
	repository: STANDARD_ENTERPRISE_REPO ('https://repo.gradle.org/gradle/repo')
	
- 初始化脚本中的插件确保在运行构建时只使用指定的存储库

在初始化脚本中应用插件时，Gradle实例化插件并调用插件实例的`Plugin.apply（T）`方法。 gradle对象将被作为参数传递该方法中，这个对象被用于配置构建的所有方面。 

当然，应用的插件可以是外部依赖，[初始化脚本中的外部依赖](https://docs.gradle.org/current/userguide/init_scripts.html#sec:custom_classpath)



	