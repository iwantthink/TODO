# 操作符

[Operators](http://www.groovy-lang.org/operators.html)


# 1. 算术运算符

Groovy支持所有Java算数运算符

## 1.1 二元运算符

Operator|Purpose|Remarks
---|---|---
+|加法|
-|减法|
*|乘法|
/|除法|`intdiv() `用来获取整除
%|求余
`**`|幂|

示例:

	assert  1  + 2 == 3
	assert  4  - 3 == 1
	assert  3  * 5 == 15
	assert  3  / 2 == 1.5
	assert 10  % 3 == 1
	assert  2 ** 3 == 8
	
## 1.2 一元运算符	

加号和减号可以作为一元运算符

	assert +3 == 3
	assert -4 == 0 - 4
	
	assert -(-1) == 1  
	
	
加号和减号还可以作为组成自增或自减运算符，可以当做前缀或者后缀

	def a = 2
	def b = a++ * 3             
	
	assert a == 3 && b == 6
	
	def c = 3
	def d = c-- * 2             
	
	assert c == 2 && d == 6
	
	def e = 1
	def f = ++e + 3             
	
	assert e == 2 && f == 5
	
	def g = 4
	def h = --g + 1             
	
	assert g == 3 && h == 4
	
- 自增或自减运算符当做前缀时，会先自增/减1，再参加运算

- 自增或自减运算符当做后缀时，会先参与运算，再自减/增1

## 1.3 赋值运算符

- `+=`

- `-=`

- `*=`

- `/=`

- `%=`

- `**=`


# 2. 关系运算符


Operator	|Purpose
---|---
`==`|相等
`!=`|不相等
`<`|小于
`<=`|小于或等于
`>`|大于
`>=`|大于或等于

# 3. 逻辑运算符

- `&&`: 逻辑`and`

- `||`: 逻辑`or`

- `!` : 逻辑`not`

示例:

	assert !false           
	assert true && true     
	assert true || false 
	
## 3.1 优先级

逻辑`not` > 逻辑`and` > 逻辑`||`	


## 3.2 短路

`||`运算符仅在左侧操作数不为真时，才会去计算右侧操作数，否则右侧操作数不会被计算

`&&`运算符在左侧操作数不为真时，不会去计算右侧操作数

示例：

	boolean checkIfCalled() {   
	    called = true
	}
	
	called = false
	true || checkIfCalled()
	assert !called              
	
	called = false
	false || checkIfCalled()
	assert called               
	
	called = false
	false && checkIfCalled()
	assert !called              
	
	called = false
	true && checkIfCalled()
	assert called    


# 4. 位运算符

位运算符可以用于 `byte`或`int`,并且返回值是`int`类型

- `&`: 按位与

- `|`: 按位或

- `^`: 按位异或

- `~`: 按位非


	int a = 0b00101010
	assert a == 42
	int b = 0b00001000
	assert b == 8
	assert (a & a) == a                     
	assert (a & b) == b                     
	assert (a | a) == a                     
	assert (a | b) == a                     
	
	int mask = 0b11111111                   
	assert ((a ^ a) & mask) == 0b00000000   
	assert ((a ^ b) & mask) == 0b00100010   
	assert ((~a) & mask)    == 0b11010101

- 原始类型的内部表示遵循Java语言规范，并且原始类型是有符号的，这意味着对于按位否定，最好只使用掩码检索必要的位

# 5. 条件运算符

## 5.1 非运算符

`not`运算符用感叹号(`!`)表示，能够反转布尔表达式的结果

	assert (!true)    == false                      
	assert (!'foo')   == false                      
	assert (!'')      == true     
	
- **非空的字符串被当做`true`**

- **空的字符串被当做`false`**	

## 5.2 三元运算符

 三元运算符是一个快捷表达式，相当于`if / else`分支为变量赋值
 
 下面俩个表达方式是相同的：
 
	if (string!=null && string.length()>0) {
	    result = 'Found'
	} else {
	    result = 'Not found'
	}

	result = (string!=null && string.length()>0) ? 'Found' : 'Not found'

## 5.3 Elvis operator

`Elvis operator`就是三元运算符的缩写
	
	displayName = user.name ? user.name : 'Anonymous'   
	displayName = user.name ?: 'Anonymous'   
	
	
# 6. 对象操作符

## 6.1 Safe navigation operator

安全导航操作符用于避免`NullPointerException`。 通常，在引用对象时，可能需要在访问对象的方法或属性之前验证它是否为`null`。 为了避免这种情况，安全导航操作符将只返回`null`而不是抛出异常

如下所示：

	// find 将返回一个null
	def person = Person.find { it.id == 123 }    
	def name = person?.name                      
	assert name == null     
	
## 6.2 直接访问字段的运算符

**在Groovy中，如果访问一个类的属性，会去使用其`getter`方法进行访问，如果想要直接访问字段而不是借助`getter`方法，那么可以使用`.@`去强制直接访问字段**

	class User {
	    public final String name                 
	    User(String name) { this.name = name}
	    String getName() { "Name: $name" }       
	}
	def user = new User('Bob')
	assert user.name == 'Name: Bob'   	
	
	assert user.@name == 'Bob'                   
	
- `user.name`是使用`getter`方法去访问

- `user.@name`直接访问字段
	
## 6.3 方法指针运算符

**方法指针操作符(`.&`)用于获取指定变量的方法引用，之后可以使用这个方法引用，并且这个方法引用时作用在对应的变量之上**

	def str = 'example of method reference'            
	def fun = str.&toUpperCase                         
	def upper = fun()                                  
	assert upper == str.toUpperCase()   
	
- 变量`fun`保存了`str`实例的`toUpperCase()`方法的引用,下面`fun`就可以当做普通的方法使用

这种方法指针的类型是`groovy.lang.Closure`,因此可以在任何可以使用闭包的地方进行使用。此外，这特别适合转换方法用来满足策略模式

	def transform(List elements, Closure action) {                    
	    def result = []
	    elements.each {
	        result << action(it)
	    }
	    result
	}
	String describe(Person p) {                                       
	    "$p.name is $p.age"
	}
	def action = this.&describe                                       
	def list = [
	    new Person(name: 'Bob',   age: 42),
	    new Person(name: 'Julia', age: 35)]                           
	assert transform(list, action) == ['Bob is 42', 'Julia is 35']  
		

方法指针由接收器和方法名称绑定。方法指针的参数在运行时解析，这意味着如果有多个具有相同名称的方法指针，语法也没有什么不同，它们会在运行选择适当方法

	def doSomething(String str) { str.toUpperCase() }    
	def doSomething(Integer x) { 2*x }                   
	def reference = this.&doSomething                    
	assert reference('foo') == 'FOO'                     
	assert reference(123)   == 246   
	
- 上面定义了俩个重载的方法，一个接收字符串作为参数，一个接收整数作为参数。然后获取了方法指针，并使用不同的参数对其进行调用


# 7. 正则表达式运算符

## 7.1 模式运算符

模式运算符(`~`)用来创建`java.util.regex.Pattern`实例

	def p = ~/foo/
	assert p instanceof Pattern
	
- 通常情况下，模式操作符会与斜线字符串一起使用，但实际上它可以和任何类型的字符串一起使用
		
		// 单引号字符串
		p = ~'foo'   
		// 双引号字符串                                                     
		p = ~"foo"   
		// 美元斜线字符串                                                     
		p = ~$/dollar/slashy $ string/$   
		// 带插值的双引号字符串                                
		p = ~"${pattern}"  	
		
## 7.2 查找运算符

除了构建一个`Pattern`实例，还可以使用匹配运算符`=~`去创建一个`java.util.regex.Matcher`实例

	def text = "some text to match"
	def m = text =~ /match/                                           
	assert m instanceof Matcher                                       
	if (!m) {                                                         
	    throw new RuntimeException("Oops, text not found!")
	}

- `=~`使用`match`为变量`text`创建了一个`Matcher`实例

- `!m`可以等价为`!m.find()`	

## 7.3 匹配运算符

**匹配运算符(`==~`)是查找运算符的变种，其不返回`Matcher`实例，而是返回一个布尔值,并且对输入的字符串进行严格匹配!**

	m = text ==~ /match/                                              
	assert m instanceof Boolean                                       
	if (m) {                                                          
	    throw new RuntimeException("Should not reach that point!")
	}
	

# 8. 其他操作符

## 8.1 展开点操作符

**展开操作符(`*.`)全称为展开点操作符(`Spread-dot`)操作符,用于对聚合对象上的所有条目进行调用。这就相当于在每个条目上都执行同样的操作，并将结果收集到一个列表中并返回**

	class Car {
	    String make
	    String model
	}
	def cars = [
	       new Car(make: 'Peugeot', model: '508'),
	       new Car(make: 'Renault', model: 'Clio')]       
	def makes = cars*.make                                
	assert makes == ['Peugeot', 'Renault']    
	
- 构建了一个条目为`Car`的列表，实际上这个列表就是一个聚合对象

- 然后访问这个`cars`列表上所有条目的`make`属性,并将属性的值保存到了一个列表`makes`中

- **表达式`cars*.make`等价于`cars.collect{it.make}`**

	`Object.collect(Closure cl)`方法用于遍历聚合对象，并将每个item传给闭包作为参数，并收集闭包的返回值
	
**Groovy的`GPath`表示法允许在引用的属性不是包含列表的属性时使用捷径，在这种情况下，它会自动扩展。例如上面的例子中，表达式`cars*.make`与`cars.make`效果相同，但通常建议保留显式的扩展点操作符**

展开点操作符是空安全的，这意味着如果集合的元素为空，它将返回`null`，而不是抛出`NullPointerException`

	cars = [
	   new Car(make: 'Peugeot', model: '508'),
	   null,                                              
	   new Car(make: 'Renault', model: 'Clio')]
	assert cars*.make == ['Peugeot', null, 'Renault']     
	assert null*.make == null   

- 不论是列表为空，或者是列表中的条目为空 都不会报错,只会返回null


**展开操作符可以在任何实现了`Iterable`接口的类上使用:**

	class Component {
	    Long id
	    String name
	}
	class CompositeObject implements Iterable<Component> {
	    def components = [
	        new Component(id: 1, name: 'Foo'),
	        new Component(id: 2, name: 'Bar')]
	
	    @Override
	    Iterator<Component> iterator() {
	        components.iterator()
	    }
	}
	def composite = new CompositeObject()
	assert composite*.id == [1,2]
	assert composite*.name == ['Foo','Bar']


**当聚合对象中嵌套聚合对象时，可以使用展开运算符多次进行调用以获得嵌套的内容：**

	class Make {
	    String name
	    List<Model> models
	}
	
	@Canonical
	class Model {
	    String name
	}
	
	def cars = [
	    new Make(name: 'Peugeot',
	             models: [new Model('408'), new Model('508')]),
	    new Make(name: 'Renault',
	             models: [new Model('Clio'), new Model('Captur')])
	]
	
	def makes = cars*.name
	assert makes == ['Peugeot', 'Renault']
	
	def models = cars*.models*.name
	assert models == [['408', '508'], ['Clio', 'Captur']]
	assert models.sum() == ['408', '508', 'Clio', 'Captur'] // flatten one level
	assert models.flatten() == ['408', '508', 'Clio', 'Captur'] // flatten all levels (one in this case)

- 聚合对象`cars `内部的条目中嵌套了另外一个聚合对象`models`,这里使用了多重展开运算符(`cars*.models*.name`)，不仅会收集聚合对象`cars`条目中的`name`属性，还会收集`models`中的`name`属性

- `Collection.sum()`

**对于集合嵌套集合的情况，使用`collectNested()`收集嵌套聚合对象与展开运算符能达到同样的效果**

	class Car {
	    String make
	    String model
	}
	def cars = [
	   [
	       new Car(make: 'Peugeot', model: '408'),
	       new Car(make: 'Peugeot', model: '508')
	   ], [
	       new Car(make: 'Renault', model: 'Clio'),
	       new Car(make: 'Renault', model: 'Captur')
	   ]
	]
	def models = cars.collectNested{ it.model }
	assert models == [['408', '508'], ['Clio', 'Captur']]

- 使用展开运算符也能达到同样的效果

		def models = cars*.make
		assert models == [['408', '508'], ['Clio', 'Captur']]
		
		
### 8.1.1 展开方法参数
当方法需要将一个列表中的每个条目拆开作为参数传入方法时，可以使用展开运算符：

假设拥有以下方法：

	int function(int x, int y, int z) {
	    x*y+z
	}	

并且需要将如下参数传入方法进行计算:
	
	def args = [4,5,6]

通过添加前缀`*`,可以直接将列表传入方法,而不需要借助中间变量

	assert function(*args) == 26

甚至可以将扩展参数与普通参数一起使用:

	args = [4]
	assert function(*args,5,6) == 26

### 8.1.2 展开列表元素

在嵌套了列表的列表中使用时，展开运算符会将被嵌套的列表的内容展开并添加到外层列表中：
	
	// 列表
	def items = [4,5]            
	// 直接将列表嵌套到列表中，而不是使用`addAll`          
	def list = [1,2,3,*items,6]            
	assert list == [1,2,3,4,5,6]  

### 8.1.3 展开Map元素

展开Map元素与展开列表元素类似，但是展开Map元素的运算符是`*:`

	def m1 = [c:3, d:4]                   
	def map = [a:1, b:2, *:m1]            
	assert map == [a:1, b:2, c:3, d:4] 


被展开的Map元素与外层的Map中的元素其位置是相对的，例如下面的示例:

	def m1 = [c:3, d:4]                   
	def map = [a:1, b:2, *:m1, d: 8]      
	assert map == [a:1, b:2, c:3, d:8] 

- `m1`被展开之后会在元素`d:8`之前，`c:3`之后


## 8.2 `Range`操作符

**Groovy支持`Range`概念，并提供了符号`..`来创建一个`Range`实例**

	def range = 0..5                                    
	assert (0..5).collect() == [0, 1, 2, 3, 4, 5]       
	assert (0..<5).collect() == [0, 1, 2, 3, 4]         
	assert (0..5) instanceof List     
	assert (0..5) instanceof groovy.lang.IntRange                  
	assert (0..5).size() == 6  

- **默认的`Range`是包含边界的，即`0..5`是下限为0，上限为5. Groovy提供`<`符号去除边界,但仅适用于上限**

`Range`的实现是轻量级的，这意味着它值存储下限和上限。 任何实现了`Comparable`接口，并且具有`next()`和`previous()`方法(用来确定下一个或上一个条目)的对象 就可以用来创建`Range`。 例如，可以通过以下方式创建一系列字符：

	assert ('a'..'d').collect() == ['a','b','c','d']


## 8.3 `Spaceship`操作符

`Spaceship`操作符`<=>`委托给了`compareTo()`方法

	assert (1 <=> 1) == 0
	assert (1 <=> 2) == -1
	assert (2 <=> 1) == 1
	assert ('a' <=> 'z') == -1

- 返回值大于零表示前者大于后者

- 返回值小于零表示前者小于后者

- 返回值等于零表示俩者相等


## 8.4 下标操作符(`Subscript operator`)

下标操作符是`getAt()`或`putAt()`的一种缩写的符号，具体下标操作符的作用取决于其是在赋值语句的左侧或右侧进行使用
	
	def list = [0,1,2,3,4]
	assert list[2] == 2                         
	list[2] = 4                                 
	assert list[0..2] == [0,1,4]                
	list[0..2] = [6,6,6]                        
	assert list == [6,6,6,3,4]     

- `list[2]`中的`[2]`可以用`.getAt(2)`替代

- `list[2]=4`可以用`list.putAt(2,4)`替代

- **下标操作符同样支持使用`Range`**,即`list[0..2]=[6,6,6]`可以用`list.putAt(0..2,[6,6,6])`替代


将下标运算符对应的实现`getAt()/putAt()`方法进行自定义实现，可以快速解构对象：

	class User {
	    Long id
	    String name
	    def getAt(int i) {                                             
	        switch (i) {
	            case 0: return id
	            case 1: return name
	        }
	        throw new IllegalArgumentException("No such element $i")
	    }
	    void putAt(int i, def value) {                                 
	        switch (i) {
	            case 0: id = value; return
	            case 1: name = value; return
	        }
	        throw new IllegalArgumentException("No such element $i")
	    }
	}
	def user = new User(id: 1, name: 'Alex')                           
	assert user[0] == 1                                                
	assert user[1] == 'Alex'                                           
	user[1] = 'Bob'                                                    
	assert user.name == 'Bob' 

- **任何定义了`getAt()`和`putAt()`方法的对象都可以使用下标操作符**


## 8.5 成员操作符(`Membership operator`)

成员操作符(`in`)的作用等同于调用`isCase()`方法。 对列表使用`in`操作符，相当于调列表的`contains()`方法

	def list = ['Grace','Rob','Emmy']
	assert ('Emmy' in list)    
	assert (list.contains('Emmy'))
	assert (list.isCase('Emmy'))

- 上面三种表达方式都能得到同样的结果

## 8.6 恒等操作符(`Identity operator`)

**在Groovy中，使用恒等运算符`==`测试等式与在Java中是不同。在Groovy中,`==`会调用`equals()`方法对内容进行比较**

- **如果要比较引用的相等性需要使用`is`操作符**

		def list1 = ['Groovy 1.8','Groovy 2.0','Groovy 2.3']        
		def list2 = ['Groovy 1.8','Groovy 2.0','Groovy 2.3']        
		assert list1 == list2                                       
		assert !list1.is(list2)    


## 8.7 强转操作符(`coercion operator`)

**强转操作符(`as`)是类型转换的一种变体，使用它不需要转换前后的类型兼容**,该操作符委托`asType()`方法实现
	
	String x = '123'
	Integer s = (Integer) x        

- 这里会抛出异常`CLassCastException`,但是使用`as`操作符不会抛出异常

		String x = '123'
		Integer s = x as Integer   


当一个对象被强制转换为另一个对象时，除非目标类型与源类型相同，否则强制将返回一个新对象。 强转规则视源类型和目标类型而定，如果没有找到转换规则，强制可能会失败。 

**可以借助`asType()`方法，实现自定义转换规则：**

	class Identifiable {
	    String name
	}
	class User {
	    Long id
	    String name
	    def asType(Class target) {                                              
	        if (target == Identifiable) {
	            return new Identifiable(name: name)
	        }
	        throw new ClassCastException("User cannot be coerced into $target")
	    }
	}
	def u = new User(name: 'Xavier')                                            
	def p = u as Identifiable                                                   
	assert p instanceof Identifiable                                            
	assert !(p instanceof User)     
	
- 创建了一个拥有自定义`asType()`方法的对象`User`,并使用其实例进行类型转换


## 8.8 菱形操作符(`diamond operator`) 

菱形运算符（`<>`）是一个仅用于语法的操作符，用于兼容Java 7中相同名称的操作符

- **它用于指示从声明中推断而来的泛型类型**

		List<String> strings = new LinkedList<>()

在动态Groovy中，这个操作符未被使用。在Groovy的静态类型检查中，它也是可选的，因为无论该操作符是否存在，Groovy类型检查器都会执行类型推断

## 8.9 调用操作符(`call operator`)

调用操作符(`()`)用于隐式调用一个名称为`call`的方法。对于任意定义了`call()`方法的对象，都可以直接使用调用操作符来替代`call()`方法的使用

	class MyCallable {
	    int call(int x) {           
	        2*x
	    }
	}

	def mc = new MyCallable()
	assert mc.call(2) == 4          
	assert mc(2) == 4 
		

# 9. 操作符优先级


Level	|Operator(s)|	Name(s)
---|---|---
1| `new` `()`|object creation, explicit parentheses
 |`()` `{}` `[]`|method call, closure, literal list/map
 |`.` `.&` `.@`|member access, method closure, field/attribute access
 |`?.` `*` `*.` `*:`|safe dereferencing, spread, spread-dot, spread-map
 |`~` `!` `(type)`|bitwise negate/pattern, not, typecast
 |`[]` `++` `--`|list/map/array index, post inc/decrement
2|`**`|power
3|`++` `--` `+` `-`|pre inc/decrement, unary plus, unary minus
4|`*` `/` `%`|multiply, div, remainder
5|`+`  `-`|addition, subtraction
6|`<<` `>>` `>>>` `..` `..<`|left/right (unsigned) shift, inclusive/exclusive range
7|`<` `<=` `>` `>=` `in` `instanceof` `as`|less/greater than/or equal, in, instanceof, type coercion
8|`==` `!=` `<=>`|equals, not equals, compare to
 |`=~` `==~`|regex find, regex match
9|`&`|binary/bitwise and
10|`^`|binary/bitwise xor
11|`|`|binary/bitwise or
12|`&&`|logical and
13|`||`|logical or
14|`? :`|ternary conditional
 |`?:`|elvis operator
15|`=` `**=` `*=` `/=` `%=` `+=` `-=` `<<=` `>>=` `>>>=` `&=` `^=` `|=`|various assignments


# 10. 操作符重载

Groovy允许重载各种运算符:

	class Bucket {
	    int size
	
	    Bucket(int size) { this.size = size }
	
	    Bucket plus(Bucket other) {                     
	        return new Bucket(this.size + other.size)
	    }
	}
- 因为类`Bucket`实现了一个特殊方法`plus()`,所以`Bucket`实例可以直接使用操作符`+`，运算时会执行`plus()`定义的逻辑

		def b1 = new Bucket(4)
		def b2 = new Bucket(11)
		assert (b1 + b2).size == 15   

## 10.1 重点

**除了比较操作符之外，所有Groovy操作符都有一个对应的方法，可以在自己的类中实现**

- 要求:

	**方法必须拥有`public`修饰符，有正确的名称，有正确的参数数量。 参数类型决定了运算符右侧支持的类型**
	
例如:

	Bucket plus(int capacity) {
	    return new Bucket(this.size + capacity)
	}

- `Bucket`中的`plus()`仅支持Integer类型的参数

## 10.2 操作符与其对应的方法


Operator	|Method	|Operator	|Method
:---:|:---:|:---:|:---:
`+`|`a.plus(b)`|`a[b]`|`a.getAt(b)`
`-`|`a.minus(b)`|`a[b] = c`|`a.putAt(b, c)`
`*`|`a.multiply(b)`|`a in b`|`b.isCase(a)`
`/`|`a.div(b)`|`<<`|`a.leftShift(b)`
`%`|`a.mod(b)`|`>>`|`a.rightShift(b)`
`**`|`a.power(b)`|`>>>`|`a.rightShiftUnsigned(b)`
`|`|`a.or(b)`|`++`|`a.next()`
`&`|`a.and(b)`|`--`|`a.previous()`
`^`|`a.xor(b)`|`+a`|`a.positive()`
`as`|`a.asType(b)`|`-a`|`a.negative()`
`a()`|`a.call()`|`~a`|`a.bitwiseNegate()`


	