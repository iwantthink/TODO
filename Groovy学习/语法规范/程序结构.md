# 程序结构

[Program structure](http://www.groovy-lang.org/structure.html#_methods)

# 简介

介绍Groovy程序结构

限定类名指代类名全称，其包含`.`分隔开的包路径

非限定类名即不带包名

# 1. 包名

**包名的作用与Java完全相同。它允许在没有任何冲突的情况下分离代码库。Groovy类必须在类定义之前指定它们的包，否则将使用默认包名**

	// defining a package named com.yoursite
	package com.yoursite

- 与Java定义包名一致

# 2. 导入(`imports`)

为了引用类，就需要拥有一个对类的包的限定引用,否则就在使用类时就需要使用限定类名

- Groovy遵循Java使用`import`语句来解析类引用


例如，Groovy提供了`MarkupBuilder`构建类，该类存在于`groovy.xml`包中，因此需要使用`import`关键字进行导入

	// Sample1.groovy
	import groovy.xml.MarkupBuilder
	def xml = new MarkupBuilder()
	assert xml != null

	// Sample2.groovy
	try{
		def xml  = new MarkupBuilder()
	}catch(e){
		println "unable to resolve class MarkupBuilder "
	}
	
	
## 2.1 默认导入
Groovy会默认导入常用的包:

	import java.lang.*
	import java.util.*
	import java.io.*
	import java.net.*
	import groovy.lang.*
	import groovy.util.*
	import java.math.BigInteger
	import java.math.BigDecimal

## 2.2 简单的导入

简单的导入就是指在`import`语句中使用完整的类名(即包含包名)
	
	// importing the class MarkupBuilder
	import groovy.xml.MarkupBuilder
	
	// using the imported class to create an object
	def xml = new MarkupBuilder()
	
	assert xml != null

## 2.3 星导入(`star import`)

**在`import`语句中，使用`*`符号 + 包名 可以实现导入该包下的全部类,这种方式就叫做星导入**

	import groovy.xml.*
	
	def markupBuilder = new MarkupBuilder()
	
	assert markupBuilder != null
	
	assert new StreamingMarkupBuilder() != null


- 星导入可能导致本地命令控件混乱！因此Groovy提供了别名操作来解决这个问题！

## 2.4 静态导入
**Groovy提供的静态导入功能支持像使用当前类的静态方法一样去使用被导入的静态方法**

	import static Boolean.FALSE
	
	assert !FALSE //use directly, without Boolean prefix!
	
**这与Java相似，但比Java更加的动态，因为它允许拥有不同参数要求但是名称相同的方法**

	// 静态导入方法
	import static java.lang.String.format 
	
	class SomeClass {
		// 与被导入方法拥有相同名称但不同参数类型的方法
	    String format(Integer i) { 
	        i.toString()
	    }
	
	    static void main(String[] args) {
	        assert format('String') == 'String' 
	        assert new SomeClass().format(Integer.valueOf(1)) == '1'
	    }
	}
	
- 上述代码在Java环境中会编译错误，但是Groovy可以正常运行

- **如果具有相同的类型，则导入的类优先**

## 2.5 静态导入别名

**使用`as`关键字,可以解决静态导入的命名空间问题**

假设使用`getInstance()`方法获取`Calendar`实例。 这是一个静态方法，因此可以使用静态导入该方法。 但每次都调用`getInstance()`会产生误导,因此借助`as`在静态导入时设置别名，可以提高代码可读性：


	import static Calendar.getInstance as now
	
	assert now().class == Calendar.getInstance().class

## 2.6 静态星导入

静态星导入将导入给定类中的所有静态方法

假设当前需要使用`java.lang.Math`类中的`sin`和`cos`静态方法,通过静态星导入的实现如下：

	import static java.lang.Math.*
	
	assert sin(0) == 0.0
	assert cos(0) == 1.0

- 可以不借助`Math`直接去调用方法

## 2.7 导入别名

**Groovy提供了类型别名功能，借助`as`符号可以将自定义的名称引用限定的类名**

	import java.util.Date
	import java.sql.Date as SQLDate
	
	Date utilDate = new Date(1000L)
	SQLDate sqlDate = new SQLDate(1000L)
	
	assert utilDate instanceof java.util.Date
	assert sqlDate instanceof java.sql.Date


# 3. 脚本 vs 类
## 3.1 `public static void main `vs 脚本

Groovy支持脚本形式和类形式

	// Main.groovy
	class Main {    
		// 类运行的主入口                                
	    static void main(String... args) {          
	        println 'Groovy world!'                 
	    }
	}
	
上面的代码是典型的Java代码，在Java中代码必须嵌入到类中才能执行.但是Groovy提供了更简单的脚本形式，下面的代码与上面的等效:

	// Main.groovy
	println 'Groovy world!'
	
- 脚本可以被认为是一个需不要声明的类,此外还有一些不同

## 3.2 脚本类

**Groovy编译器会将脚本编译成一个类，并将脚本的主体复制到一个`run`方法中**

因此3.1小节中脚本的示例会生成如下的类:

	// Main.groovy
	import org.codehaus.groovy.runtime.InvokerHelper
	class Main extends Script {                     
	    def run() {                                 
	        println 'Groovy world!'                 
	    }
	    static void main(String[] args) {           
	        InvokerHelper.runScript(Main, args)     
	    }
	}

- `Main`类继承自`groovy.lang.Script`类

- `groovy.lang.Script`类需要一个带返回值的`run()`方法

- 脚本内容都被放到`run()`方法中

- `main()`方法会被自动生成,在该方法内会在`run()`方法上委托脚本执行


**如果脚本位于文件中，则使用该文件的基本名称来确定生成的脚本类的名称。例如文件的名称为`Main.groovy`，则脚本类名称将为`Main`**


## 3.3 方法
**在脚本中允许定义方法**,示例如下:

	int fib(int n) {
	    n < 2 ? 1 : fib(n-1) + fib(n-2)
	}
	assert fib(10)==89

**生成的脚本类中会携带所有的脚本中定义的方法，并将所有脚本主体添加到`run()`方法中**

	println 'Hello'                                 
	
	int power(int n) { 2**n }                       
	
	println "2^6==${power(6)}"  
	
- 上面代码生成的脚本类：

		import org.codehaus.groovy.runtime.InvokerHelper
		class Main extends Script {
		    int power(int n) { 2** n}                   
		    def run() {
		        println 'Hello'                         
		        println "2^6==${power(6)}"              
		    }
		    static void main(String[] args) {
		        InvokerHelper.runScript(Main, args)
		    }
		}	
	
即使Groovy通过脚本生成一个类，它对用户来说也是完全透明的。特别是，脚本被编译成字节码，行号被保留。这意味着，如果在脚本中抛出异常，堆栈跟踪将显示与原始脚本对应的行号，而不是生成的代码中的行号



## 3.4 变量

脚本中的变量不需要指定类型：

	int x = 1
	int y = 2
	assert x+y == 3

等同于以下代码

	x = 1
	y = 2
	assert x+y == 3


实际上这两者之间存在语义上的差异:

1. 如果以形式1 声明变量,那么这些变量是**局部变量(`local variable`)**.编译成字节码之后,会被放到`run()`方法之中,并且这些变量对脚本类的主体是不可见的

2. 如果以形式2 声明变量,这些变量会进入脚本绑定（`groovy.lang.Binding`）. 从方法中可以访问绑定，如果使用脚本与应用程序交互并需要在脚本和应用程序之间共享数据，那么绑定尤为重要.

	查看[INtegration guide](http://docs.groovy-lang.org/latest/html/documentation/guide-integrating.html#_integrating_groovy_in_a_java_application)以获得更加详细的信息


**如果希望变量称为成员变量,且不通过`Binding`这种方式,可以通过添加`@Field`注释来实现**


### 3.4.1 实例

例如：  

	def x = "hello groovy xxx"	
	def y = 'hello groovy yyy'
	def z = 1234565
	def printx(){
   	 	println x
    	println y
    	println z
	}
	printx()

反编译过来的话如下：  

	public Object run()
  	{
   		CallSite[] arrayOfCallSite = $getCallSiteArray(); 	
		Object x = "hello groovy xxx";
    	Object y = "hello groovy yyy";
    	Object z = Integer.valueOf(1234565);

   	 	if ((__$stMC) || (BytecodeInterface8.disabledStandardMetaClass())) return arrayOfCallSite[1].callCurrent(this); else return printx(); return null;
  	}

  	public Object printx()
 	 {
    	CallSite[] arrayOfCallSite = $getCallSiteArray(); arrayOfCallSite[2].callCurrent(this, arrayOfCallSite[3].callGroovyObjectGetProperty(this));
    	arrayOfCallSite[4].callCurrent(this, arrayOfCallSite[5].callGroovyObjectGetProperty(this));
    	return arrayOfCallSite[6].callCurrent(this, arrayOfCallSite[7].callGroovyObjectGetProperty(this)); return null;
  	}


上面的代码中，`printx()`被定义成test类的成员函数，`def x = 1`实际上是在run()当中去定义的，所以`printx()`访问不到变量`x`。解决办法就是：**定义的时候不要添加类型和def**  
如下代码就是 不添加类型和def 反编译类中的run()方法中的代码：  

	    CallSite[] arrayOfCallSite = $getCallSiteArray(); 
		String str = "hello groovy xxx"; 
		ScriptBytecodeAdapter.setGroovyObjectProperty(str, test.class, this, (String)"x");

但是这种方式仍然不是将变量x定义成成员变量,虽然可以令pintx()访问到变量x！解决办法就是：**在x前面加上@Field标注，这样，x就彻彻底底是test的成员变量了**

	import groovy.transform.Field;   //必须要先import  
	@Field x = 1






