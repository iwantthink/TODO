# 语义
[编程语言的操作语义](https://blog.csdn.net/majestyhao/article/details/43834603)

[Semantics](http://www.groovy-lang.org/semantics.html)

# 简介

`syntax `（句法） 和`grammar`(语法)在自然语言下代表句子或短语的组成规则，规定了什么样的句子或短语是合法的

`semantics `(语义/含义) 指句子或短语在符合语法之后所表达的意思，符合语法的句子或短语不见得具有实际意义

举个例子，在自然语言范围内，`The dog is a man!`,这句话语法没有问题，但是语义是错的，因为`dog`不是`man`..

# 1 Statements
## 1.1. Variable definition
变量可以使用具体类型(`String`等)或关键字`def`定义

	String x
	def o

- 这能使得Groovy解析器能够检测到变量定义

- 可以把`def`当做`Object`来理解

## 1.2. Variable assignment
### 1.2.1. Multiple assignment
Groovy支持多重分配，即一次性给多个变量赋值

	def (a, b, c) = [10, 20, 'foo']
	assert a == 10 && b == 20 && c == 'foo'

还能够给变量指定类型
	
	def (int i, String j) = [10, 'foo']
	assert i == 10 && j == 'foo'

除了在声明变量时支持多重分配，已经存在的变量也支持多重分配

	def nums = [1, 3, 5]
	def a, b, c
	(a, b, c) = nums
	assert a == 1 && b == 3 && c == 5
	
该语法适用于列表，数组 以及 返回这俩种类型数据的方法	
	def (_, month, year) = "18th June 2009".split()
	assert "In $month of $year" == 'In June of 2009'

### 1.2.2 Overflow and Underflow
**在多重分配时，如果 变量数 大于 值的数量，那么超出部分的变量会被赋值为`null`.  如果 值的数量大于 变量数, 那么超过部分的值就会被忽略**

	def (a, b, c) = [1, 2]
	assert a == 1 && b == 2 && c == null
	
	def (a, b) = [1, 2, 3]
	assert a == 1 && b == 2

### 1.2.3 Object destructuring with multiple assignment

**通过组合 多重分配 和 自定义下标操作符，可以使用对象解构**

	@Immutable
	class Coordinates {
	    double latitude
	    double longitude
	
	    double getAt(int idx) {
	        if (idx == 0) latitude
	        else if (idx == 1) longitude
	        else throw new Exception("Wrong coordinate index, use 0 or 1")
	    }
	}

	def coordinates = new Coordinates(latitude: 43.23, longitude: 3.67) 
	
	def (la, lo) = coordinates
	assert la == 43.23                                                  
	assert lo == 3.67

- 重载了下标操作符之后，就可以对对象直接使用多重分配	

## 1.3. Control structures
### 1.3.1. Conditional structures
if / else
---
Groovy支持Java常见的`if-else`语法

	def x = false
	def y = false
	
	if ( !x ) {
	    x = true
	}

	if ( x ) {
	    x = false
	} else {
	    y = true
	}

Groovy支持Java中嵌套的`if-else`语法

	if ( ... ) {
	    ...
	} else if (...) {
	    ...
	} else {
	    ...
	}

switch / case
---
Groovy的`switch`语句兼容Java代码

	def x = 1.23
	def result = ""

	switch ( x ) {
	    case "foo":
	        result = "found foo"
	        // lets fall through
	
	    case "bar":
	        result += "bar"
	
	    case [4, 5, 6, 'inList']:
	        result = "list"
	        break
	
	    case 12..30:
	        result = "range"
	        break
	
	    case Integer:
	        result = "integer"
	        break
	
	    case Number:
	        result = "number"
	        break
	
	    case ~/fo*/: // toString() representation of x matches the pattern?
	        result = "foo regex"
	        break
	
	    case { it < 0 }: // or { x < 0 }
	        result = "negative"
	        break
	
	    default:
	        result = "default"
	}

	assert result = "number"

`Switch`支持以下的类型进行对比：

1. 类，判断传入值类型是否符合case
2. 正则表达式，判断传入值`toString`是否符合case
3. 集合，判断传入值是否包含在集合中
4. 闭包，判断闭包返回的值是否为true(实际上传入值会赋值给闭包中的`it`)
5. 值，判断传入值是否等于case


### 1.3.2 Looping structures
Classic for loop
---
Groovy支持Java标准的`for`循环
	
	String message = ''
	for (int i = 0; i < 5; i++) {
	    message += 'Hi '
	}
	assert message == 'Hi Hi Hi Hi Hi '

for in loop
---
Groovy中的`for`循环支持任何类型的数组，集合，Map等

	// iterate over a range
	def x = 0
	for ( i in 0..9 ) {
	    x += i
	}
	assert x == 45
	
	// iterate over a list
	x = 0
	for ( i in [0, 1, 2, 3, 4] ) {
	    x += i
	}
	assert x == 10
	
	// iterate over an array
	def array = (0..4).toArray()
	x = 0
	for ( i in array ) {
	    x += i
	}
	assert x == 10
	
	// iterate over a map
	def map = ['abc':1, 'def':2, 'xyz':3]
	x = 0
	for ( e in map ) {
	    x += e.value
	}
	assert x == 6
	
	// iterate over values in a map
	x = 0
	for ( v in map.values() ) {
	    x += v
	}
	assert x == 6
	
	// iterate over the characters in a string
	def text = "abc"
	def list = []
	for (c in text) {
	    list.add(c)
	}
	assert list == ["a", "b", "c"]

enhanced for
---
**Groovy还支持高级`for`循环**

	def x = "string"
	for (char c:x){
		println c
	}

- **高级`for`循环中的变量类型必须定义**

while loop
---

	def x = 0
	def y = 5
	
	while ( y-- > 0 ) {
	    x++
	}
	
	assert x == 5

### 1.3.3. Exception handling
### 1.3.4. try / catch / finally
Groovy支持三种形式组合,`try-catch-finally`, `try-catch`,`try-finally`

- 每个块都必须使用花括号进行包裹

### 1.3.5. Multi-catch

从Groovy 2.0 开始，支持在一个`catch`块中处理多个异常

	try {
	    /* ... */
	} catch ( IOException | NullPointerException e ) {
	    /* one block to handle 2 exceptions */
	}

## 1.4. Power assertion

断言的形式如下：

	assert [left expression] == [right expression] : (optional message)

- **`optional message`是用来替代默认情况下断言不成立时的输出信息**


## 1.5. Labeled statements
任何语句都能够与一个标签结合,标签不会影响语句的语义，但是能够使得语句更加可读

	given:
	    def x = 1
	    def y = 2
	when:
	    def z = x+y
	then:
	    assert z == 3

**可以通过使用`break`指令指定标签作为跳转目标,但是不建议!!!**

	for (int i=0;i<10;i++) {
	    for (int j=0;j<i;j++) {
	        println "j=$j"
	        if (j == 5) {
	            break exit
	        }
	    }
	    exit: println "i=$i"
	}


# 2. Expressions
## 2.1. GPath expressions
`GPath`是一种集成到Groovy中的路径表达式语言，它允许识别嵌套结构化数据的一部分。

- `GPath`使用点标记法

### 2.1.1. Object navigation
### 2.1.2. Expression Deconstruction
逐步分析`this.class.methods.name.grep(~/.*Bar/)`，并得到`GPath`的评估流程

	this.class

属性访问，相当于`this.getClass()`,得到一个类对象

	this.class.methods

属性访问，相当于`this.getClass().getMethods()`,得到一组方法对象

	this.class.methods.name
	
对每个数组中的元素进行属性访问，得到一组结果

	this.class.methods.name.grep(...)
	
对每个数组中的元素调用`grep()`方法,得到一组结果	

**`GPath`中对一个数组进行属性访问 会被转换成 对数组中的每个元素进行属性访问**


###2.1.3. GPath for XML navigation
# 3. Promotion and coercion
## 3.1. Number promotion
## 3.2. Closure to type coercion
### 3.2.1. Assigning a closure to a SAM type
**将一个闭包通过`as`关键字分配给`SAM`类型(拥有单个抽象方法的类或接口)，从而得到一个拥有具体实现的类**

	interface Predicate<T> {
	    boolean accept(T obj)
	}

	abstract class Greeter {
	    abstract String getName()
	    void greet() {
	        println "Hello, $name"
	    }
	}

	Predicate filter = { it.contains 'G' } as Predicate
	assert filter.accept('Groovy') == true
	
	Greeter greeter = { 'Groovy' } as Greeter
	greeter.greet()

### 3.2.2. Calling a method accepting a SAM type with a closure

将一个闭包通过`as`关键字分配给`SAM`类型(接口，或者拥有单个抽象方法的类)，从而得到一个拥有具体实现的类。并使用它来调用方法

	public <T> List<T> filter(List<T> source, Predicate<T> predicate) {
	    source.findAll { predicate.accept(it) }
	}

	assert filter(['Java','Groovy'], { it.contains 'G'} as Predicate) ==['Groovy']

- **在Groovy 2.2.0之后，可以忽略将闭包进行强转，直接将闭包传递给需要SAM类型参数的方法**.因为使用了闭包，所以闭包相关的语法也可以使用，例如将闭包放在圆括号之外，提高代码可读性

		assert filter(['Java','Groovy']) { it.contains 'G'} == ['Groovy']

### 3.2.3. Closure to arbitrary type coercion

**闭包除了能被强转为`SAM`类型之外，还可以强转到任意类型，例如接口，类**

示例(闭包强转成接口)

	interface FooBar {
	    int foo()
	    void bar()
	}

	def impl = { println 'ok'; 123 } as FooBar

- **如果接口中定义多个方法，那么这个闭包会被所有方法作为实现**

示例(闭包强转成类)

	class FooBar {
	    int foo() { 1 }
	    void bar() { println 'bar' }
	}
	
	def impl = { println 'ok'; 123 } as FooBar
	
	assert impl.foo() == 123
	impl.bar()

## 3.3. Map to type coercion

通常情况下，Groovy通过强转闭包来实现接口或者类，但是在接口或类中存在多个方法时，这种方式会用一个闭包实现多个方法。**为了对每个方法单独设置，Groovy允许将Map映射到类型转换**

- **`Map`中的`key`会被解释为方法名称，而对应的`value`就是方法的实现**

将一个`Map`强转成`Iterator`

	def map
	map = [
	  i: 10,
	  hasNext: { map.i > 0 },
	  next: { map.i-- },
	]
	def iter = map as Iterator

- 正常情况下，只需要将提供需要真正需要实现的方法即可。

**如果`Map`中没有提供指定方法实现，那么在调用时会抛出`MissingMethodException `或`UnsupportedOperationException `**

	interface X {
	    void f()
	    void g(int n)
	    void h(String s, int n)
	}
	
	x = [ f: {println "f called"} ] as X
	x.f() // method exists
	x.g() // MissingMethodException here
	x.g(5) // UnsupportedOperationException here
	
- 如果方法调用时，传入的参数与方法定义的参数不符合，并且方法未定义，那么抛出`MissingMethodException `

- 如果方法调用时，传入的参数与方法定义的参数符合,并且方法未定义，那么抛出`UnsupportedOperationException `
	

## 3.4. String to enum coercion

**Groovy支持将`String`/`GString`强转成`enum`,并且不需要显示的添加`as`关键字**

- **如果字符串与枚举值不能匹配，那么运行时抛出一个`IllegalArgumentException `异常**

示例(`String`)

	enum State {
	    up,
	    down
	}

	State st = 'up'
	assert st == State.up
	
示例(`GString`)

	def val = "up"
	State st = "${val}"
	assert st == State.up
		
示例(不匹配的转换)

	try{
		State st = "not an enum value"
	}catch(e){
		println e
	}
	
**注意，在`switch`语句中，能够对`case`中的字符串进行隐式的转换(如果传入条件是枚举)**

	State switchState(State st) {
	    switch (st) {
	        case 'up':
	            return State.down // explicit constant
	        case 'down':
	            return 'up' // implicit coercion for return types
	    }
	}

- **如果是一个方法需要一个枚举参数，那么必须显示的使用`as`对其进行转换**

## 3.5. Custom type coercion

**通过重载`as`操作符,即重写`asType()`方法，可以自定义类型强转逻辑**

	class Polar {
	    double r
	    double phi
	    
	    def asType(Class target) {
		    if (Cartesian==target) {
		        return new Cartesian(x: r*cos(phi), y: r*sin(phi))
		    }
		}
	}
	class Cartesian {
	   double x
	   double y
	}
	
	def sigma = 1E-16
	def polar = new Polar(r:1.0,phi:PI/2)
	def cartesian = polar as Cartesian
	assert abs(cartesian.x-sigma) < sigma
	
**通过`metaclass `可以对那些没有源代码的类进行改写：**

	Polar.metaClass.asType = { Class target ->
	    if (Cartesian==target) {
	        return new Cartesian(x: r*cos(phi), y: r*sin(phi))
	    }
	}
	

## 3.6. Class literals vs variables and the as operator

`as`关键字只能在拥有一个静态的类引用时才能使用,其他情况下，例如通过反射获取一个类，是不可以使用`as`关键字

示例(`静态类引用`)

	interface Greeter {
	    void greet()
	}
	def greeter = { println 'Hello, Groovy!' } as Greeter // Greeter is known statically
	greeter.greet()

示例(`反射获取`)

	Class clazz = Class.forName('Greeter')
	greeter = { println 'Hello, Groovy!' } as clazz
	// throws:
	// unable to resolve class clazz
	// @ line 9, column 40.
	//   greeter = { println 'Hello, Groovy!' } as clazz
	
- **对于这种形式，必须使用`asType()`对其进行强转**

		greeter = { println 'Hello, Groovy!' }.asType(clazz)
		greeter.greet()	

# 4 Optionality
## 4.1 Optional parentheses

**对于至少有一个参数并且没有歧义的方法，在调用时可以省略圆括号**

	println 'Hello World'
	def maximum = Math.max 5, 10

**如果方法不需要参数，或者省略圆括号可能造成歧义时，必须带上圆括号:**

	println()
	println(Math.max(5, 10))

## 4.2 Optional semicolons
**如果一行代码中只包含一条语句，那么句尾的分号(`;`)可以被省略 。 对于一行代码包含多条语句的情况，必须带上分号`;`**

## 4.3 Optional return keyword

**在Groovy中，默认情况下方法或闭包主体中最后一个表达式的值会被返回。这意味着`return`关键字是可选的**

## 4.4 Optional public keyword

**在Groovy中，如果没有添加修饰符，那么类和方法默认都是`public`**


# 5 The Groovy Truth
Groovy通过应用以下规则来确定一条表达式的值为`true/false`
## 5.1 Boolean expressions

**表达式的值对应布尔值的值，布尔值为`true`，表达式也为`true`**

	assert true
	assert !false

## 5.2 Collections and Arrays
**非空的集合或数组即为`true`**

	assert [1, 2, 3]
	assert ![]

## 5.3 Matchers
**如果正则表达式至少有一个匹配，那么就是`true`**

	assert ('a' =~ /a/)
	assert !('a' =~ /b/)

## 5.4 Iterators and Enumerations

**能够迭代的迭代器，或者拥有值的枚举，即为`true`**

	assert [0].iterator()
	assert ![].iterator()
	Vector v = [0] as Vector
	Enumeration enumeration = v.elements()
	assert enumeration
	enumeration.nextElement()
	assert !enumeration


## 5.5 Maps
**非空的`Map`即为`true`**

	assert ['one' : 1]
	assert ![:]

## 5.6 Strings

**非空的`String`,`GString`,`Char`即为`true`**

	assert 'a'
	assert !''
	def nonEmpty = 'a'
	assert "$nonEmpty"
	def empty = ''
	assert !"$empty"

## 5.7 Numbers
**非零的数字即为`true`**

	assert 1
	assert 3.5
	assert !0

## 5.8 Object References
**非空的对象引用即为`true`**

	assert new Object()
	assert !null
	def b 
	assert b

## 5.9 Customizing the truth with asBoolean() methods

**通过在类中重写`asBoolean()`方法，可以自定义当前类被Groovy解析为`true/false`**

	class Color {
	    String name
	
	    boolean asBoolean(){
	        name == 'green' ? true : false
	    }
	}

	assert new Color(name: 'green')
	assert !new Color(name: 'red')	

# 6 Typing
##6.1. Optional typing

**作为一门动态语言，Groovy支持可选类型，即不显示的声明变量类型,在运行时类型会被动态解析**

	def aString = 'foo'                         
	assert aString.toUpperCase()  

此**外，方法的参数类型以及返回值类型都可以通过`def`来进行声明(即不显示的声明变量类型)**

	String concat(String a, String b) {
	    a+b
	}
	assert concat('foo','bar') == 'foobar'
	
	def concat(def a, def b) {                              
	    a+b
	}
	assert concat('foo','bar') == 'foobar'                  
	assert concat(1,2) == 3     
	
**实际上，在Groovy中可以使用`Object`来替换`def`，俩者完全相等，能够实现同样的功能**	

##6.2. Static type checking

默认情况下，因为Groovy主要是一种动态语言，所以在编译时仅仅执行最低限度的类型检查，大多数的错误都是在运行时抛出

- 通过运行时元编程添加方法，会更改类或对象的运行时行为

		class Person {                                                          
		    String firstName
		    String lastName
		}
		def p = new Person(firstName: 'Raymond', lastName: 'Devos')  
		try{        
		assert p.formattedName == 'Raymond Devos' 
		}catch(e){
			println "groovy.lang.MissingPropertyException: No such property: ormattedName for class: Person"
		}


这种情况对于动态语言来说，这十分常见.添加下面代码到类`Person`的声明下面，即可解决问题

	Person.metaClass.getFormattedName = { "$delegate.firstName $delegate.lastName" }

**一般来说，在Groovy中，除了已经声明过的类型，不能对其他对象的类型进行假设，因为在编译时无法确定会调用什么方法，获取检索什么属性**

但是如果编写的程序不依赖动态特性，获取希望在编译时，如果遇到类似上述属性未定义的情况，抛出异常提示,可以通过`@groovy.lang.TypeChecked`

**激活类型检查，编译器将会做出如下行为：**

- 类型推断被激活，这意味着即使对局部变量使用`def`，类型检查器也能够从赋值中推断变量的类型

- 方法调用在编译时解析，这意味着如果没有在类上声明方法，编译器将抛出错误

- 所有在静态语言中可能出现的编译时错误都可能出现，例如 找不到方法，找不到属性，方法调用不兼容，数字精度错误等等

### 6.2.1 The `@TypeChecked` annotation
Activating type checking at compile time
---
**注解`@groovy.lang.TypeChecked`可以放置在类或者方法上:**

	@groovy.transform.TypeChecked
	class Calculator {
	    int sum(int x, int y) { x+y }
	}
	
	class Calculator {
	    @groovy.transform.TypeChecked
	    int sum(int x, int y) { x+y }
	}

- **放置在类上，所有在类当中的元素都会进行类型检查**

- **放置在方法上，只有方法本身，它包含的闭包/匿名内部类会进行类型检查**

Skipping sections
---

**类型检查的范围可以被限制，例如一个类被注解为需要进行类型检查，但是跳过指定的方法**

	import groovy.transform.TypeChecked
	import groovy.transform.TypeCheckingMode
	
	@TypeChecked                                        
	class GreetingService {
	    String greeting() {                             
	        doGreet()
	    }
	
	    @TypeChecked(TypeCheckingMode.SKIP)             
	    private String doGreet() {
	        def b = new SentenceBuilder()
	        b.Hello.my.name.is.John                     
	        b
	    }
	}
	def s = new GreetingService()
	assert s.greeting() == 'Hello my name is John'
	
- `GreetingService`类被标明为需要进行类型检查，但是`doGreet()`方法被标明忽略，因此会忽略在编译时对`doGreet()`方法中不存在属性/方法的检查
	

### 6.2.2 Type checking assignments

一个`A`类型的对象`o`可以被赋值给一个`T`类型的变量，当且仅当满足如下条件时:

- `T`等于`A`

		Date now = new Date()

- `T`属于`String`,`boolean`,`Boolean`,`Class`

		String s = new Date() // implicit call to toString
		Boolean boxed = 'some string'       // Groovy truth
		boolean prim = 'some string'        // Groovy truth
		Class clazz = 'java.lang.String'    // class coercion

- `o`是一个null,`T`不是原始类型

		String s = null         // passes
		int i = null            // fails
		
- `T`和`A`都是数组，并且`A`的元素类型可以赋值给`B`的元素类型

		int[] i = new int[4]        // passes
		int[] i = new String[4]     // fails

- `T`是数组类型，`A`是集合类型，并且`A`的元素类型可以赋值给`B`的元素类型

		int[] i = [1,2,3]               // passes
		int[] i = [1,2, new Date()]     // fails

- `T`是`A`的父类

		AbstractList list = new ArrayList()     // passes
		LinkedList list = new ArrayList()       // fails

- `T`是`A`所实现的接口

		List list = new ArrayList()             // passes
		RandomAccess list = new LinkedList()    // fails
		
- `T`和`A`都是原始类型，并且它们的包装类型可以赋值

		int i = 0
		Integer bi = 1
		int x = new Integer(123)
		double d = new Float(5f)	
		
- `T`是一个`SAM`类型,`A`继承`groovy.lang.Closure`

		Runnable r = { println 'Hello' }
		interface SAMType {
		    int doSomething()
		}
		SAMType sam = { 123 }
		assert sam.doSomething() == 123
		abstract class AbstractSAM {
		    int calc() { 2* value() }
		    abstract int value()
		}
		AbstractSAM c = { 123 }
		assert c.calc() == 246	

- `T`和`A`来自`java.lang.Number`,并且遵循以下列表

T	|A	|Examples
---|---|---
Double|Any but BigDecimal or BigInteger|Double d1 = 4d
 ||Double d2 = 4f
 ||Double d3 = 4l
 ||Double d4 = 4i
 ||Double d5 = (short) 4
 ||Double d6 = (byte) 4
Float|Any type but BigDecimal, BigInteger or Double|Float f1 = 4f
 ||Float f2 = 4l
 ||Float f3 = 4i
 ||Float f4 = (short) 4
 ||Float f5 = (byte) 4
Long|Any type but BigDecimal, BigInteger, Double or Float|Long l1 = 4l
 ||Long l2 = 4i
 ||Long l3 = (short) 4
 ||Long l4 = (byte) 4
Integer|Any type but BigDecimal, BigInteger, Double, Float or Long|Integer i1 = 4i
 ||Integer i2 = (short) 4
 ||Integer i3 = (byte) 4
Short|Any type but BigDecimal, BigInteger, Double, Float, Long or Integer|Short s1 = (short) 4
 ||Short s2 = (byte) 4
Byte|Byte|Byte b1 = (byte) 4

### 6.2.3 List and map constructors

**可以将list或map类型的`A`赋值给类型为`T`类型的变量,但是需要满足以下要求:**

1. **赋值是一个变量声明，`A`是一个list,`T`拥有一个构造函数，并且其构造函数中的参数类型与list中的元素相符合**

2. **赋值时一个变量声明，`A`是一个map,`T`拥有一个无参构造函数，并且`A`中的所有key都对应一个`A`中的属性**


示例()：

	@groovy.transform.TupleConstructor
	class Person {
	    String firstName
	    String lastName
	}
	Person classic = new Person('Ada','Lovelace')

示例(使用`list constructor`替代)

	Person list = ['Ada','Lovelace']

示例(使用`map constructor`替代)

	Person map = [firstName:'Ada', lastName:'Lovelace']

- **使用`map constructor`默认会去检查map中的key是一一对应类中的属性,如果没有找到则会抛出`GroovyCastException`异常，如果使用了类型检查，那么会在编译时抛出`No such property: age for class: Person`**

		@groovy.transform.TupleConstructor
		class Person {
		    String firstName
		    String lastName
		}
		Person map = [firstName:'Ada', lastName:'Lovelace', age: 24] 

### 6.2.4 Method resolution

在类型检查中，方法在编译时期被解析。解析仅对方法名称和参数进行，返回值在选择方法时无关。

一个`A`类型的对象`o`,可以传给方法中`T`类型的参数，当且仅当满足下面要求：

- `T`等于`A`

		int sum(int x, int y) {
		    x+y
		}
		assert sum(3,4) == 7

- `T`是一个`String`,`A`是一个`GString`

		String format(String str) {
		    "Result: $str"
		}
		assert format("${3+4}") == "Result: 7"

- 对象`o`为null,`T`不是原始类型

		String format(int value) {
		    "Result: $value"
		}
		assert format(7) == "Result: 7"
		format(null)           // fails

- `T`和`A`都是一个数组，并且`A`中的元素可以被赋值给`T`

		String format(String[] values) {
		    "Result: ${values.join(' ')}"
		}
		assert format(['a','b'] as String[]) == "Result: a b"
		format([1,2] as int[])              // fails

- `T`是`A`的父类

		String format(AbstractList list) {
		    list.join(',')
		}
		format(new ArrayList())              // passes
		String format(LinkedList list) {
		    list.join(',')
		}
		format(new ArrayList())              // fails

- `T`是`A`实现的接口
		
		String format(List list) {
		    list.join(',')
		}
		format(new ArrayList())                  // passes
		String format(RandomAccess list) {
		    'foo'
		}
		format(new LinkedList())                 // fails
		
- `T`或者`A`是原始类型，并且`A`的包装类型可以赋值给`T`	
		int sum(int x, Integer y) {
		    x+y
		}
		assert sum(3, new Integer(4)) == 7
		assert sum(new Integer(3), 4) == 7
		assert sum(new Integer(3), new Integer(4)) == 7
		assert sum(new Integer(3), 4) == 7

- `T`继承自`groovy.lang.Closure`,`A`是一个SAM类型(single method type)

		interface SAMType {
		    int doSomething()
		}
		int twice(SAMType sam) { 2*sam.doSomething() }
		assert twice { 123 } == 246
		abstract class AbstractSAM {
		    int calc() { 2* value() }
		    abstract int value()
		}
		int eightTimes(AbstractSAM sam) { 4*sam.calc() }
		assert eightTimes { 123 } == 984

- `T`和`A`都是数字类型，那么它们遵循6.2.2的表格


**如果在编译期间没有找到一个拥有符合的参数以及符合的方法名称 的方法，那么会抛出异常**. 

	class MyService {
	    void doSomething() {
	    	 try{
	    	 	printLine 'Do something'   
	    	 }catch(e){
	    	 	println "只有在调用该方法时，才会捕获异常"
	    	 }      
	    }
	}

- `printLine`是一个错误，但是因为处于动态模式，所以错误不会再编译时被捕获,只有在调用`doSomething()`方法时，才会抛出异常

- 如果想在编译时抛出异常，那么可以通过添加`@groovy.transform.TypeChecked`注解

**类型检查背后的逻辑：**

- 它是在编译时进行检查，因此这时无法知道任何在运行时的元编程。 这意味着如果激活类型检查，一些在原本有效的代码会抛出异常

		class Duck {
		    void quack() {              
		        println 'Quack!'
		    }
		}
		class QuackingBird {
		    void quack() {              
		        println 'Quack!'
		    }
		}
		@groovy.transform.TypeChecked
		void accept(quacker) {
		    quacker.quack()             
		}
		accept(new Duck())  
		
	- `quacker`参数会在运行时决定其具体类型，但是因为添加了类型检查注解，所以会抛出异常	

### 6.2.5 Type inference
Principles
---
**当代码添加了`@TypeCheked`注解后，解析器会在编译器对类型进行推测。它不仅仅依赖于静态类型，而且还使用各种技术来推断变量、返回类型和文字的类型，因此即使激活了类型检查器，代码也尽可能保持干净**

	def message = 'Welcome to Groovy!'              
	println message.toUpperCase()                   
	println message.upper() // compile time error

- `toUpperCase()`方法之所以被执行成功，是因为message变量被推测为`String`


Variables vs fields in type inference
---
**编译器总是会对局部变量进行类型推测，但是不会对字段进行类型推测，总是会使用字段的声明类型**。因此推荐对字段显示的声明类型，对于局部变量则没有特殊要求

	class SomeClass {
	    def someUntypedField                                                                
	    String someTypedField                                                               
	
	    void someMethod() {
	        // 可以分配任何对象给someUntypedField
	        someUntypedField = '123' 
	        // 因为没有被正确的分配类型，所以调用方法失败                                                       
	        someUntypedField = someUntypedField.toUpperCase()  // compile-time error        
	    }
	
	    void someSafeMethod() {
	    	 // 只能分配String 给 someTypedField
	        someTypedField = '123' 
	        // String类型存在toUpperCase() ,因此调用成功                                                         
	        someTypedField = someTypedField.toUpperCase()                                   
	    }
	
	    void someMethodUsingLocalVariable() {
	    	  // 定义局部变量
	        def localVariable = '123' 
	        // 进行类型推测                                                      
	        someUntypedField = localVariable.toUpperCase()                                  
	    }
	}

- **导致上述问题主要原因是线程安全**

- **在编译时期，无法保证任何字段的类型，这是因为任意线程都可以在任意时间访问任意字段**。假设字段可以进行类型推测，那么当A线程访问字段C,然后推测其为`String`类型，那么同一时间B线程修改了字段C的内容，那么就会出现错误

	对于局部变量来说，能够知道它是否"转义"，因此可以确保变量的类型是否是一个常量

	无论字段是否被`final`修饰，JVM都不能保证它是`final`的
	

Collection literal type inference
---
Groovy的三种集合:

- `list`,使用`[]`表示

- `map`,使用`[:]`表示

- `range`,使用`from..to`(包含右边界)，`from..<to`(不包含右边界)

当使用这三种表示方式对集合进行赋值时,编译器会根据具体的表达式来推断类型(即使用`def`声明的变量的类型)


Literal	|Inferred type
---:|:---
`def list = []`| `java.util.List`
`def list = ['foo','bar']` | `java.util.List<String>`
`def list = ["${foo}","${bar}"]` |`java.util.List<GString> be careful, a GString is not a String!`
`def map = [:]`|`java.util.LinkedHashMap`
`def map1 = [someKey: 'someValue']`| 
`def map2 = ['someKey': 'someValue']`| `java.util.LinkedHashMap<String,String>`
`def map = ["${someKey}": 'someValue']`|`java.util.LinkedHashMap<GString,String> be careful, the key is a GString!`
`def intRange = (0..10)`|`groovy.lang.IntRange`
`def charRange = ('a'..'z')`|`groovy.lang.Range<String> : uses the type of the bounds to infer the component type of the range`

- **除了`IntRange`之外，推断出来的类型会使用泛型来描述集合对象的内容，如果集合中的内容不同，那么会采用它们的`Least upper bound`**

Least upper bound(LUB)
---
LUB就是同时可以分配给俩个类型的最小的类型， Groovy中通过如下规则来定义`A`类型和`B`类型的LUB

- `A`和`B`拥有相同的父类

- `A`和`B`实现了相同的接口

- 如果`A`或`B`是原始类型且`A`不等于`B`，则`A`和`B`的`least upper bound`是其包装类型的最小上限

示例:

	class Top {}
	class Bottom1 extends Top {}
	class Bottom2 extends Top {}
	
	assert leastUpperBound(String, String) == String                    
	assert leastUpperBound(ArrayList, LinkedList) == AbstractList       
	assert leastUpperBound(ArrayList, List) == List                     
	assert leastUpperBound(List, List) == List                          
	assert leastUpperBound(Bottom1, Bottom2) == Top                     
	assert leastUpperBound(List, Serializable) == Object  

LUB始终是JVM支持的类型，并且可以添加更加复杂的LUB,下面例子中，`Bottom `和`SerializableFooImpl `的LUB是`Serializable`和`Foo`

	interface Foo {}
	class Top {}
	class Bottom extends Top implements Serializable, Foo {}
	class SerializableFooImpl implements Serializable, Foo {}


instanceof inference
---
Groovy可以使用`instanceof`进行类型判断

	class Greeter {
	    String greeting() { 'Hello' }
	}
	
	void doSomething(def o) {
	    if (o instanceof Greeter) {     
	        println o.greeting()        
	    }
	}
	
	doSomething(new Greeter())

- 因为Groovy动态分配(方法会在运行时选择),所以不需要将对象进行强转之后再调用该对象的方法


Flow typing
---
`Flow typing`是类型推断的扩展，它允许编译器不仅仅在初始化时进行类型推断，还能够在代码流中进行

	@groovy.transform.TypeChecked
	void flowTyping() {
	    def o = 'foo'                       
	    o = o.toUpperCase()                 
	    o = 9d                              
	    o = Math.sqrt(o)                    
	}

- `o=9d`之后，编译器已经知道`o`是一个`double`


Advanced type inference
---

### 6.2.6 Closures and type inference
Return type inference
---
**返回类型推断仅适用于闭包**


Parameter type inference
---
Explicit closure parameters
---
Parameters inferred from single-abstract method types
---
The @ClosureParams annotation
---
@DelegatesTo
---

## 6.3 Static compilation
### 6.3.1 Dynamic vs static
### 6.3.2 The @CompileStatic annotation
### 6.3.3 Key benefits
# 7 Type checking extensions
## 7.1 Writing a type checking extension
### 7.1.1 Towards a smarter type checker
### 7.1.2 The extensions attribute
### 7.1.3 A DSL for type checking
### 7.1.4 Type checking extensions API
AST
Events
### 7.1.5. Working with extensions
Support classes
Class nodes
Helping the type checker
Throwing an error
isXXXExpression
Virtual methods
Scoping
Other useful methods
## 7.2. Advanced type checking extensions
### 7.2.1. Precompiled type checking extensions
### 7.2.2. Using @Grab in a type checking extension
### 7.2.3. Sharing or packaging type checking extensions
### 7.2.4. Global type checking extensions
### 7.2.5. Type checking extensions and @CompileStatic
### 7.2.6. Mixed mode compilation
### 7.2.7. Transforming the AST in an extension
### 7.2.8. Examples