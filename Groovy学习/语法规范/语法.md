# Groovy语法

[Groovy Syntax](http://www.groovy-lang.org/syntax.html)

# 简介

Groovy编程语言的语法源自Java语法，但是使用Groovy的特定构造对其进行了增强，并允许某些简化


# 1. 注释

## 1.1 单行注释

单行注释以`//`开头,直到行尾的字符都被视为注释的一部分

	// a standalone single line comment
	println "hello" // a comment till the end of the line

## 1.2 多行注释

多行注释以`/*`开头,以`*/`结尾，这之间的所有内容都会被当做注释的一部分(包括换行符). 因此多行注释可以放在语句的末尾，或者放在语句的内部！


	/* a standalone multiline comment
	   spanning two lines */
	println "hello" /* a multiline comment starting
	                   at the end of a statement */
	println 1 /* one */ + 2 /* two */

## 1.3 Groovy文档注释

Groovy文档注释以`/**`开头，以`*/`结尾。除了开头了结尾之外的注释行 可以以`*`作为开头！

文档注释与以下内容有关:

1. 类型定义(类，接口，枚举，注解)

2. 字段和属性的定义

3. 方法的定义

虽然编译器不会抱怨Groovy文档注释与上述语言元素没有关联，但是最好的做法是在它之前添加注释

	/**
	 * A Class description
	 */
	class Person {
	    /** the name of the person */
	    String name
	
	    /**
	     * Creates a greeting method for a certain person.
	     *
	     * @param otherPerson the person to greet
	     * @return a greeting message
	     */
	    String greet(String otherPerson) {
	       "Hello ${otherPerson}"
	    }
	}

- `GroovyDoc`与Java的`JavaDoc`拥有同样的约定

## 1.4 Shebang line
**`shebang line`是UNIX系统可以理解的行，它允许脚本直接在命令行中运行，但是前提是已经安装了Groovy发行版，并且Groovy命令在`PATH`上可用**

	#!/usr/bin/env groovy
	println "Hello from the shebang line"

- `#`字符必须是文件的第一个字符，任何缩进都可能导致编译错误

# 2. 关键字
关键字|关键字|关键字|关键字
:---:|:---:|:---:|:---:
as|assert|break|case
catch|class|const|continue
def|default|do|else
enum|extends|false|finally
for|goto|if|implements
import|in|instanceof|interface
new|null|package|return
super|switch|this|throw
throws|trait|true|try
while|


# 3. 标识符

## 3.1 普通标识符
标识符以字母、`$`符号或下划线开头,但是不能以数字开头

字母可以在以下范围内：

- `'a'` to `'z'` (lowercase ascii letter)

- `'A'` to `'Z'` (uppercase ascii letter)

- `'\u00C0'` to `'\u00D6'`

- `'\u00D8'` to `'\u00F6'`

- `'\u00F8'` to `'\u00FF'`

- `'\u0100'` to `'\uFFFE'`


当跟随一个点（`.`）时，所有关键字也是有效的标识符

	foo.as
	foo.break

## 3.2 带引号的标识符

**带引号的标识符可以用在点表达式的点(`.`)之后**，例如，`person.name`表达式的`name`部分可以添加引号变成`person."name"`或`person.'name'`

- 当某些标识符包含Java语言规范所禁止的非法字符，但Groovy却允许在引用时使用这些字符，就可以将标识符放入双引号中进行引用。例如，字符如破折号(`-`)、空格、感叹号等
	
示例：
	
	def map = [:]
	// 包含空格的标识符
	map."an identifier with a space and double quotes" = "ALLOWED"
	// 包含破折号的标识符
	map.'with-dash-signs-and-single-quotes' = "ALLOWED"
	
	assert map."an identifier with a space and double quotes" == "ALLOWED"
	assert map.'with-dash-signs-and-single-quotes' == "ALLOWED"


**Groovy提供了不同类型的字符串文本,并且所有类型的字符串都允许在点之后使用**

	map.'single quote'
	map."double quote"
	map.'''triple single quote'''
	map."""triple double quote"""
	map./slashy string/
	map.$/dollar slashy string/$

`String`和`GStrings`（插值字符串）在用作标识符时存在差异，因为在`Gstrings`中，插值将被替换并生成最终字符串，以评估整个标识符

	def firstname = "Homer"
	map."Simpson-${firstname}" = "Homer Simpson"
	
	assert map.'Simpson-Homer' == "Homer Simpson"


# 4. 字符串

## 4.1 单引号字符串
单引号字符串是普通的`java.lang.String`，不支持插值

	def name = 'ryan'
	def str = 'i am $name'
	assert str == 'i am $name'
	
## 4.2 字符串连接	

所有Groovy字符串都可以通过`+`运算符连接

	assert 'ab' == 'a'+'b'

## 4.3 双引号字符串

**双引号字符串如果没有插值表达式，那么其类型是`java.lang.String`,否则类型是`groovy.lang.GString`**。

- 要在双引号字符串中转义双引号，可以使用反斜杠字符(`/`)

		def str = "A double quote: \""


### 4.3.1 字符串插值

除了单引号字符串和三引号字符串外，任何Groovy表达式都可以插入到所有字符串文本中。插值是在字符串中的占位符，在估值时用具体的值替换字符串中的占位符。占位符表达式被`${}`包围，或者用`$`作为表达式前缀

- 当`GString`被传递给以`String`为参数的方法时，会通过调用该表达式上的`toString()`方法将计算包含占位符的表达式，并以字符串形式表示

	GString类型的字符串在调用`toString()`方法之后就是String类型
	
示例：
	
	def name = 'Guillaume' // a plain string
	def greeting = "Hello ${name}"
	
	assert greeting.toString() == 'Hello Guillaume'	assert greeting.toString().class == String.class
	
- 插值中包含任何Groovy表达式都是有效的，例如算术表达式：

		def sum = "The sum of 2 and 3 equals ${2 + 3}"
		assert sum.toString() == 'The sum of 2 and 3 equals 5'
	
**`$ {}`占位符之间不仅允许表达式，还允许语句**。但是因为语句的值是`null`，所以如果在该占位符中插入了多个语句，则最后一个语句应返回一个有意义的值以便插入。 例如，`${def a = 1; def b = 2; a + b}`用来表示1+2的和

- **建议在`GString`的占位符中仅使用简单表达式**

	
**除了使用`${}`占位符之外，还可以使用一个单独的`$`符号作为点表达式的前缀**

	def person = [name: 'Guillaume', age: 36]
	assert "$person.name is $person.age years old" == 'Guillaume is 36 years old'
		
- **这种形式只对`a.b`，`a.b.c`等形式的点表达式才有效，无法对包含括号的表达式（如方法调用，闭包的花括号或算术运算符）生效**

	以下语句将抛出`groovy.lang.MissingPropertyException`，因为Groovy认为当前在尝试访问该数字的`toString`属性，然而该属性不存在：

		shouldFail(MissingPropertyException) {
		    println "$number.toString()"
		}

	- `"$number.toString()"`可以理解为，解析器将其解释为`"${number.toString}()"`


### 4.3.2 嵌入式闭包

**当占位符包含一个箭头`${->}`时，表达式实际上是一个闭包表达式，可以将它看作一个前面带`$`符号的闭包**
	
	def sParameterLessClosure = "1 + 2 == ${-> 3}" 
	assert sParameterLessClosure == '1 + 2 == 3'
	
	def sOneParamClosure = "1 + 2 == ${ w -> w << 3}" 
	assert sOneParamClosure == '1 + 2 == 3'
	
- 第一个闭包是一个没有参数的闭包，它不接受参数

- 第二个闭包接受一个`java.io.StringWriter`类型的参数，并在后续的表达式中通过`<<`操作符往其内写入内容

	**以上俩种情况，这俩个闭包都是嵌入式闭包**


`${->}`比纯粹的`${}`有一个优势，就是延迟求值`lazy evalution`

		def number1 = 123.456	
		// 占位符
		def eagerGstring = "value = ${number1}"
		// 嵌入式闭包
		def lazyGstring = "value = ${->number1}"
		assert eagerGstring == "value = 123.456"
		assert lazyGstring  == "value = 123.456"
		number1 = 2
		assert eagerGstring == "value = 123.456"
		assert lazyGstring == "value = 2"

- **在`Gstring`中的嵌入式闭包至多只允许一个参数,超过一个之后会在运行时抛出异常**

### 4.3.3 与Java的互通性

当一个方法（无论是用Java还是Groovy实现）需要一个`java.lang.String`类型的参数，但传递一个`groovy.lang.GString`示例时，`GString`的`toString()`方法会被自动且透明地调用

	String takeString(String message) {         
	    assert message instanceof String        
	    return message
	}

	def message = "The message is ${'hello'}"   
	assert message instanceof GString           
	
	def result = takeString(message)            
	assert result instanceof String
	assert result == 'The message is hello'

### 4.3.4 GString和String的hash值

**普通Java字符串是不可变的，而`GString`的结果字符串会根据其内插值的变化而变化。即使对于拥有相同内容的`GString`和`String`也没有相同的hashCode**

	assert "one: ${1}".hashCode() != "one: 1".hashCode()

因为`GString`和`String`拥有不同的hashcode.因此尽量避免使用`GString`作为`Map`的key，尽量使用`String`作为`Map`的key

	def key = "a"
	def m = ["${key}": "letter ${key}"]     
	
	assert m["a"] == null       
	
	
## 4.4 三重单引号字符串

三重单引号字符串是由三组单引号包围的一系列字符，其就是普通的`java.lang.String`,不支持插值
 
	 '''a triple single quoted string'''

支持多行：

	def aMultilineString = '''line one
	line two
	line three'''

如果代码是缩进的，例如在类方法的主体中，则字符串将包含缩进的空格。`GDK`提供了`stripIndent()`方法来剥离缩进，以及`stripMargin()`方法使用分隔符来标识要从字符串开头删除的文本
	
	def startingAndEndingWithANewline = '''
	line one
	line two
	line three
	'''

- `startingAndEndingWithANewline `字符串的第一个字符是一个换行符，可以通过反斜线转义换行符以实现删除换行符的操作

		def strippedFirstNewline = '''\
		line one
		line two
		line three
		'''
		assert !strippedFirstNewline.startsWith('\n')


### 4.4.1 转义特殊字符

可以使用反斜杠字符转义单引号，以避免终止字符串文字

	'an escaped single quote: \' needs a backslash'

可以使用双反斜杠来转义转义字符本身

	'an escaped escape character: \\ needs a double backslash'



Escape sequence	|Character
---|---
`\t`|tabulation
`\b`|backspace
`\n`|newline
`\r`|carriage return
`\f`|formfeed
`\\`|backslash
`\'`|single quote (for single quoted and triple single quoted strings)
`\"`|double quote (for double quoted and triple double quoted strings)


### 4.4.2 Unicode转义序列

对于键盘上不存在的字符，可以使用unicode转义序列:一个反斜杠，后跟“u”，然后是4个十六进制数字

	'The Euro currency symbol: \u20AC'



## 4.5 三重双引号字符串

三引号字符串类似于双引号字符串，**不支持插值**，区别是内容支持随意换行，并且在三重双引号中，**单引号和双引号 不需要转义**

	def name = 'Groovy'
	def template = """
	    Dear Mr ${name},
	
	    You're the winner of the lottery!
	
	    Yours sincerly,
	
	    Dave
	"""
	
	assert template.toString().contains('Groovy')
	

## 4.6 斜线字符串

斜线字符串使用正斜线(`/`)作为分隔符，适用于定义正则表达式和patterns,**因为不需要转义反斜线（允许不转义的带上反斜线），但是正斜线需要转义**
	
	def fooPattern = /.*foo.*/
	assert fooPattern == '.*foo.*'
	
正斜线是需要被转义的:

	def escapeSlash = /The character \/ is a forward slash/
	assert escapeSlash == 'The character / is a forward slash'

斜线字符串支持多行：

	def multilineSlashy = /one
	    two
	    three/

	assert multilineSlashy.contains('\n')

斜线字符串也允许使用插值:

	def color = 'blue'
	def interpolatedSlashy = /a ${color} car/
	
	assert interpolatedSlashy == 'a blue car'
	
空斜线字符串不能使用双斜线表示,这会被Groovy解析器当做单行注释	
**如果直接使用println 输出斜线字符串，不能使用反斜线`\`**	

## 4.7 美元斜线字符串

美元斜线字符串是用开头`$/`结尾`/$`为分隔的多行`GString`。转义字符是`$`符号，它可以转义`$`或正斜杠(`/`)。但是`$`和正斜杠默认都不需要转义，除非 出现类似`GString`占位符序列的不字符串(不使用占位功能，仅输出)，或者出现类似美元斜线字符串结尾的符号`/$`

	def name = "Guillaume"
	def date = "April, 1st"
	
	def dollarSlashy = $/
	    Hello $name,
	    today we're ${date}.
	
	    $ dollar sign
	    $$ escaped dollar sign
	    \ backslash
	    / forward slash
	    $/ escaped forward slash
	    $$$/ escaped opening dollar slashy
	    $/$$ escaped closing dollar slashy
	/$
	
	assert [
	    'Guillaume',
	    'April, 1st',
	    '$ dollar sign',
	    '$ escaped dollar sign',
	    '\\ backslash',
	    '/ forward slash',
	    '/ escaped forward slash',
	    '$/ escaped opening dollar slashy',
	    '/$ escaped closing dollar slashy'
	].every { dollarSlashy.contains(it) }


## 4.8 字符串总结表

![](http://ww1.sinaimg.cn/large/6ab93b35gy1fm02ms78yxj20ko0a4wei.jpg)

## 4.9 字符

与Java不同，Groovy没有明确的字符文字。 但是，可以通过三种不同的方式明确地将Groovy字符串设置为字符：
	
-  通过指定保存字符的变量为`char`类型

		char c1 = 'A' 
		assert c1 instanceof Character

-  通过对`as`操作符进行类型强制转换

		def c2 = 'B' as char 
		assert c2 instanceof Character

-  通过`(char)`形式进行类型强转
	
		def c3 = (char)'C' 
		assert c3 instanceof Character

# 5. Numbers

Groovy支持不同类型的整数和小数，由Java的`Number`类型支持


作为动态语言，**Groovy世界中的所有事物都是对象**。所以，int，boolean这些Java中的基本数据类型，在Groovy代码中其实对应的是它们的包装数据类型。比如int对应为Integer，boolean对应为Boolean。

	def int x = 1
	println x.getClass().getCanonicalName()//java.lang.Integer

## 5.1 整数

Groovy支持的整数类型与Java一致：

- `byte`

- `char`

- `short`

- `int`

- `long`
	
- `java.lang.BigInteger`

可以通过以下形式创建不同类型的整数:

	// primitive types
	byte  b = 1
	char  c = 2
	short s = 3
	int   i = 4
	long  l = 5
	
	// infinite precision
	BigInteger bi =  6

**使用`def`定义整数，变量的类型会根据类型的容量去适应这个整数值**
		
	def a = 1
	assert a instanceof Integer
	
	// Integer.MAX_VALUE
	def b = 2147483647
	assert b instanceof Integer
	
	// Integer.MAX_VALUE + 1
	def c = 2147483648
	assert c instanceof Long
	
	// Long.MAX_VALUE
	def d = 9223372036854775807
	assert d instanceof Long
	
	// Long.MAX_VALUE + 1
	def e = 9223372036854775808
	assert e instanceof BigInteger

这个规则同样适用于负数：

	def na = -1
	assert na instanceof Integer

	// Integer.MIN_VALUE
	def nb = -2147483648
	assert nb instanceof Integer

	// Integer.MIN_VALUE - 1
	def nc = -2147483649
	assert nc instanceof Long

	// Long.MIN_VALUE
	def nd = -9223372036854775808
	assert nd instanceof Long

	// Long.MIN_VALUE - 1
	def ne = -9223372036854775809
	assert ne instanceof BigInteger

### 5.1.1 不同进制的数字

整数可以以二进制，八进制，十六进制，十进制进行表示

二进制数以`0b`作为前缀

	int xInt = 0b10101111
	assert xInt == 175

八进制数以`0`作为前缀

	int xInt = 077
	assert xInt == 63
	
十六进制以`0x`作为前缀	

	int xInt = 0x77
	assert xInt == 119

## 5.2 小数

Groovy的小数类型与Java一致：

- `float`

- `double`

- `java.lang.BigDecimal`


可以使用以下方式创建不同类型的小数:

	// primitive types
	float  f = 1.234
	double d = 2.345
	
	// infinite precision
	BigDecimal bd =  3.456

小数可以使用exponents(指数)形式表达，通过`e`或`E`来表示

	assert 1e3  ==  1_000.0
	assert 2E4  == 20_000.0
	assert 3e+1 ==     30.0
	assert 4E-2 ==      0.04
	assert 5e-1 ==      0.5



**Groovy默认使用`java.lang.BigDecimal`作为默认的小数类型**

- 但是Groovy同样支持`float`和`double`，使用这俩种类型需要手动指定类型，强制转换，或者使用后缀

## 5.3 在数字中使用下划线

**允许在数字中使用下划线`_` 增加数字可阅读性**

	long reditCardNumber = 123456_789
	assert reditCardNumber == 123456789

## 5.4 数字类型的后缀

通过给数字添加一个后缀，可以强制一个数字（包括二进制，八进制和十六进制）具有特定类型


数字类型|后缀
---|---
BigInteger|`G` or `g`
Long | `L` or `l`
Integer | `I` or `i`
BigDecimal | `G` or `g`
Double | `D` or `d`
Float| `F` or `f`

	// 可以通过添加后缀 指定数字类型
	assert 1i.class == Integer
	assert 1i.class != Long



## 5.5 数学运算

讨论一下除了除法和次方运算:

1. **`byte, char, short` 和 `int` 互相进行二元运算，结果都是 `int`**

2. `long` 和 `(byte, char, short and int)` 进行二元运算，结果都是long

3. `BigInteger` 和任何整数类型进行计算，结果都是 `BigInteger`

4. `BigDecimal` 和 `(byte, char, short, int and BigInteger )`进行计算，结果是`BigDecimal`

5. `float, double ` 和 `BigDecimal`互相进行二元运算，结果是`double`

6. 俩个`BigDecimal`计算结果还是 `BigDecimal`

**规则总结如图：**

![](http://ww1.sinaimg.cn/large/6ab93b35gy1fm03wh0vrsj20n80f83yq.jpg)

- **由于Groovy的运算符重载，所以运算符可以直接作用于`BigInteger`和`BigDecimal`类型的数据,而不需要通过特定的方法去操作**

### 5.5.1 除法运算符

当操作数是`float`或`double`，则除法运算符`/`（`/=`用于相除并赋值)计算的结果是`double`

当俩个操作数是整数类型中的`short`,`char`,`byte`,`int`,`long`,`BigInteger`或`BigDecimal`中的任意组合时，则除法运算符`/`（和/ =用于除法和赋值)计算的结果是`BigDecimal`


	 assert (4/3).class == BigDecimal
	 assert (4d/3).class == Double
	 assert (4f/3).class == Double
	 assert (4l/3).class == BigDecimal

**Groovy不提供专用的整除运算符号**！只能通过`intdiv()`函数
		
	assert 6.intdiv(5)==1

### 5.5.2 幂运算(`power operator`)

幂运算符号是`**` ，拥有俩个参数基数(`base `)和指数(`exponent `)，表达式为:`基数**指数`

Groovy的幂运算使用以下规则来确定结果类型

- **如果指数是小数** 

	1. 如果可以表示为 `Integer ` 那就返回 `Integer `
	2. 如果可以表示为`Long` ,那就返回`Long`
	3. 否则的话统一返回`Double`
		
			assert 2**0.1 instanceof Double
			assert 2**-0.1 instanceof Double
			assert 1**-0.3f instanceof Integer
			assert 9.9**1.9 instanceof Double

- **如果指数是整数**
	- 负整数: 按照数值类型是否满足其容量，返回`Integer`,`Long` 或 `Double`

			assert 10**-1 instanceof Double
			assert 1**-1 instanceof Integer

	- 正整数或零:根据基数分类

		1. 如果基数是`BigDecimal`那么返回 `BigDecimal`
		2. 如果基数是`BigInteger`那么返回`BigInteger`
		3. 如果基数是`Integer` 	那么返回`Integer `，当数据放不下时  就返回 `BigInteger`
		4. 如果基数是`Long`   	那么返回`Long` ， 当数据放不下时  就返回`BigInteger`
			
				assert new BigDecimal(10) ** 0 instanceof BigDecimal
				assert new BigInteger(10) ** 1 instanceof BigInteger
				assert 10i ** 1 instanceof Integer
				assert 10i ** 10 instanceof BigInteger
				assert 10l ** 10 instanceof Long
				assert 10l ** 100 instanceof BigInteger


# 6. 布尔值

Boolean是一种特殊的数据类型，拥有俩个值:`true`和`false` . 可以存储在变量中，分配到字段中，就像任何其他数据类型一样

	def myBooleanVariable = true
	boolean untypedBooleanVar = false
	booleanField = true

更复杂的布尔表达式可以用逻辑运算符表示


此外，Groovy有[特殊的规则](http://docs.groovy-lang.org/latest/html/documentation/core-semantics.html#Groovy-Truth)用于强制非布尔对象为布尔值


# 7. 列表
Groovy使用逗号分隔开的值列表(由方括号`[]`包围)来表示`list`

- Groovy中的列表是JDK的`java.util.List`,因为Groovy没有定义自己的集合类

- 定义列表时使用的具体列表实现是`java.util.ArrayList`，这是默认值

示例:

	def numbers = [1, 2, 3]         
	
	assert numbers instanceof List  
	assert numbers instanceof ArrayList
	assert numbers.size() == 3   

列表中不仅可以包含相同类型的元素，还可以包含不同类型的元素

	def heterogeneous = [1, "a", true]  
	

默认情况下，Grvooy的列表是`java.util.ArrayList`类型，但是通过`as`操作符，显示声明变量类型或强制类型转换 可以将列表转换成不同类型

	def arrayList = [1, 2, 3]
	assert arrayList instanceof java.util.ArrayList
	
	def linkedList = [2, 3, 4] as LinkedList    
	assert linkedList instanceof java.util.LinkedList
	
	LinkedList otherLinked = [3, 4, 5]          
	assert otherLinked instanceof java.util.LinkedList

## 7.1 使用索引访问

列表支持使用索引来对值进行读取或设置，索引有俩种类型：正索引和负索引(后者反向访问)

	def letters = ['a', 'b', 'c', 'd']
	
	assert letters[0] == 'a'     
	assert letters[1] == 'b'
	
	assert letters[-1] == 'd'    
	assert letters[-2] == 'c'

	letters[2] = 'C'             
	assert letters[2] == 'C'

列表支持左移操作符`<<`添加元素：

	letters << 'e'               
	assert letters[ 4] == 'e'
	assert letters[-1] == 'e'

列表支持范围访问：

	// 一次性访问俩个索引
	assert letters[1, 3] == ['b', 'd'] 
	// 一次性访问三个索引         
	assert letters[2..4] == ['C', 'd', 'e']   

Groovy支持创建多维列表(将列表作为列表的元素):
	
	def multi = [[0, 1], [2, 3]]     
	assert multi[1][0] == 2    

# 8. 数组

**Groovy为数组重用了列表表示法，但是为了创建数组，还需要通过强制类型转换或类型声明来确定定义的是数组**

	String[] arrStr = ['Ananas', 'Banana', 'Kiwi']  
	
	assert arrStr instanceof String[]    
	assert !(arrStr instanceof List)
	
	def numArr = [1, 2, 3] as int[]      
	
	assert numArr instanceof int[]       
	assert numArr.size() == 3

Groovy支持创建多维数组:

	def matrix3 = new Integer[3][3]         
	assert matrix3.size() == 3
	
	Integer[][] matrix2                     
	matrix2 = [[1, 2], [3, 4]]
	assert matrix2 instanceof Integer[][]
	
- **允许定义数组的边界，也允许不定义数组的边界**	

**对数组元素的访问遵循与列表相同的表示法:**

	String[] names = ['Cédric', 'Guillaume', 'Jochen', 'Paul']
	assert names[0] == 'Cédric'     
	
	names[2] = 'Blackdrag'          
	assert names[2] == 'Blackdrag'
	
	

Groovy不支持Java的数组初始化，因为花括号可能会被Groovy误解为闭包符号
	
	// Java中的数组初始化
	String[] arrays = {1,2,3};
	
	
# 9. Map

在其他语言中，`Map`有时被称为字典或关联数组。`Map`的作用是将键与值相关联，其使用冒号(`:`)分隔键和值，使用逗号(`,`)分隔每个键/值对，使用方括号`[]`包含所有键值对

	def colors = [red: '#FF0000', green: '#00FF00', blue: '#0000FF']   
	
	assert colors['red'] == '#FF0000'    
	assert colors.green  == '#00FF00'    
	
	colors['pink'] = '#FF00FF'           
	colors.yellow  = '#FFFF00'           
	
	assert colors.pink == '#FF00FF'
	assert colors['yellow'] == '#FFFF00'
	
	assert colors instanceof java.util.LinkedHashMap

- 当使用键的名称时，实际上是在`Map`中定义了字符串类型的键

- Groovy创建的`Map`其实际上是`java.util.LinkedHashMap`类型的实例

如果尝试访问一个在`Map`中不存在的键时,会返回一个`null`

	assert colors.unknown == null
	
Groovy不仅支持字符串作为键，还支持其他类型作为键:

	def numbers = [1: 'one', 2: 'two']
	
	assert numbers[1] == 'one'	
	
- **使用数字作为key时，必须使用 `map.[key]`形式进行访问**	
	
## 9.1 变量作为键时的特殊情况

如果想要传递一个变量来作为键，那么该变量本身会转换为字符串并被作为键使用，而不是该变量对应的值作为键

	def key = 'name'
	def person = [key: 'Guillaume']
	// 不包含'name'字符串      
	assert !person.containsKey('name')   
	// 包含`key`
	assert person.containsKey('key')  	
	

Groovy支持使用带引号的字符串作为键，如果需要使用无效的关键字作为标识符，那么必须使用带引号形式的字符串作为键

	def m = ["street-name": "Main street"]	
**当需要强制使用变量的值为`Map`的键，则可以通过 用圆括号将变量或表达式括起来来实现**
	
	person = [(key): 'Guillaume']        
	
	assert person.containsKey('name')    
	assert !person.containsKey('key')  

	