# 闭包

[closures](http://www.groovy-lang.org/closures.html)

# 简介

Groovy中的闭包(`Closure`)是一个开放的、匿名的代码块，它可以接受参数、返回值并分配给变量。闭包可以引用在其周围声明的变量

与闭包的正式定义相反，Groovy语言中的闭包还可以包含定义在闭包周围范围之外的自由变量。在打破闭包的形式概念的同时，它提供了本章描述的各种优势


# 1. 语法

## 1.1 定义闭包

闭包的定义语法如下：

	{ [closureParameters -> ] statements }

-  **其中`[closureParameters->]`是一个以逗号分隔开的可选参数列表，`statements`即零条或者多条Groovy语句**

- **参数列表类似于方法参数列表，可以是类型化(`typed`)的或非类型化(`untyped`)的**

- 当指定参数列表时，需要使用`->`字符，用于将参数和闭包体分离


示例：

	// 引用了变量item的闭包
	{ item++ }                                          
	
	// 可接受参数为空的闭包
	{ -> item++ }                                       
	// 使用隐式参数it 的闭包
	{ println it }                                      
	// 声明了显示参数it 的 闭包
	{ it -> println it }                                
	// 声明了显示参数name的闭包
	{ name -> println name }                            
	// 接收俩个类型化参数的闭包
	{ String x, int y ->                                
	    println "hey ${x} the value is ${y}"
	}
	// 包含多条语句的闭包
	{ reader ->                                         
	    def line = reader.readLine()
	    line.trim()
	}

## 1.2 作为对象的闭包

**闭包就是`groovy.lang.Closure`类的实例,尽管它是一个代码块，但是可以分配给变量或者字段**
	
	def listener = { e -> println "Clicked on $e.source" }      
	assert listener instanceof Closure
	Closure callback = { println 'Done!' }                      
	Closure<Boolean> isTextFile = {
	    File it -> it.name.endsWith('.txt')                     
	}

- **通过往闭包添加泛型，可以指定闭包返回值的类型!**

## 1.3 调用闭包

**闭包作为匿名代码块，可以像其他方法一样调用**

### 1.3.1 闭包的使用步骤

1. 定义一个不接收参数的闭包

		def code = { 123 }

2. 通过使用变量来实现闭包的调用，可以当做常规方法进行处理

		assert code() == 123

	- 闭包内的代码只会在你调用闭包时执行

3. 或者可以通过显示的执行闭包的`call()`方法来调用闭包

		assert code.call() == 123


如果闭包接收参数，那么调用原理也相同:

	// 接收参数i 的闭包
	def isOdd = { int i -> i%2 != 0 }                           
	assert isOdd(3) == true                                     
	assert isOdd.call(2) == false                               
	// 接收隐式参数it的闭包
	def isEven = { it%2 == 0 }                                  
	assert isEven(3) == false                                   
	assert isEven.call(2) == true   

**与方法不同，闭包在调用时总是有一个返回值**

# 2. 闭包的参数

## 2.1 普通参数

闭包的参数遵循与常规方法的参数相同的原则：

- 参数类型(可选)

- 参数名称

- 参数默认值(可选)

- 参数之间使用逗号(`,`)分隔

示例：

	def closureWithOneArg = { str -> str.toUpperCase() }
	assert closureWithOneArg('groovy') == 'GROOVY'

	def closureWithOneArgAndExplicitType = { String str -> str.toUpperCase() }
	assert closureWithOneArgAndExplicitType('groovy') == 'GROOVY'

	def closureWithTwoArgs = { a,b -> a+b }
	assert closureWithTwoArgs(1,2) == 3

	def closureWithTwoArgsAndExplicitTypes = { int a, int b -> a+b }
	assert closureWithTwoArgsAndExplicitTypes(1,2) == 3

	def closureWithTwoArgsAndOptionalTypes = { a, int b -> a+b }
	assert closureWithTwoArgsAndOptionalTypes(1,2) == 3
	
	def closureWithTwoArgAndDefaultValue = { int a, int b=2 -> a+b }
	assert closureWithTwoArgAndDefaultValue(1) == 3


## 2.2 隐式参数

**当闭包没有显式定义参数列表时，闭包总是定义一个名为`it`的隐式参数(不包括使用`->`指定无参数)**

	def greeting = { "Hello, $it!" }
	assert greeting('Patrick') == 'Hello, Patrick!'

- 与下面的代码完全相等

		def greeting = { it -> "Hello, $it!" }
		assert greeting('Patrick') == 'Hello, Patrick!'
		
		
**Groovy支持声明不接受任何参数的闭包**

	def magicNumber = { -> 42 }
	// 调用失败，因为闭包不接受任何参数
	magicNumber(11)
	

## 2.3 可变参数

**Groovy支持为闭包声明可变参数**
	
	def concat1 = { String... args -> args.join('') }           
	assert concat1('abc','def') == 'abcdef'                     
	def concat2 = { String[] args -> args.join('') }            
	assert concat2('abc', 'def') == 'abcdef'
	
	def multiConcat = { int n, String... args ->                
	    args.join('')*n
	}
	assert multiConcat(2, 'abc','def') == 'abcdefabcdef'
		
- **调用声明了可变参数的闭包时，可以传递任意参数，并且无需将参数包装到数组中**

- **将参数声明为数组可以实现将参数声明为可变参数同样的功能**

- **可变参数必须作为参数列表的最后一个参数**

# 3. 代理策略

## 3.1 闭包与lambda表达式

Groovy定义闭包为`Closure`类的实例，这与java 8 中的`lambda`表达式有些不同。 其中Groovy拥有一个`lambda`没有的核心概念：**代理(`Delegation`)**

- **更改闭包的代理或更改闭包的代理策略使得Groovy可以设计出漂亮的领域特定语言（`DSL`）**


## 3.2 `Owner`,`delegate` 和 `this`

**要理解闭包中的代理概念，必须要先理解闭包中的`this`。实际上闭包中定义了三个不同的代理：**

1. `this`

	**对应于定义闭包的外部类(`enclosing class`)**
	
2. `owner`

	**对应于定义闭包的外部对象，该对象可以是类，也可以是闭包**
	
3. `delegate`

	**对应于一个三方对象，每当闭包解析到不存在的方法或属性时，就会交给`delegate`**

	
### 3.2.1 `this`的含义

**闭包中的`this`属性会返回直接拥有闭包的类对象(`该类直接拥有闭包`),此外调用`getThisObject ()`方法能够实现同等效果**

1. **闭包定义在外部类中，此时`this`返回外部类实例**

		class Enclosing {
		    void run() {
		    	 // 定义在Enclosing类中的闭包
		        def whatIsThisObject = { getThisObject() } 
		        // 调用闭包会返回定义闭包的类对象         
		        assert whatIsThisObject() == this
		        // 可以在闭包中使用this 替代getThisObject()方法                   
		        def whatIsThis = { this }                           
		        assert whatIsThis() == this                         
		    }
		}
	
2. 	**闭包定义在内部类中，此时`this`返回的是内部类实例**
	
		class EnclosedInInnerClass {
		    class Inner {
		        Closure cl = { this }                               
		    }
		    void run() {
		        def inner = new Inner()
		        assert inner.cl() == inner                          
		    }
		}
	
3. **在外部类中存在嵌套闭包的闭包，此时里层闭包的`this`返回的是外部类实例!**

		class NestedClosures {
		    void run() {
		        def nestedClosures = {
		            def cl = { this }                               
		            cl()
		        }
		        assert nestedClosures() == this                     
		    }
		}


示例(在方法中调用闭包)

	class Person {
	    String name
	    int age
	    String toString() { "$name is $age years old" }
	
	    String dump() {
	        def cl = {
	            String msg = this.toString()               
	            println msg
	            msg
	        }
	        cl()
	    }
	}
	def p = new Person(name:'Janice', age:74)
	assert p.dump() == 'Janice is 74 years old'

- 实际上闭包`cl`内的`this`指代的是`Person`类实例，因此`Person`类的`toString()`方法会被执行

### 3.2.2 `owner`的含义

**闭包中的`owner`属性将返回直接拥有闭包的对象，该对象可以是闭包或者类。 此外调用`getOwner()`方法能够实现同等效果**

1. **闭包定义在外部类中，此时`owner`返回外部类实例**

		class Enclosing {
		    void run() {
		        def whatIsOwnerMethod = { getOwner() }               
		        assert whatIsOwnerMethod() == this                   
		        def whatIsOwner = { owner }                          
		        assert whatIsOwner() == this                         
		    }
		}
	
2. 	**闭包定义在内部类中，此时`owner`返回的是内部类实例**
	
		class EnclosedInInnerClass {
		    class Inner {
		        Closure cl = { owner }                               
		    }
		    void run() {
		        def inner = new Inner()
		        assert inner.cl() == inner                           
		    }
		}
	
3. **在外部类中存在嵌套闭包的闭包，此时内部闭包的`owner`返回的是外部闭包实例!**
	
		class NestedClosures {
		    void run() {
		        def nestedClosures = {
		            def cl = { owner }                               
		            cl()
		        }
		        assert nestedClosures() == nestedClosures            
		    }
		}

### 3.2.3 `delegate`的含义

`delegate`指的是闭包会使用的一个用户自定义对象,Groovy通过`delegate`属性或`getDelegate()`方法获得该对象

- `delegate`是Groovy构建DSL的一个重要概念

**默认情况下，`delegate`属性指代的对象是`owner`**

	class Enclosing {
	    void run() {
	        def cl = { getDelegate() }                          
	        def cl2 = { delegate }                              
	        assert cl() == cl2()                                
	        assert cl() == this                                 
	        def enclosed = {
	            { -> delegate }.call()                          
	        }
	        assert enclosed() == enclosed                       
	    }
	}

**闭包的`delegate`可以指代任何对象**

下面通过一个示例说明:

	class Person {
	    String name
	}
	class Thing {
	    String name
	}
	
	def p = new Person(name: 'Norman')
	def t = new Thing(name: 'Teapot')

定义一个闭包，借助`delegate`来访问`name`属性

	def upperCasedName = { delegate.name.toUpperCase() }

通过修改`delegate`指代的对象，获取的`name`属性值也会有所区别

	upperCasedName.delegate = p
	assert upperCasedName() == 'NORMAN'
	upperCasedName.delegate = t
	assert upperCasedName() == 'TEAPOT'
	
闭包的词法范围内使用一个`target`变量也能实现同样的功能

	def target = p
	def upperCasedNameUsingVar = { target.name.toUpperCase() }
	assert upperCasedNameUsingVar() == 'NORMAN'
	
- 但是存在以下几点差异

	1. `target`是闭包引用的一个局部变量

	2. `delegate`可以透明的使用，也就是说不需要明确通过`delegate`来调用方法	

### 3.2.4 解析策略

每当在闭包中访问属性而不没有设置接收器对象(`getter`)时，那么就会涉及解析策略

	class Person {
	    String name
	}
	def p = new Person(name:'Igor')
	def cl = { name.toUpperCase() }                 
	cl.delegate = p                                 
	assert cl() == 'IGOR'   

- 闭包`cl`中调用的属性`name`不是闭包词法范围内的变量,可以改变闭包的代理为`Person`类实例


**闭包提供了多种解析策略:**

1. `Closure.OWNER_FIRST`

	- **默认闭包解析策略**

	- **优先从`owner`中寻找方法或属性，其次再去`delegate`中寻找**

2. `Closure.DELEGATE_FIRST`

	- **优先从`delegate `中寻找方法或属性，其次再去`owner`中寻找**

3. `Closure.OWNER_ONLY`

	- **只会从`owner`中寻找方法或属性,`delegate`会被忽略**

4. `Closure.DELEGATE_ONLY`

	- **只会从`delegate `中寻找方法或属性,`delegate`会被忽略**

5. `Closure.TO_SELF`

	- 被需要进行高级元编程开发的开发者使用，提供自定义的代理策略

	- 解析不会在`owner`或`delegate`上执行，而只在闭包类本身上执行

	- **这只要在实现了`Closure`类子类时才有意义**


举例`OWNER_FIRST`

	class Person {
	    String name
	    def pretty = { "My name is $name" }             
	    String toString() {
	        pretty()
	    }
	}
	class Thing {
	    String name                                     
	}
	
	def p = new Person(name: 'Sarah')
	def t = new Thing(name: 'Teapot')
	
	assert p.toString() == 'My name is Sarah'           
	p.pretty.delegate = t                               
	assert p.toString() == 'My name is Sarah' 

- 闭包`p.pretty`的代理被设置为实例`t`之后，并不影响`name`值得获取，因为它会优先从`owner`中获取，如果`owner`不存在才会去`delegate`中获取


- **闭包的`resolveStrategy`属性可以用来修改闭包的解析策略**

		p.pretty.resolveStrategy = Closure.DELEGATE_FIRST
		assert p.toString() == 'My name is Teapot'


下面的例子展示了`delegate first`和`delegate only`,以及`owner first`和`owner only`的区别:

	class Person {
	    String name
	    int age
	    def fetchAge = { age }
	}
	class Thing {
	    String name
	}
	
	def p = new Person(name:'Jessica', age:42)
	def t = new Thing(name:'Printer')
	def cl = p.fetchAge
	cl.delegate = p
	assert cl() == 42
	cl.delegate = t
	assert cl() == 42
	cl.resolveStrategy = Closure.DELEGATE_ONLY
	cl.delegate = p
	assert cl() == 42
	cl.delegate = t
	try {
	    cl()
	    assert false
	} catch (MissingPropertyException ex) {
	    // "age" is not defined on the delegate
	}

# 4. GString中的闭包

查看如下例子

	def x = 1
	def gs = "x = ${x}"
	assert gs == 'x = 1'

	x = 2
	assert gs != 'x = 2'
	
- 在上面的例子中，GString根据一个引用`x`变量的表达式创建的。

	当GString创建时，变量`x`的值为1，此时Gstring使用该值进行创建. 当断言触发时，GString被计算，并通过`toString()`方法转换成String

	后续将变量`x`改成2,但因为它和1不是同一个对象，而GString仍然会使用旧的对象(即1)
	
- **GString只有在引用的值发生变化时才会更改其`toString()`表示的值。如果引用发生更改，则什么也不会发生**

		class Person {
		    String name
		    String toString() { name }          
		}
		def sam = new Person(name:'Sam')        
		def lucy = new Person(name:'Lucy')
		// 指定p 引用 sam对象      
		def p = sam                             
		def gs = "Name: ${p}"                   
		assert gs == 'Name: Sam'
		// 修改p 引用 lucy对象                
		p = lucy            
		// 此时gs中对p的引用不会改变                    
		assert gs == 'Name: Sam'
		// 如果修改了p原始引用对象的值,那么gs就会发生变化了                
		sam.name = 'Lucy'                       
		assert gs == 'Name: Lucy'   


**如果需要在GString中使用一个真正的闭包，例如强制对变量进行延迟计算,需要使用`${->}`这种语法**

	def x = 1
	def gs = "x = ${-> x}"
	assert gs == 'x = 1'
	
	x = 2
	assert gs == 'x = 2'


**如果不想通过使用对象来实现延迟计算，那么最好的变法就是在GString中使用闭包**

	class Person {
	    String name
	    String toString() { name }
	}
	def sam = new Person(name:'Sam')
	def lucy = new Person(name:'Lucy')
	def p = sam
	// Create a GString with lazy evaluation of "p"
	def gs = "Name: ${-> p}"
	assert gs == 'Name: Sam'
	p = lucy
	assert gs == 'Name: Lucy'
	
# 5. 闭包的强转

闭包可以转换成一个接口或拥有单个抽象方法的抽象类.详细内容查看[closure-coercion](http://docs.groovy-lang.org/latest/html/documentation/core-semantics.html#closure-coercion)	

# 6. 函数式编程

闭包是Groovy函数式编程核心，一些函数式编程操作可以直接在闭包上使用


## 6.1 Currying

在Groovy中，`currying`是指部分应用的概念。它与函数式编程中`currying`的真实概念不符，因为Groovy对闭包应用了不同的作用域规则

- 在Groovy中进行`Currying`操作将设置闭包的一个参数的值，并且它将返回一个接收参数少一个的新闭包


### 6.1.1 Left currying

`Left currying`就是返回一个已经设置了最左边参数的闭包

	def nCopies = { int n, String str -> str*n }    
	def twice = nCopies.curry(2)                    
	assert twice('bla') == 'blabla'                 
	assert twice('bla') == nCopies(2, 'bla') 

### 6.1.2 Right currying

`Right currying`就是返回一个已经设置了最右边参数的闭包

	def nCopies = { int n, String str -> str*n }    
	def blah = nCopies.rcurry('bla')                
	assert blah(2) == 'blabla'                      
	assert blah(2) == nCopies(2, 'bla')      

### 6.1.3 Index based currying

如果闭包接受超过2个参数，则可以使用ncurry设置任意位置的参数：

	def volume = { double l, double w, double h -> l*w*h }    
	// 设置第二个参数  
	def fixedWidthVolume = volume.ncurry(1, 2d)                 
	assert volume(3d, 2d, 4d) == fixedWidthVolume(3d, 4d)
	// 从第二参数开始设置，连续设置俩个参数       
	def fixedWidthAndHeight = volume.ncurry(1, 2d, 4d)          
	assert volume(3d, 2d, 4d) == fixedWidthAndHeight(3d)  
	
	
	
## 6.2 Memoization

## 6.3 Composition

闭包组合对应于函数组合的概念，即通过组合两个或多个函数（链接调用）来创建新函数，如下例所示：

	def plus2  = { it + 2 }
	def times3 = { it * 3 }
	// 将times3 闭包作为参数 传入 plus2 闭包
	def times3plus2 = plus2 << times3
	assert times3plus2(3) == 11
	assert times3plus2(4) == plus2(times3(4))
	// 将plus2 闭包作为参数 传入 times3 闭包
	def plus2times3 = times3 << plus2
	assert plus2times3(3) == 15
	assert plus2times3(5) == times3(plus2(5))
	
	// 反向组合 与 plus2<< times3 一样
	assert times3plus2(3) == (times3 >> plus2)(3)
	
## 6.4 Trampoline

递归算法通常受最大堆栈高度限制。 例如，如果调用递归调用自身的方法太深，则最终会收到`StackOverflowException`

- 这种情况下，通过闭包以及闭包的`Trampoline`功能能够解决

通过`TrampolineClosure`类对普通闭包进行包装，在调用时,`TrampolineClosure `会调用被包装的闭包来获取结果

- 如果调用的结果的另一个`TrampolineClosure `实例，也许是因为调用`trampoline（）`方法而创建的，则将再次调用`Closure`.不断的重复调用直到返回除`TrampolineClosure `之外的值。这个值将成为最终结果. 通过这种形式，使得调用是连续进行的，而不是填充堆栈

		def factorial
		factorial = { int n, def accu = 1G ->
		    if (n < 2) return accu
		    factorial.trampoline(n - 1, n * accu)
		}
		factorial = factorial.trampoline()
		
		assert factorial(1)    == 1
		assert factorial(3)    == 1 * 2 * 3
		assert factorial(1000) // == 402387260.. plus another 2560 digits	
		

## 6.5 方法指针

Groovy支持将常规方法作为闭包使用。例如，可能会希望对一个方法使用`currying`功能，但是方法是没有这个功能的，那么就可以通过方法指针运算符从方法获取闭包[method-pointer-operator](http://docs.groovy-lang.org/latest/html/documentation/core-operators.html#method-pointer-operator)