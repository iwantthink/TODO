# 面向对象

[Object Orientation](http://www.groovy-lang.org/objectorientation.html)


# 1. 类型

## 1.1 原始类型

Groovy支持Java语言标准的原始类型

- 整数类型:`byte`(8 bit), `short` (16 bit), `int` (32 bit) 和 `long`(64 bit)

- 浮点类型: `float` (32 bit) and `double` (64 bit)

- 布尔类型:`boolean`(`true`/`false`)

- 字符类型:`char`(16 bit),可用作数字类型，表示UTF-16编码


当Groovy声明字段或变量为原始类型时，它会被会自动包装对基本类型的引用(因为Groovy中的一切都是对象)

### 1.1.1 原始类型的包装类

Primitive type	|Wrapper class
:---:|:---:
boolean|Boolean
char|Character
short|Short
int|Integer
long|Long
float|Float
double|Double

示例(使用`int`)

	class Foo {
	  static int i
	}
	
	assert Foo.class.getDeclaredField('i').type == int.class
	assert Foo.i.class != int.class && Foo.i.class == Integer.class

- 实际上，Groovy使用运算符时不会每次都进行拆箱和装箱，因为Groovy会将操作符编译成对应的方法调用

- 此外Groovy在调用一个接收原始类型参数的Java方法时会自动将参数拆箱为原始类型，同时会自动对原始类型的返回值进行装箱

## 1.2 类

Groovy类与Java类相似，并且与JVM级别的Java类兼容

- Groovy类可以拥有方法，字段和属性

- **类和类的成员可以拥有与Java相同的修饰符(`public`,`protected`,`private`,`static`等),不过还是会有一些区别**


Groovy类与Java类的主要区别:

- **Groovy的类或方法如果没有显示的声明修饰符，那么默认的是`public`(Groovy提供了一个特殊的注解来实现包可见性)**

- **Groovy的字段如果没有显示的声明修饰符，那么默认会被当做属性处理**

- **Groovy类的名称可以与源文件的文件的名称不相同**

- **Groovy的一个源文件可以保存多个类，但是如果存在一些代码不在类中，那么该源文件可能会被认为是一个脚本**

	**脚本仅仅是具有特殊约定的类，并且与源文件拥有相同的名称(因此，不要在脚本中定义与源文件拥有相同名称的类，这会导致冲突)**
	
	
	
示例：

	class Person {                       
	
	    String name                      
	    Integer age
	
	    def increaseAge(Integer years) { 
	        this.age += years
	    }
	}


### 1.2.1 普通类(`Normal class`)

普通类指的是顶层的并且有具体实现的类。 这意味着它们可以在没有任何其他类或脚本限制的情况下实例化。 因此，它们只能是公共的（即使可以抑制public关键字）

- **类通过使用new关键字调用其构造函数来实例化类**

		def p = new Person()

### 1.2.2 内部类(`Inner class`)

**内部类指的是定义在另外一个类内部的类**

- **外部类可以直接使用内部类，而内部类可以访问外部类中的成员(即使成员是私有的)**

- **除了外部类之外的类不允许访问内部类**

示例：

	class Outer {
	    private String privateStr
	
	    def callInnerMethod() {
	        new Inner().methodA()       
	    }
	
	    class Inner {                   
	        def methodA() {
	            println "${privateStr}." 
	        }
	    }
	}

使用内部类的几种原因:

- 内部类无法被其他类直接访问(除了直接的外部类),这将增加封装性

- 将一组指定的内部类添加到同一个外部类中将提供良好的代码结构

- 仅在使用内部类的地方定义内部类，易于维护代码


**在某种情况下，内部类是接口的实现，并且需要提供给外部类使用**

	class Outer2 {
	    private String privateStr = 'some string'
	
	    def startThread() {
	       new Thread(new Inner2()).start()
	    }
	
	    class Inner2 implements Runnable {
	        void run() {
	            println "${privateStr}."
	        }
	    }
	}

- 定义`Inner2`内部类仅仅是为了提供外部类中`Thread`类所需的参数的实现，其实可以使用匿名内部类来消除这种代码冗余

		class Outer3 {
		    private String privateStr = 'some string'
		
		    def startThread() {
		        new Thread(new Runnable() {      
		            void run() {
		                println "${privateStr}."
		            }
		        }).start()                       
		    }
		}
		
	- 使用匿名内部类完全可以替换掉那种仅使用了一次的内部类


### 1.2.3 抽象类

抽象类表示通用概念，因此，它们无法实例化。 抽象类的成员包括字段/属性和抽象或具体方法。 抽象方法没有具体实现，必须由子类去实现

	abstract class Abstract {         
	    String name
	
	    abstract def abstractMethod() 
	
	    def concreteMethod() {
	        println 'concrete'
	    }
	}

- 抽象类/方法必须使用`abstract`关键字声明

抽象类与接口的区别:

1. 抽象类可以拥有字段/属性，以及拥有具体实现的方法，但是接口只能拥有抽象方法

2. 同一个类只能继承一个抽象类，但是能实现多个接口

## 1.3 接口

接口定义了类需要遵循的约定，其仅定义了一组需要被实现的方法，但是不提供方法的具体实现
	
	interface Greeter {                                         
	    void greet(String name)                                 
	}

接口中的方法必须是`public`,如果使用`protected`或`private`等修饰符进行修饰，则会报错

如果在类的`implements`列表中或父类中定义了接口，那么就表示该类实现了这个接口

	class SystemGreeter implements Greeter {                    
	    void greet(String name) {                               
	        println "Hello $name"
	    }
	}
	
	def greeter = new SystemGreeter()
	assert greeter instanceof Greeter  
	
	
对于一个与接口拥有相同方法的类来说，并且该类没有实现接口， Groovy允许通过`as`强转操作符在运行时将其转成接口类型

	class DefaultGreeter {
	    void greet(String name) { println "Hello" }
	}
	greeter = new DefaultGreeter()    
	assert !(greeter instanceof Greeter)                          
	coerced = greeter as Greeter                                
	assert coerced instanceof Greeter
	
- `greeter `对象并没有实现接口，`coerced`对象代理了`greeter`对象

## 1.4 构造函数

**构造函数是用于初始化具有特定状态的对象的特殊方法**

- 与普通方法一样，只要每个构造函数具有唯一的类型签名，类就可以声明多个构造函数

- 如果没有手动提供空参数的构造函数，Groovy编译器将提供一个


Groovy支持俩种形式的构造函数调用

1. 使用位置参数调用构造函数

2. 使用命名参数调用构造函数(允许在调用构造函数时设置指定名称的参数)

### 1.4.1 位置参数(`Positional parameters`)

使用位置参数来创建构建，那么对应的类中就要存在一个或多个构造函数，并且每个构造函数的类型签名必须唯一

**通常情况下，如果声明了至少一个构造函数，那么类就只能通过这些构造函数来实例化,这种情况下无法使用命名参数进行实例化**

使用位置参数调用构造函数有三种形式:

1. 使用`new`关键字

2. 使用`as`关键字进行强转

3. 在赋值时进行强转


示例：

	class PersonConstructor {
	    String name
	    Integer age
	
	    PersonConstructor(name, age) {          
	        this.name = name
	        this.age = age
	    }
	}
	
	def person1 = new PersonConstructor('Marie', 1)  
	def person2 = ['Marie', 2] as PersonConstructor  
	PersonConstructor person3 = ['Marie', 3]


### 1.4.2 命名参数(`Named parameters`)

**Groovy仅在三种情况下可以使用命名参数来调用构造函数**

1. 类包含一个无参构造函数(或者类没有构造函数)

2. 类包含一个仅接受`Map`类型参数的构造函数(需要在构造函数中对变量赋值)

3. 类包含一个首参数为`Map`类型的多参数构造函数(需要在构造函数中对变量赋值)


示例：

	class PersonWOConstructor {                                  
	    String name
	    Integer age
	}
	
	def person4 = new PersonWOConstructor()                      
	def person5 = new PersonWOConstructor(name: 'Marie')         
	def person6 = new PersonWOConstructor(age: 1)                
	def person7 = new PersonWOConstructor(name: 'Marie', age: 2)
	
	
**注意:**	

1. 如果类拥有无参构造函数或无构造函数， 使用命名参数调用构造函数去创建对象时，Groovy会自动调用每个参数对应的`setter`

2. 如果类的构造函数为`Map`类型，并且仅有一个参数(或者拥有多个参数，但是第一个参数类型为`Map`),那么在使用命名参数调用构造函数去创建对象时，需要手动在构造函数中对属性进行设置

3. 可以通过同时提供无参构造函数和带位置参数的构造函数来支持在构造函数中同时使用位置参数和命名参数


## 1.5 方法

### 1.5.1 方法的定义

方法可以定义返回值类型，或者通过`def`关键字来使得返回值没有指定类型

- 方法可以接收任意个数参数，并且参数类型可以未指定

- Groovy中的方法可以使用Java的修饰符，如果没有提供修饰符，那么默认方法就是`public`

Groovy中的方法总是会返回值

- 如果没有使用`return`关键字指定返回值，那么方法的最后一行计算出的值会被返回

		def someMethod() { 'method called' }                           
		String anotherMethod() { 'another method called' }             
		def thirdMethod(param1) { "$param1 passed" }                   
		static String fourthMethod(String param1) { "$param1 passed" }
		
		
		
### 1.5.2 命名参数

Groovy提供了一种惯例写法来支持使用命名参数形式调用方法，那就是使用`Map`类型的参数作为方法的第一个参数

- 在方法内部就可以通过这个`Map`类型的参数来使用传入的参数

- 如果方法只有一个`Map`参数，那么所有在方法中被使用的参数都需要在命名参数列表中声明
	
**位置参数和命名参数可以混合使用**
	
- 使用要求:

	1. **`Map`类型参数作为方法的第一个参数，后续可以添加位置参数**.如果不作为第一个参数，那么不能使用命名参数来调用方法(只能作为普通的`map`类型参数传递)

			def foo(Integer number, Map args) { "${args.name}: ${args.age}, and the number is ${number}" }
			// 抛出异常!!!!!!!!!!!!
			foo(name: 'Marie', age: 1, 23)  
			// 传递普通的Map类型参数即可避免异常
			foo(23, [name: 'Marie', age: 1])

	2. **位置参数必须按照顺序调用，命名参数不需要**

- 示例:
		
		def foo(Map args, Integer number) { "${args.name}: ${args.age}, and the number is ${number}" }
		foo(name: 'Marie', age: 1, 23)  
		foo(23, name: 'Marie', age: 1)  

### 1.5.3 默认参数

默认参数使得参数可选，如果不提供参数，那么就会使用默认值

	def foo(String par1, Integer par2 = 1) { [name: par1, age: par2] }
	assert foo('Marie').age == 1
	
- 如果存在默认参数，那么就不允许定义强制参数	

### 1.5.4 可变参数

Groovy支持方法拥有可变长度的参数.通过`def foo(T …​ args)`形式定义

	def foo(Object... args) { args.length }
	assert foo() == 0
	assert foo(1) == 1
	assert foo(1, 2) == 2
	
- 方法`foo()`接收任意个数的参数(包括零个)

- **使用数组作为参数可以实现同样的功能**

如果传递一个`null`给接收可变参数为参数的方法，那么参数就是`null`

	def foo(Object... args) { args }
	assert foo(null) == null

如果传递数组给一个接收可变参数为参数的方法，那么该可变参数就是这个数组

	def foo(Object... args) { args }
	Integer[] ints = [1, 2]
	assert foo(ints) == [1, 2]

在调用方法时，如果同时存在拥有指定数量的方法 和 拥有可变参数的方法，那么优先会调用拥有指定数量的方法
	
	def foo(Object... args) { 1 }
	def foo(Object x) { 2 }
	assert foo() == 1
	assert foo(1) == 2
	assert foo(1, 2) == 1

### 1.5.5 Method selection algorithm

### 1.5.6 异常声明

Groovy会同等对待检查异常和非检查异常，即可以不声明对异常的捕获,即使该方法可能抛出异常

	def badRead() {
	    new File('doesNotExist.txt').text
	}
	
	shouldFail(FileNotFoundException) {
	    badRead()
	}

- 可以通过声明`throws Exception`来表示方法可能抛出异常，但是这只能作为注释来对待，该方法仍然可以不在检查时处理异常

		def badRead() throws FileNotFoundException {
		    new File('doesNotExist.txt').text
		}
		
		shouldFail(FileNotFoundException) {
		    badRead()
		}


## 1.6 字段和属性

### 1.6.1 字段

**字段是类的成员，并且具有以下特征：**

1. 必须有访问修饰符(`public`, `protected`, `private`)

2. 零个或多个可选的修饰符(`static`,`final`,`synchronized`)

3. 可选的类型(字段可以不声明其类型)

		class BadPractice {
		    private mapping                         
		}
		class GoodPractice {
			// 拥有强类型
		    private Map<String,String> mapping      
		}

4. 必须拥有名称


示例：
	
	class Data {
	    private int id                                  
	    protected String description                    
	    public static final boolean DEBUG = false       
	}

**字段可以在声明时直接初始化:**

	class Data {
	    private String id = IDGenerator.next() 
	    // ...
	}
	
### 1.6.2 属性

属性是类外部可见的特征(可以在外部访问属性)，Groovy中的属性具有以下特征

1. 不允许有访问修饰符(`public`,`protected`,`private`)

2. 零个或多个可选的修饰符(`static`,`final`,`synchronized`)

3. 可选的类型(属性可以不声明其类型)

4. 必须拥有名称

**Groovy会自动为属性添加`getter/setter`,在类外部通过名称访问属性时，Groovy会自动调用其`getter/setter`方法，但是如果在类内部访问属性，则会直接访问属性本身!**

- **如果给属性添加`final`修饰符，那么不会生成对应的`setter`方法**

- 直接访问支持字段的行为是为了防止在定义属性的类中 使用属​​性访问语法时可能造成的堆栈溢出

示例：

	class Person {
	    String name
	    void name(String name) {
	    	 // this.name 直接访问属性本身
	        this.name = "Wonder$name"       
	    }
	    String wonder() {
	    	 // this.name 访问属性本身
	        this.name                       
	    }
	}
	def p = new Person()
	// 在类外部访问属性，调用setter
	p.name = 'Marge'        
	// 在类外部访问属性,调用getter                
	assert p.name == 'Marge' 
	// 直接调用name方法               
	p.name('Marge')                         
	assert p.wonder() == 'WonderMarge' 
	

**使用`meta`字段`properties`可以获取对象实例中的属性列表**

	class Person {
	    String name
	    int age
	}
	def p = new Person()
	assert p.properties.keySet().containsAll(['name','age'])	

**按照惯例，即使没有支持字段(`backing field`)，Groovy也可以识别属性，前提是有遵循Java bean规范的getter或setter**. 例如：


	class PseudoProperties {
	    // a pseudo property "name"
	    void setName(String name) {}
	    String getName() {}
	
	    // a pseudo read-only property "age"
	    int getAge() { 42 }
	
	    // a pseudo write-only property "groovy"
	    void setGroovy(boolean groovy) {  }
	}
	def p = new PseudoProperties()
	p.name = 'Foo'                      
	assert p.age == 42                  
	p.groovy = true   

-  这种语法糖是Groovy编写的许多DSL的核心

## 1.7 注解(`Annotation`)

### 1.7.1 注解定义

注解是一种专用于注解代码元素的特殊接口

- 注解的父类接口是`Annotation`

- 使用`@interface`关键字声明注释，形式如下：

		@interface SomeAnnotation {}


注解中可以定义方法，并且该方法的主体和默认值都是可选的。以下为可能的成员类型：

1. 原始类型

2. 字符串类型

3. 类（类和闭包）

4. 枚举

5. 另外一个类型的注解

6. 或者是上面的类型所组成的数组

示例：

	@interface SomeAnnotation {
	    String value()                          
	}
	@interface SomeAnnotation {
		// 设置了默认值
	    String value() default 'something'      
	}
	@interface SomeAnnotation {
	    int step()                              
	}
	@interface SomeAnnotation {
	    Class appliesTo()                       
	}
	@interface SomeAnnotation {}
	@interface SomeAnnotations {
	    SomeAnnotation[] value()                
	}
	enum DayOfWeek { mon, tue, wed, thu, fri, sat, sun }
	@interface Scheduled {
	    DayOfWeek dayOfWeek()                   
	}

- **Groovy中的注解可能更改语义**

### 1.7.2 注解的位置

注解可能应用于代码中的任意元素(方法，类，变量)

	@SomeAnnotation                 
	void someMethod() {
	    // ...
	}
	
	@SomeAnnotation                 
	class SomeClass {}
	
	@SomeAnnotation String var  
	
Groovy提供了`Target`注解用来指定注解的作用域:

	import java.lang.annotation.ElementType
	import java.lang.annotation.Target
	
	@Target([ElementType.METHOD, ElementType.TYPE])     
	@interface SomeAnnotation {}   

- [ElementType enumeration](http://docs.oracle.com/javase/8/docs/api/index.html?java/lang/annotation/ElementType.html)列出了可以使用的注解作用域，但是java8中的`TYPE_PARAMETER `和`TYPE_USE `是不支持的


### 1.7.3 注解成员的值
**在使用注解时，必须要对所有没有默认值得注解成员进行设置**

	@interface Page {
	    int statusCode()
	}
	
	@Page(statusCode=404)
	void notFound() {
	    // ...
	}
	
如果注解中的成员只有一个需要设置值时，可以忽略 成员名称+等号 (即`value=`)

	@interface Page {
	    String value()
	    int statusCode() default 200
	}
	
	@Page(value='/home')                    
	void home() {
	    // ...
	}
	// 忽略了成员名称+等号
	@Page('/users')                         
	void userList() {
	    // ...
	}
	
	@Page(value='error',statusCode=404)     
	void notFound() {
	    // ...
	}
	
### 1.7.4 保留政策(`Retention Policy`)

注解的可见度取决于保留政策(指定注解用于源码，字节码，运行时),Groovy提供注解`Retention`来设置保留政策

	import java.lang.annotation.Retention
	import java.lang.annotation.RetentionPolicy
	
	@Retention(RetentionPolicy.SOURCE)                   
	@interface SomeAnnotation {}    

- [RetentionPolicy](https://docs.oracle.com/javase/8/docs/api/index.html?java/lang/annotation/RetentionPolicy.html)列出了所有的保留政策

- 通常保留政策是用来指定注解用于编译时或运行时

### 1.7.5 作为注解参数的闭包
**Groovy允许闭包作为注解参数，因此注解可以与各种表达式一起使用**

例如某个注解仅在符合指定jdk版本或os时执行

	@Retention(RetentionPolicy.RUNTIME)
	@interface OnlyIf {
	    Class value()                    
	}

	class Tasks {
	    Set result = []
	    void alwaysExecuted() {
	        result << 1
	    }
	    @OnlyIf({ jdk>=6 })
	    void supportedOnlyInJDK6() {
	        result << 'JDK 6'
	    }
	    @OnlyIf({ jdk>=7 && windows })
	    void requiresJDK7AndWindows() {
	        result << 'JDK 7 Windows'
	    }
	}

- `Class`类型的成员即支持闭包作为参数

下面是上述例子的使用示例:

	class Runner {
	    static <T> T run(Class<T> taskClass) {
	    	  // 创建类实例
	        def tasks = taskClass.newInstance()
	        // 模拟条件                                         
	        def params = [jdk:6, windows: false] 
	        // 遍历类实例的方法                                       
	        tasks.class.declaredMethods.each { m ->   
	        	  // 方法为public,并且没有参数                                  
	            if (Modifier.isPublic(m.modifiers) && m.parameterTypes.length == 0) {   
	            		// 获取注解
	                def onlyIf = m.getAnnotation(OnlyIf)  
	                // 非空                              
	                if (onlyIf) {
	                		// 创建注解实例
	                    Closure cl = onlyIf.value().newInstance(tasks,tasks) 
	                    // 设置代理对象           
	                    cl.delegate = params
	                    // 执行闭包，如果符合闭包条件则执行方法                                          
	                    if (cl()) {                                                     
	                        m.invoke(tasks)                                             
	                    }
	                } else {
	                	   // 没有注解 那么直接可以执行方法
	                    m.invoke(tasks)                                                 
	                }
	            }
	        }
	        // 返回类实例
	        tasks                                                                       
	    }
	}

	def tasks = Runner.run(Tasks)
	assert tasks.result == [1, 'JDK 6'] as Set

### 1.7.6 元注解(`meta-annotations`)

**元注解是Groovy的功能，不适用于Java。并且元注解的定义和使用都必须使用Groovy去编写**

**元注解的声明**
---

元注解，也称为注解别名，是在编译时被其他注解替换的注解(一个元注解是一个或多个注解的别名)。元注解可用于减少涉及多个注解的代码的大小。

例如，现在有俩个注解`@Service `和` @Transactional`想同时用在一个方法上，传统做法是:

	@Service
	@Transactional
	class MyTransactionalService {}

- 但是如果存在更多的注解，那么使用起来会十分麻烦，需要在每次都添加所有的注解。为了解决这个，**Groovy提供了元注解，元注解可以将多个注解组合成一个新的注解**

- **声明元注解与声明普通的注解相似，Groovy提供了`@AnnotationCollector`注解用于将需要的注解组合起来**

上面的例子可以写成如下形式:

	import groovy.transform.AnnotationCollector
	
	@Service                                        
	@Transactional                                  
	@AnnotationCollector                            
	@interface TransactionalService {
	}

	@TransactionalService                           
	class MyTransactionalService {}


**元注解的行为**
---

**Groovy同时支持预编译和源码形式的注解**

**当Groovy解析元注解时，会在"语义分析编译阶段"用收集到的注解去替换元注解**,例如上面的例子，当遇到`@TransactionalService `元注解时，会将其替换成`@Service`和`@Transactional`

	def annotations = MyTransactionalService.annotations*.annotationType()
	assert (Service in annotations)
	assert (Transactional in annotations)


**元注解的参数**
---
**元注解可以接收其所代表的注解的参数，甚至可以覆盖掉注解本身的参数**

	@Timeout(after=3600)
	@Dangerous(type='explosive')
	@AnnotationCollector
	public @interface Explosive {}
	
	@Explosive(after=0)                 
	class Bomb {}

- `after=0`会覆盖掉`after=3600`

**如果两个注解定义相同的参数名称，则处理器默认会将参数值传递给所有接受此名称参数的注解**

	@Retention(RetentionPolicy.RUNTIME)
	public @interface Foo {
	   String value()                                   
	}
	@Retention(RetentionPolicy.RUNTIME)
	public @interface Bar {
	    String value()                                  
	}
	
	@Foo
	@Bar
	@AnnotationCollector
	public @interface FooBar {}                         
	
	@Foo('a')
	@Bar('b')
	class Bob {}                                        
	
	assert Bob.getAnnotation(Foo).value() == 'a'        
	println Bob.getAnnotation(Bar).value() == 'b'       
	
	@FooBar('a')
	class Joe {}                                        
	assert Joe.getAnnotation(Foo).value() == 'a'        
	println Joe.getAnnotation(Bar).value() == 'a'  

- 如果俩个具有相同名称参数的注解的类型不同，那么可能导致编译错误，例如上述的`@Foo`注解的`value()`方法返回`String`,而`@Bar`注解的`value()`方法返回`Integer`

**处理重复的注解**
---
`@AnnotationCollector`注解提供了一个模式参数，该参数可用于更改处理器在出现重复注解时的处理方式

	TODO

**自定义注解处理器**
---

	TODO

## 1.8 继承
	TODO
## 1.9 泛型
	TODO
# 2. 特质(`Trait`)

特质是语言的结构构造，它允许：

- 组合行为

- 接口的运行时实现

- 行为的覆盖

- 静态类型检查和编译的兼容性

**Groovy提供了关键字`trait`来定义特质，特质可以当做拥有默认实现和状态的接口**

	trait FlyingAbility {
		// 声明特征内的方法                           
	    String fly() { "I'm flying!" }          
	}

- 定义完特质之后，就可以像接口一样用`implements`使用
		
		// 添加特征到Bird类
		class Bird implements FlyingAbility {}          
		def b = new Bird()
		// Bird类实例就拥有该特征的能力了                              
		assert b.fly() == "I'm flying!"    


## 2.1 方法
介绍特质中拥有不同修饰符的方法
### 2.1.1 公有方法
**在特质中声明的公有方法可以像类中的任何常规方法一样去调用**

	trait FlyingAbility {                           
	        String fly() { "I'm flying!" }          
	}

### 2.1.2 抽象方法

**特质可以声明抽象方法，因此实现特质的类需要提供特质的抽象方法的具体实现**

	trait Greetable {
	    abstract String name()                              
	    String greeting() { "Hello, ${name()}!" }           
	}

	class Person implements Greetable {
		// 必须实现该抽象方法                     
	    String name() { 'Bob' }                             
	}
	
	def p = new Person()
	assert p.greeting() == 'Hello, Bob!'  

### 2.1.3 私有方法

**特质可以是声明私有方法,私有方法不会出现在实现特质的类中,只能在特质自身中被调用**


	trait Greeter {
		// 特质中的私有方法
	    private String greetingMessage() {                      
	        'Hello from a private method!'
	    }
	    String greet() {
	        def m = greetingMessage()                           
	        println m
	        m
	    }
	}
	
	class GreetingMachine implements Greeter {}                 
	def g = new GreetingMachine()
	assert g.greet() == "Hello from a private method!"          
	try {
		// 无法调用特质的私有方法
	    assert g.greetingMessage()                              
	} catch (MissingMethodException e) {
	    println "greetingMessage is private in trait"
	}
	
### 2.1.4 其他方法

特质仅支持被`public`和`private`修饰的方法，`protected`和包作用域的方法不支持

### 2.1.5 final方法

如果一个类实现了一个特质，那么概念上来讲类就"继承"了一个特质，实际上Groovy是直接将方法添加到类中.

**特质中的方法上的`final`修饰符只是用来表示被添加的方法的修饰符,在类实现特质的过程没有特殊的含义.即Groovy允许继承,覆盖,多重继承特质中的方法，无论是否带有`final`修饰符!**

**如果希望获得获得一个无法被重写的方法，那么可以使用一个类去实现该特质,并且该类作为基类提供给其他类继承**


继承特质，重写特质中的`final`方法:

	trait A{
	    final void sayH(){
	        println "hhhhhhhhhhh"
	    }
	    
	    void sayA(){
	        println "aaaaaaaa"
	    }
	    
	}
	
	class B implements A{
	
	    void sayA(){
	        println "override sayA"
	    }
	    
	    void sayH(){
	        println "override sayH"
	    }
	
	}
	
	def b = new B()
	b.sayH()
	b.sayA()

继承类，重写父类中的`final`方法：

	class D {
	
	    final void sayHi(){
	        println "hjhhh"
	    }
	}
	
	class E extends D{
	    void sayHi(){
	        println "ddddd"
	    }
	}
	
	def e = new E()
	e.sayHi()

	执行失败，并抛出
	You are not allowed to override the final method sayHi() from class 'D'.

- 这里将抛出异常	

## 2.2 this的含义

特质中的`this`代表实现特质的类的实例,可以将特质看做一个父类

	trait Introspector {
	    def whoAmI() { this }
	}
	class Foo implements Introspector {}
	def foo = new Foo()

	foo.whoAmI()
	assert foo.whoAmI().is(foo)


## 2.3 接口

特质可以实现接口,并且可以不提供具体实现(但是实现特质的类需要实现)，通过关键字`implements`实现

	interface Named {                                       
	    String name()
	}
	trait Greetable implements Named {                      
	    String greeting() { "Hello, ${name()}!" }
	}
	class Person implements Greetable {                     
	    String name() { 'Bob' }                             
	}
	
	def p = new Person()
	assert p.greeting() == 'Hello, Bob!'                    
	assert p instanceof Named                               
	assert p instanceof Greetable   


## 2.4 属性

特质可以定义属性,类继承特质后就可以通过属性访问器来进行访问
	
	trait Named {
	    String name                             
	}
	class Person implements Named {}            
	def p = new Person(name: 'Bob')             
	assert p.name == 'Bob'                      
	assert p.getName() == 'Bob'   
	
## 2.5 字段
### 2.5.1 私有字段
**特质允许使用私有字段,私有方法不会出现在实现特质的类中,只能在特质自身中被调用**

	trait Counter {
	    private int count = 0                   
	    int count() { count += 1; count }       
	}
	class Foo implements Counter {}             
	def f = new Foo()
	assert f.count() == 1                       
	assert f.count() == 2

### 2.5.2 公有字段
**特质的公有字段可以像类中的任何公有字段一样被使用,但是为了避免[Diamond problem](https://stackoverflow.com/questions/2064880/diamond-problem),字段名称会在具体实现类中被重新映射**

	trait Named {
	    public String name                      
	}
	class Person implements Named {}            
	def p = new Person()                        
	p.Named__name = 'Bob'  
	
**实现了特质的类中，来自特质的字段,其名称取决于特质的限定名称**

- 特质的限定名称中的点(`.`)需要替换成下划线(`_`),此外在公有字段前还需要加俩个下划线

	例如一个`my.package`包下有一个名称为`Foo`的特质，其拥有一个`String`类型的私有字段`bar`.那么在具体实现特质的类中就需要按照如下方式调用:

		String my_package_Foo__bar


**不建议在特质中添加公有字段**
## 2.6 行为的组合
**特质可用于实现多重继承**

	trait FlyingAbility {                           
		String fly() { "I'm flying!" }          
	}
	trait SpeakingAbility {
	    String speak() { "I'm speaking!" }
	}

	class Duck implements FlyingAbility, SpeakingAbility {} 
	
	def d = new Duck()                                      
	assert d.fly() == "I'm flying!"                         
	assert d.speak() == "I'm speaking!"  

- 特质允许对象之间重用功能，并通过组合现有的行为创建新类

## 2.7 覆盖默认方法
特质为方法提供了默认的实现，但是可以在实现类中覆盖它们。例如，稍微改变上面的例子，让一只鸭子嘎嘎叫

	class Duck implements FlyingAbility, SpeakingAbility {
	    String quack() { "Quack!" }                         
	    String speak() { quack() }                          
	}
	
	def d = new Duck()
	assert d.fly() == "I'm flying!"                         
	assert d.quack() == "Quack!"                            
	assert d.speak() == "Quack!"     

## 2.8 扩展特质
### 2.8.1  单继承(Simple inheritance)
特质可以通过关键字`extends `实现特质之间的继承

	trait Named {
	    String name                                     
	}
	trait Polite extends Named {                        
	    String introduce() { "Hello, I am $name" }      
	}
	class Person implements Polite {}
	def p = new Person(name: 'Alice')                   
	assert p.introduce() == 'Hello, I am Alice'  

### 2.8.2 多重继承(Multiple inheritance)
特质支持多继承,所有的父类特质都在`implements`从句中


	trait WithId {                                      
	    Long id
	}
	trait WithName {                                    
	    String name
	}
	trait Identified implements WithId, WithName {} 

## 2.9 Duck typing and traits
### 2.9.1 Dynamic code
特质支持调用任何动态代码，就像普通的Groovy代码一样. 也就是说，可以在方法体中调用应该存在于实现类中的方法，而无需在接口中显式声明它们。 这意味着特质与`duck typing`完全兼容

	trait SpeakingDuck {
	    String speak() { quack() }                      
	}
	class Duck implements SpeakingDuck {
	    String methodMissing(String name, args) {
	        "${name.capitalize()}!"                     
	    }
	}
	def d = new Duck()
	assert d.speak() == 'Quack!'   

- `quack()`方法并没有在特质中被定义,也没有在其实现类中实现，那么就会调用`methodMissing(String name,args)`方法

### 2.9.2 Dynamic methods in a trait

特质可以实现`MOP`方法，例如`methodMissing`或`propertyMissing`，在这种情况下，实现类将继承特质的行为，如下例所示：

	trait DynamicObject {                               
	    private Map props = [:]
	    def methodMissing(String name, args) {
	        name.toUpperCase()
	    }
	    def propertyMissing(String prop) {
	        props[prop]
	    }
	    void setProperty(String prop, Object value) {
	        props[prop] = value
	    }
	}
	
	class Dynamic implements DynamicObject {
	    String existingProperty = 'ok'                  
	    String existingMethod() { 'ok' }                
	}
	def d = new Dynamic()
	assert d.existingProperty == 'ok'                   
	assert d.foo == null                                
	d.foo = 'bar'                                       
	assert d.foo == 'bar'                               
	assert d.existingMethod() == 'ok'                   
	assert d.someMethod() == 'SOMEMETHOD'  

- 获取实现类中不存在的属性时，会调用特质中的`propertyMissing()`方法

- 对实现类中不存在的属性设置值时，会调用特质中的`setProperty ()`方法

- 调用实现类中不存在的方法时，会调用特质中的`methodMissing()`方法


## 2.10 多重继承冲突
### 2.10.1 默认的冲突解决方案

**类可以实现多个特质，如果多个特质之间存在某个签名相同的方法，那么就会导致冲突**

	trait A {
	    String exec() { 'A' }               
	}
	trait B {
	    String exec() { 'B' }               
	}
	class C implements A,B {}    
	
	def c = new C()
	assert c.exec() == 'B'
	
- **默认情况下，如果后定义的特质会覆盖前面的特质中的具有相同签名的方法**

### 2.10.2 自定义的冲突解决方案

**冲突时除了使用默认的解决方案外，还可以通过使用`Trait.super.methodName`语法可以选择调用指定特质的方法**

	class C implements A,B {
	    String exec() { A.super.exec() }    
	}
	def c = new C()
	assert c.exec() == 'A'    

## 2.11 特质的运行时实现
### 2.11.1 在运行时实现特质
**Groovy支持在运行时动态实现特性,这允许在对已经存在对象进行装饰**. **Groovy提供了`as`关键字用来在运行时实现特质**

	trait Extra {
	    String extra() { "I'm an extra method" }            
	}
	class Something {                                       
	    String doSomething() { 'Something' }                
	}
	// 在运行时为类Something 实现特质Extra
	def s = new Something() as Extra                        
	s.extra()                                               
	s.doSomething()    

- **使用关键字`as`实现了特质的对象前后不是同一个**
	
### 2.11.2 在运行时一次性实现多个特质

**Groovy提供了`withTraits()`方法用来在运行时一次性实现多个特质**

	trait A { void methodFromA() {} }
	trait B { void methodFromB() {} }
	
	class C {}
	
	def c = new C()
	try{
	    c.methodFromA()                     
	    c.methodFromB()   
	}catch(e){
	    println "$e"
	}                  
	def d = c.withTraits A, B           
	d.methodFromA()                     
	d.methodFromB()  
	
- **使用`withTraits()`方法强转前后的对象不是同一个**


## 2.12 行为链(`Chaining behavior`)

**Groovy支持可堆叠特质的概念。即如果当前特质不能处理消息，则将从一个特质委托给另一个特质处理**

### 2.12.1 特质实现类中的`super`

	interface MessageHandler {
	    void on(String message, Map payload)
	}

	trait DefaultHandler implements MessageHandler {
	    void on(String message, Map payload) {
	        println "Received $message with payload $payload"
	    }
	}
	
	class SimpleHandler implements DefaultHandler {
		void on(String message, Map payload){
			println "SimpleHandler"
			println "Received $message with payload $payload"
		}
	}
	
	def handler = new SimpleHandler()
	handler.on("Test",[name:"jack",age:14])

- **默认情况下重写`SimpleHandler `的`on()`方法会覆盖父特质的方法,但是通过`SuperClassName.super.methodnName()`的形式可以调用指定父类的某个方法**

		class SimpleHandlerWithLogging implements DefaultHandler {
		    void on(String message, Map payload) {                                  
		        println "Seeing $message with payload $payload"
		        // 调用父类的方法                     
		        DefaultHandler.super.on(message, payload)                           
		    }
		}

- 但是`SuperClassName.super.methodnName()`这种方式有缺陷:

	1. 被绑定到了一个具体的处理器

	2. 存在对父类方法的显式引用，这意味着如果更改了类对特质的实现，会导致错误

### 2.12.2 行为链中的`super`

**为了解决上述问题，可以通过`super.methodName()`形式将调用委托给行为链中的下一个特质**,**这种形式十分强大，因为它允许编写彼此不认识的处理程序，并且按照任意的顺序组合它们**

	trait LoggingHandler implements MessageHandler {                            
	    void on(String message, Map payload) {
	        println "Seeing $message with payload $payload"                     
	        super.on(message, payload)                                          
	    }
	}
		
	class HandlerWithLogger implements DefaultHandler, LoggingHandler {}
	def loggingHandler = new HandlerWithLogger()
	loggingHandler.on('test logging', [:])
	// 输出结果
	Seeing test logging with payload [:]
	Received test logging with payload [:]	
- **因为在声明特质的语句中，越靠后的特质优先级越高。**因此会先输出`LoggingHandler`，再输出`DefaultHandler`
		

如果这时候再添加一个特质,该特质仅处理特定信息,能够看到行为链的作用:
	
	trait SayHandler implements MessageHandler {
	    void on(String message, Map payload) {
	        if (message.startsWith("say")) {                                    
	            println "I say ${message - 'say'}!"
	        } else {
	            super.on(message, payload)                                      
	        }
	    }
	}

	class Handler implements DefaultHandler, SayHandler, LoggingHandler {}
	def h = new Handler()
	h.on('foo', [:])
	h.on('sayHello', [:])

- 根据优先级，`LoggingHandler`会先进行处理，输出自身的日志后，调用其下一个特质`SayHandler`去处理. 在`SayHandler`中根据对参数的判断，执行不同的逻辑

- 通过更改特质声明顺序，可以改变行为

		class AlternateHandler implements DefaultHandler, LoggingHandler, SayHandler {}
		h = new AlternateHandler()
		h.on('foo', [:])
		h.on('sayHello', [:])
		// 输出内容
		Seeing foo with payload [:]
		Received foo with payload [:]
		I say Hello!

### 2.12.1 `super`在特质中的语义

如果一个类实现了若干特质，并且每个特质中都使用了未限定的`super`调用

1. **如果在行为链中，拥有下一个特质，那么就会调用下一个特质的方法**

2. **如果在行为链中，当前特质已经是最后一个，那么这时`super`就代表`this`**


## 2.13 Advanced features
### 2.13.1 SAM type coercion
### 2.13.2 Differences with Java 8 default methods
## 2.14 Differences with mixins
## 2.15 Static methods, properties and fields
## 2.16 Inheritance of state gotchas
## 2.17 Self types
### 2.17.1 Type constraints on traits
### 2.17.2 The @SelfType annotation
## 2.18 Limitations
### 2.18.1 Compatibility with AST transformations
### 2.18.2 Prefix and postfix operations